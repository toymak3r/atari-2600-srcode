;*********************************************************************
;
; SET UP POSITIONS AND KERNEL TEMPS
;
;*********************************************************************
;
;
VB0:
VB1:
;
VBATRC:
	LDA	GAME
	BPL	VBCOMN
	AND	#20
	BEQ	5$		;NOT 4 SECOND DELAY
;DELAY STILL ON
	LDA	SOUND
	BNE	20$		;DELAY
	LDA	GAME
	AND	#0DF		;DELAY BIT OFF
	STA	GAME
;
5$:
	BIT	INPT4
	BMI	10$		;NO BUTTON
	JMP	GAMINI
;
;
10$:
;
	LDA	FRMCTR
	BNE	20$
	LDA	FRMCTR+1
	AND	#7
	BNE	20$
15$:
	JSR	INIT0		;NEW SCREEN
	LDA	SCREEN
	EOR	#10
	STA	SCREEN
	JSR	INITW		;WAVE INIT
;
20$:
;
VBCOMN:
;CHECK GAME SWITCHES
	LDA	SWCHB
	AND	#1		;GAME RESET
	BNE	10$
;RESET PUSHED IS IT STILL DOWN
	LDA	BLSTAT
	AND	#20
	BNE	20$		;PUSHED LAST FRAME
;GAME RESET HERE
	LDA	BLSTAT
	ORA	#20
	STA	BLSTAT
	JMP	GAMINI
;
10$:
	LDA	BLSTAT
	AND	#0DF		;BUTTON NOT PUSHED
	STA	BLSTAT
;
20$:
;BUTTON NOT PUSHED OR STILL HELD DOWN
	JMP	VBCOM1
GAMINI:
	LDA	#0
	STA	FRMCTR+1
	STA	SCORE
	STA	SCORE+1
	STA	SCORE+2
	STA	JMPNDX		;ON GROUND
	STA	SOUND
	STA	SOUND+1
	JSR	INIT0
	LDA	GAME
	AND	#7
	STA	GAME
	LDA	#5
	STA	BNTLIV
	JSR	INITW
;DO SOUND
	LDY	#4
	LDX	#0
	JSR	SNDST1
	LDY	#5
	LDX	#1
	JSR	SNDST1
VBCOM1:
	LDA	SWCHB
	AND	#2
	BEQ	10$		;SELECT PUSHED
;SELECT NOT PUSHED
	LDA	BLSTAT
	AND	#0EF
	STA	BLSTAT		;NO SELECT
	JMP	99$
;
10$:
;SELECT HELD DOWN, WAS IT STILL DOWN?
	LDA	FRMCTR
	AND	#0F
	STA	TEM0
	LDA	BLSTAT
	AND	#10
	BEQ	20$		;FRESHLY PUSHED
;BUTTON STILL DEPRESSED, IS IT TIME?
	LDA	BLSTAT
	AND	#0F
	CMP	TEM0
	BNE	99$		;NOT TIME TO CYCLE
	BEQ	30$		;JMP
20$:
;BUTTON FRESHLY PUSHED HERE
	LDA	BLSTAT
	AND	#0F0
	ORA	#10
	ORA	TEM0		;TIME
	STA	BLSTAT
;
30$:
	LDA	GAME
	BIT	GAME
	BVC	50$		;NOT SELECT MODE
;CYCLE
	LDA	#0
	STA	SCORE
	STA	SCORE+1
	STA	SCORE+2
	LDA	GAME
	CLC
	ADC	#1
	AND	#7
50$:
;SET SELECT MODE
	AND	#0C7
	ORA	#0D0		;DELAY OFF, SELECT ON
	STA	GAME
	JSR	INIT0
	JSR	INITW
	LDA	#0
	STA	FRMCTR+1
	STA	SOUND
	STA	SOUND+1
;
99$:
;
VBSEL:
	LDA	SCREEN
	AND	#10
	BEQ	99$
	JMP	VBSC1
99$:
;
;
;*********************************************************************
;
;PLAYFIELD
;
;*********************************************************************
;
INIPF:
	LDX	MAZE
	LDA	PF1PTL(X)
	STA	PF1PTR
	LDA	PF1PTH(X)
	STA	PF1PTR+1
	LDA	PF2PTL(X)
	STA	PF2PTR
	LDA	PF2PTH(X)
	STA	PF2PTR+1
	LDA	#0
	STA	COLUBK
	LDA	WAVE
	AND	#1F
	TAY
	LDA	CLRPF(Y)
	STA	COLUPF
	LDA	#21
	STA	CTRLPF
	STA	VDELP1
;
	LDA	#2
	DEC	CNT3
	BPL	5$
	STA	CNT3
5$:
	LDA	#4
	DEC	CNT5
	BPL	7$
	STA	CNT5
7$:
	LDA	#6
	DEC	CNT7
	BPL	9$
	STA	CNT7
9$:
;
WV7:
;OBJECTS ASCENDING
	LDA	WAVST
	CMP	#7
	BCC	99$
	CMP	#9
	BCS	99$
;WAVE 7 OR 8(YOU OR THEY GOT IT)
	LDX	#P1NUM
	STX	TEM0
5$:
	LDA	INCTBL(X)
	BEQ	10$		;NOBODY HOME
	LDA	GRFX(X)
	CMP	#NXHATW
	BEQ	10$		;DONT MOVE WORN HAT
	LDA	FRMCTR
	LSR
	LDA	#1
	ADC	YPOS(X)
	STA	YPOS(X)
	LDY	GRFX(X)
	LDA	#TOPLIN+1
	SEC
	SBC	P1HGT1(Y)
	CMP	YPOS(X)
	BCS	15$		;STILL ON SCREEN
	LDA	#0
	STA	INCTBL(X)
10$:
	DEC	TEM0
15$:
	DEX
	BNE	5$
;
	LDA	TEM0
	BNE	99$
;ALL GONE
;DELAY UNTIL SOUND
	LDA	SNDNDX
	CMP	#10
	BCS	99$		;WAIT
	LDA	#0
	STA	INCTBL+P1NUM-1	;ZERO WORN HAT
	LDA	#0E0
	STA	YPOS+P1NUM-1
	LDA	WAVST
	CMP	#7
	BEQ	50$		;YOU GOT IT
	LDY	#SNDTHY
	LDX	#0
	JSR	SNDST1
	LDY	#SNDTHY+1
	LDX	#1
	JSR	SNDST1
	JMP	60$
50$:
	LDY	#SNDYOU
	LDX	#0
	JSR	SNDST1
	LDY	#SNDYOU+1
	LDX	#1
	JSR	SNDST1
60$:
	LDA	#9
	STA	WAVST		;NOW WAIT FOR THIS SOUND TO BE OVER
;
99$:
;
WAV9:
	LDA	WAVST
	CMP	#9
	BNE	99$		;NOT WAITING FOR YOU/THEY TO END
	LDA	SOUND
	BNE	99$		;STILL GOING
	STA	SOUND+1		;ZERO IN CASE STILL GOING
;NOW TIME FOR NEXT MAZE
	INC	WAVE
	LDA	WAVE
	AND	#3F
	STA	WAVE		;KEEP IT LOW
	TAY
	LDX	MAZWAV(Y)
	STX	MAZE
	AND	#1
	BNE	30$		;DONT SPEED UP
	LDA	GSPEED
	AND	#1F
	BEQ	30$
	SEC
	SBC	#1
	STA	GSPEED
30$:
;
	JSR	INITW
;
;
	LDA	WAVE
	AND	#3
	BNE	40$		;NOT NEW LEVEL
	LDY	#SNDNLV
	LDX	#0
	JSR	SNDST1
	LDY	#SNDNLV+1
	LDX	#1
	JSR	SNDST1
;
	JMP	99$
;
40$:
	LDY	#SNDNMZ
	LDX	#0
	JSR	SNDST1
	LDY	#SNDNMZ+1
	LDX	#1
	JSR	SNDST1
;
99$:
;
WV0:
	LDA	WAVST
	BNE	WV1
	LDA	FRMCTR
	AND	#1
	BNE	50$
	LDY	ENDLIN
	BEQ	10$
	DEC	ENDLIN
	DEC	ENDLIN
	JMP	50$
10$:
	DEC	WAVTMR
	BNE	99$
	LDX	#61.
	STX	WAVTMR
	INC	WAVST
	LDX	MAZE
	LDA	DOTNUM(X)
	STA	DOTCNT
	LDA	#0
	STA	DOTCNT+1
;
50$:
	LDY	ENDLIN		;3C TO 0
	LDA	#0
	STA	DOTW0+1(Y)
	STA	DOTW1+1(Y)
	STA	DOTW2+1(Y)
	STA	DOTW3+1(Y)
;
99$:
	JMP	INIP0
;
;*********************************************************************
;
;
WV1:
;DRAW DOTS
	LDA	WAVST
	CMP	#1
	BNE	99$
	LDX	MAZE
	LDA	DT1PTH(X)
	STA	TEM0+1
	LDA	DT1PTL(X)
	STA	TEM0
	LDA	DT2PTL(X)
	STA	TEM0+2
	LDA	DT2PTH(X)
	STA	TEM0+3
	LDA	WAVTMR
	TAX
	LSR
	TAY			;INDEX INTO DOT TABLES
	BCC	10$		;USE PF2
	LDA	@TEM0(Y)
	STA	DOTW0(X)
	STA	DOTW1(X)
	JMP	20$
10$:
	LDA	@TEM0+2(Y)
	STA	DOTW2+1(X)
	STA	DOTW3+1(X)
20$:
	DEC	WAVTMR
	BMI	1$
	JMP	ANIP0
1$:
	JSR	WVRINI
	LDA	#2
	STA	WAVST
	LDA	#40		;MUST BE EVEN
	STA	WAVTMR
;
99$:
;
;
WV5:
;MOVE BENTLEY UP
	LDA	WAVST
	CMP	#5
	BNE	99$
	LDA	YPOS
	ADC	#3		;ADD 4
	STA	YPOS
	CMP	#P0MAXL+1
	BCC	99$
	LDA	#6
	STA	WAVST
	LDA	#TIMDI5
	STA	WAVTMR
	
;
99$:
;
;
WV6:
;DELAY AFTER BENTLEY ASCENSION BEFORE OBJECTS DISAPPEAR
	LDA	WAVST
	CMP	#6
	BNE	99$
	DEC	WAVTMR
	BNE	99$
;NOW IS GAME OVER?
	LDA	BNTLIV
	BEQ	1$
	DEC	BNTLIV
	BNE	1$		;NOT OVER YET
;GAME OVER
	LDY	#1
	LDX	#0
	JSR	SNDST1
	LDY	#2
	LDX	#1
	JSR	SNDST1
;
	LDA	GAME
	AND	#7		;GAME OVER
	ORA	#0B0		;SCREEN 1
	STA	GAME
	LDA	#0
	STA	FRMCTR
	STA	FRMCTR+1
	JMP	VBSC1
1$:
;DESCEND
	LDX	#P1NUM-1
5$:
	LDY	#0E0		;TO REMOVE IT
	LDA	INCTBL(X)
	BEQ	7$		;OFF SCREEN(YPOS MUST BE -)
	LDA	GRFX(X)
	CMP	#NXSCR1
	BCS	7$		;REMOVE IT, SCORE
	LDY	#70
;
7$:
	STY	YPOS(X)
	LDA	#0
	STA	INCTBL(X)
10$:
	DEX
	BNE	5$
;
	JSR	INITP0
	JSR	WVRINI
	LDA	#2
	STA	WAVST
	LDA	#40
	STA	WAVTMR
;
99$:
;
WV2:
;OBJECTS DESCEND
	LDA	WAVST
	CMP	#2
	BNE	99$
	LDY	#0FF		;DROP 1
	DEC	WAVTMR
	BEQ	1$		;ONLY 1 MORE TO GO
	LDA	CNT3
	BNE	1$		;DROP 1
	DEC	WAVTMR		;DROP 2
	DEY
1$:
	STY	TEM0
	LDX	#P1NUM-1
5$:
	LDA	#0
	LDY	YPOS(X)
	BMI	8$		;OFF
	LDA	YPOS(X)
	CLC
	ADC	TEM0		;SUBTR 1 OR 2
	STA	YPOS(X)
	LDA	INCTBL(X)
	BNE	10$		;ALREADY ON
	LDY	GRFX(X)
	LDA	#TOPLIN+1
	SEC
	SBC	P1HGT1(Y)
	CMP	YPOS(X)
	BCC	10$
	LDA	#80
8$:
	STA	INCTBL(X)
10$:
	DEX
	BNE	5$
;
	STX	INCTBL+P1NUM
	STX	AI2+P1NUM	;HONEY POT BIT
	STX	BEECNT		;#TIMES THEY DROPPED
	LDX	#0FB		;THIS MAKES THE HAT
	STX	HATNDX		;START ON GROUND
	LDX	#0E0
	STX	YPOS+P1NUM
;
	LDA	WAVTMR
	BNE	99$
;DOWN TO EARTH
	LDA	#3
	STA	WAVST
;
99$:
;
;
;*********************************************************************
;
; PLAYER 1
;
;*********************************************************************
;
;
;ANIMATE
ANIP1:
	LDA	WAVST
	CMP	#3
	BCC	99$
	CMP	#6
	BCS	99$
	LDX	#P1NUM
;
1$:
	LDY	GRFX(X)
	LDA	P1GRAT(Y)
	AND	FRMCTR
	BNE	10$
	LDA	P1GSEQ(Y)
	STA	GRFX(X)
;
10$:
	DEX
	BNE	1$		;MORE
;
99$:
;
;
;*********************************************************************
;
;HAT STUFF
;
;*********************************************************************
;
;
HATP1:
	LDA	WAVST
	CMP	#7
	BEQ	1$
	CMP	#8
	BEQ	1$		;KEEP IT ON HIS HEAD
	CMP	#3
	BNE	99$
1$:
	LDA	STATE
	LSR
	BCC	50$		;NOT WORN HAT
	LDA	XPOS
	STA	XPOS+P1NUM-1
	LDA	YPOS
	CLC
	LDX	JMPNDX
	ADC	AIR1(X)
	CMP	#P0MAXL+1
	BCC	9$
	LDA	#P0MAXL
9$:
	ADC	#HEDHGT*2
	LSR
	STA	YPOS+P1NUM-1
;
	JMP	99$
;
50$:
;
;NOT WORN
;MAKE IT BOUNCE ON LATER WAVES
	LDA	WAVST
	CMP	#3
	BNE	99$		;NOT NORMAL
	LDA	GSPEED
	AND	#1F
	CMP	#HTBNCE		;SPEED BELOW WHICH TO BOUNCE HAT
	BCS	99$		;DONT BOUNCE
	LDA	GRFX+P1NUM-1	;IS IT A HAT?
	CMP	#NXHAT
	BNE	99$		;NOT HAT
	DEC	HATNDX
	LDA	HATNDX
	AND	#1F
	TAX
	LDA	AIRHAT(X)
	STA	AI1+P1NUM-1
	CLC
	ADC	YPOS+P1NUM-1
	STA	YPOS+P1NUM-1
;
99$:
;
;
;*********************************************************************
;
;FLICKER ALGORITHM HERE
;
;*********************************************************************
;
FLKRP1:
;
;FIND HIGHEST PRIORITY GUY
	LDA	#0
	STA	TMP+0
	STA	TMP+1
	STA	TMP+2
	STA	TMP+3
	STA	TMP+4
	STA	TMP+5
	STA	TMP+6
	STA	TMP+7
	LDX	#P1NUM
	LDA	INCTBL+7
	CMP	INCTBL+6
	BCS	1$		;IM HIGHER
	LDX	#6
	LDA	INCTBL+6
1$:
	CMP	INCTBL+5
	BCS	2$		;IM HIGHER
	LDX	#5
	LDA	INCTBL+5
2$:
	CMP	INCTBL+4
	BCS	4$		;IM HIGHER
	LDX	#4
	LDA	INCTBL+4
4$:
	CMP	INCTBL+3
	BCS	6$		;IM HIGHER
	LDX	#3
	LDA	INCTBL+3
6$:
	CMP	INCTBL+2
	BCS	8$		;IM HIGHER
	LDX	#2
	LDA	INCTBL+2
8$:
	CMP	INCTBL+1
	BCS	10$		;IM HIGHER
	LDX	#1
	LDA	INCTBL+1
10$:
;X HAS INDEX OF MOST IMPORTANT GUY
;A HAS PRIORITY
	TAY
	BEQ	99$		;NOBODY HOME
;PUT HIM UP FIRST
	STX	TMP		;P1TOP
	TXA
	STA	TMP(X)		;INCTBL(X) POINTS TO HIMSELF
	LDY	GRFX(X)
	CLC
	LDA	YPOS(X)
	ADC	P1HGT1(Y)
	ADC	#2
	CMP	#3F
	BCC	15$		;OKAY
	ASL			;HEAD IS HIGHER THAN A KITE
15$:
	STA	TMP+P1NUM+1(X)	;HEAD
	LDA	#0
	STA	INCTBL(X)	;DONT REPEAT
;ASSIGN OFF GUYS NEXT
;COULD WRITE THIS OUT
	LDX	#P1NUM
	STX	TEM0+2
20$:
	LDX	TEM0+2
	LDA	INCTBL(X)
	BPL	25$
	JSR	INSERT
	LDA	#0
	STA	INCTBL(Y)	;DONT REPEAT
25$:
	DEC	TEM0+2
	BNE	20$
;ON GUYS
	LDX	#P1NUM
	STX	TEM0+2
30$:
	LDX	TEM0+2
	LDA	INCTBL(X)
	BEQ	35$		;NOBODY HOME
	JSR	INSERT
35$:
	DEC	TEM0+2
	BNE	30$
;
;NOW FIX LIST
	LDX	TMP
	STX	P1TOP
	LDX	TMP+1
	STX	INCTBL+1
	LDX	TMP+2
	STX	INCTBL+2
	LDX	TMP+3
	STX	INCTBL+3
	LDX	TMP+4
	STX	INCTBL+4
	LDX	TMP+5
	STX	INCTBL+5
	LDX	TMP+6
	STX	INCTBL+6
	LDX	TMP+7
	STX	INCTBL+7
;
99$:
;
;
;*********************************************************************
;
;ANIMATE P0
ANIP0:
	LDA	WAVST
	CMP	#1
	BCC	99$
	CMP	#4
	BCS	99$
1$:
	LDA	SPEED
	BEQ	10$		;NOT MOVING
	AND	#8
	STA	TEM0
	LDA	REFLCT
	AND	#0F7
	ORA	TEM0
	STA	REFLCT
;
10$:
	LDA	REFLCT
	STA	REFP0		;SET REFLECT
	LDA	SPEED
	BEQ	99$		;NOT MOVING
	AND	#4		;FRONT/BACK
	STA	TEM0
	LDA	GRFX
	AND	#0FB
	ORA	TEM0		;ORIENT HIM
	STA	GRFX
;
	LDA	FRMCTR		;TIME TO ANIMATE?
	AND	#01
	BNE	99$		;NOT YET
	LDA	CNT3
	BNE	99$		;NOT YET
	LDA	STATE
	AND	#0EF		;TURN OFF MOTION BIT
	CMP	STATE
	STA	STATE
	BEQ	20$
	LDX	GRFX
	LDA	P0GSEQ(X)	;NEXT GRAPHICS
	STA	GRFX
20$:
;
99$:
;
;
;*********************************************************************
;
;
INIP0:
	LDA	#20
	STA	NUSIZ0
	LDA	#CLRP0
	STA	COLUP0
;CALCULATE INFO FOR P0
;BY ROTATING THROUGH THE PAGES OF DATA
;FIRST GET HIS YPOS WITH JUMP INCLUDED
	LDA	#1
	LDX	WAVST
	CPX	#1
	BCC	6$
	CPX	#6
	BEQ	6$
	LDA	YPOS
	CLC
	LDX	JMPNDX
	ADC	AIR1(X)
	SEC			;MAKE HIS POSITION SUCH THAT
	SBC	#1		;YPOS=2,3 ARE ON LINE Y=1
	CMP	#P0MAXL
	BCC	5$
	LDA	#P0MAXL
5$:
	STA	TEM0+2
	AND	#3
	STA	TEM0+1
	LDX	#6
1$:	TXA
	ASL
	ADC	TEM0+1
	TAY
	LDA	#P00D0&0FF00/100
	STA	P00PTR+1(X)	;MSB POINTERS(PAGES)
;
	LDA	P00TB(Y)
	LDY	GRFX
	ADC	P0PTL(Y)
	STA	P00PTR(X)
;
	DEX
	DEX
	BPL	1$		;DO 4 POINTERS
;
;
	LDA	TEM0+2
	LSR
	LSR
	ASL
	ADC	#P0HGT/2+4	;(C=0) Y/4*2+2+2 . CALCULATE YSTRT
6$:
	STA	P0STRT
	LDX	#P0INAC&0FF	;FOR P0 LOWER THAN TOP LINE
	LDA	#TOPLIN&0FE+2	;MAKE IT EVEN
	CMP	P0STRT		;IS IT AT TOP OF SCREEN
	BNE	3$		;BELOW TOP
;AT TOP HERE
2$:
	LDX	#P0ACT&0FF	;START HIM ON FIRST LINE
	LDA	#P0HGT/4
	STA	P0LINE
3$:
	STX	EVNLIN
;
;
;
INIP1:
	LDX	P1TOP		;TOP INCARNATION
	BNE	1$
	INX
1$:
	STX	P1NDX		;FIRST P1 GUY
	LDA	#TOPLIN+1
	SEC
	SBC	YPOS(X)		;FOOT YPOS
	STA	P1TMP		;FOR P1START
	LDX	#0
	STX	P02
	STX	P10
	LDX	#P11D00&0FF00/100
	STX	P11PTR+1
	INX
	STX	P10PTR+1
	INX
	STX	P1CPTR+1
;
	LDA	#0
	STA	NUSIZ1
	STA	VDELP0
	LDA	#P0INAC&0FF00/100
	STA	EVNLIN+1
	LDA	#P11WAT&0FF
	STA	ODDLIN
	LDA	#P11WAT&0FF00/100
	STA	ODDLIN+1
;
;
;*********************************************************************
;
;CHECK DIFFICULTY SWITCH
;
	LDA	GSPEED
	AND	#1F
	BIT	SWCHB
	BVC	10$		;EASY
	ORA	#20
10$:
	STA	GSPEED
;
;
;*********************************************************************
;
;NOW DO RESETS
;
	LDX	#RESET0&0FF
	STX	JMP
	LDX	#RESET0&0FF00/100
	STX	JMP+1
	JMP	SWBN10
;
;
;
;*********************************************************************
;
;TABLE OF P0 OFFSETS
P00TB:
	.BYTE	1,69,46,23,24,1,69,46
	.BYTE	47,24,1,69,6A,47,24,1
;
;*********************************************************************
;
;INSERTION
;
;*********************************************************************
;
INSERT:
;DONT DO THIS FOR FIRST GUY
;X HAS INDEX
	LDY	GRFX(X)
	CLC
	LDA	YPOS(X)
	STA	TEM0+1
	ADC	P1HGT1(Y)
	ADC	#2		;#LINES
	CMP	#3F		;IN AIR?
	BCC	10$		;OKAY
	ASL			;HIGH AS A KITE
10$:
	STX	TEM0+2		;MY INDEX
	STA	TMP+P1NUM+1(X)	;HEAD LIST
	LDY	#0		;PREVIOUS GUY
	LDX	TMP		;P1TOP
	CMP	YPOS(X)		;HIS FEET
	BCS	30$		;MY HEAD IS HIGHER
;IM LOWER
20$:
	STX	TEM0
	LDY	TEM0		;TXY
	LDX	TMP(Y)		;NEXT
	CPX	TEM0		;POINTS TO HIMSELF?
	BEQ	40$		;LAST GUY
;SEE IF IM LOWER
	CMP	YPOS(X)		;FEET
	BCC	20$		;MY HEAD IS LOWER
;MY HEAD IS ABOVE HIS FEET, MUST BE ABOVE HIS HEAD
30$:
	LDA	TMP+P1NUM+1(X)	;HIS HEAD
	CMP	TEM0+1		;MY FEET
	BCS	50$		;CONFLICT
;Y HAS PREVIOUS, X HAS NEXT
	LDA	TEM0+2		;MY INDEX
	STA	TMP(Y)		;IM AFTER HIM
	TAY
	STX	TMP(Y)		;HES AFTER ME
;
	RTS
;
40$:
;Y HAS PREVIOUS, IM LAST GUY
	LDA	TEM0+2		;MY INDEX
	STA	TMP(Y)		;IM AFTER HIM
	TAY
	STA	TMP(Y)		;IM AFTER ME TOO
;
	RTS
;
;
50$:
;CONFLICT
	LDY	TEM0+2		;MY INDEX
	LDX	INCTBL(Y)
	BPL	55$
	INX			;ONE MORE FRAME
	STX	TMP(Y)		;INCTBL
;
	RTS
;
55$:
	LDX	#80
	STX	TMP(Y)
;
	RTS
;
;
;*********************************************************************
;
; TITLE SCREEN
;
;*********************************************************************
;
VBSC1:
;SET UP TITLE SCREEN
;
	STA	WSYNC
	LDX	#3
	STX	NUSIZ0
	STX	NUSIZ1
	STX	VDELP0
	STX	VDELP1
	STX	REFP0
	STX	REFP1
	LDX	#10
	STX	HMP1		;(25)
	LDX	#VBTIME&0FF
	STX	JMP
	LDX	#VBTIME&0FF00/100
	STX	JMP+1
	NOP
	STA	RESP0		;(40)
	STA	RESP1		;(41)
	STA	WSYNC
	STA	HMOVE
	STA	WSYNC
	STA	HMCLR
;
;
VBTIME:	LDA	INTIM
	BNE	VBTIME
;
;*****************************************************************
;
; KERNEL LOGIC
;
;*****************************************************************
;
	STA	CXCLR		;CLEAR COLLISIONS
;
	STA	HMCLR
	LDA	SCREEN
	LSR
	LSR
	LSR
	LSR
	AND	#01
	TAX
	LDA	KRNL(X)		;LO BYTE
	STA	JMP
	LDA	KRNH(X)
	STA	JMP+1		;HI BYTE
;FIX HERE
	AND	#0F0
	CMP	#BANK2&0FF00/100
	BNE	10$
	JMP	SWBN12
10$:
	JMP	SWBN10
;
;
KRNL:
	.BYTE	KRN0&0FF
	.BYTE	KRN1&0FF
KRNH:
	.BYTE	KRN0&0FF00/100
	.BYTE	KRN1&0FF00/100
;
;
;
;
;
;INITIALIZATION ROUTINES
INIT0:
	LDA	GAME
	AND	#7
	TAX
	LDA	GAMSPD(X)
	STA	GSPEED
	LDY	INIWAV(X)
	STY	WAVE
	LDA	MAZWAV(Y)
	STA	MAZE
	LDA	#0
	STA	BNTLIV
;
	RTS
;
GAMSPD:
	.BYTE	31.,29.,27.,25.,23.,21.,19.,17.
;
INIWAV:
	.BYTE	0,4,8,0C,10,14,18,1C
;
MAZWAV:
;MAZE ORDER
;THIS TABLE AFFECTS GR1-GR6, XP7, AI17,WTCHTB
	.BYTE	4		;ONE
	.BYTE	5		;SLANT 8
	.BYTE	9		;X
	.BYTE	1		;PETER
	.BYTE	0A		;SLANT 8 W/BALCONY
	.BYTE	6		;SLANT 8 W/TOP
	.BYTE	2		;MIKE
	.BYTE	4		;ONE
;
	.BYTE	9		;X
	.BYTE	0A		;SLANT 8 W/BALCONY
	.BYTE	7		;BENTLEY
	.BYTE	5		;SLANT 8
	.BYTE	2		;MIKE
	.BYTE	4		;ONE
	.BYTE	1		;PETER
	.BYTE	0		;MINE
;
	.BYTE	6		;SLANT 8 W/TOP
	.BYTE	0A		;SLANT 8 W/BALCONY
	.BYTE	0B		;DOWN UP
	.BYTE	7		;BENTLEY
	.BYTE	1		;PETER
	.BYTE	4		;ONE
	.BYTE	9		;X
	.BYTE	2		;MIKE
;
	.BYTE	7		;BENTLEY
	.BYTE	5		;SLANT 8
	.BYTE	0		;MINE
	.BYTE	8		;BALCONY
	.BYTE	0B		;DOWN UP
	.BYTE	7		;BENTLEY
	.BYTE	9		;X
	.BYTE	3		;SILOS
;
	.BYTE	8		;BALCONY
	.BYTE	1		;PETER
	.BYTE	0A		;SLANT 8 W/BALCONY
	.BYTE	0		;MINE
	.BYTE	2		;MIKE
	.BYTE	0B		;DOWN UP
	.BYTE	3		;SILOS
	.BYTE	9		;X
;
	.BYTE	0		;MINE
	.BYTE	7		;BENTLEY
	.BYTE	6		;SLANT 8 W/TOP
	.BYTE	0B		;DOWN UP
	.BYTE	8		;BALCONY
	.BYTE	4		;ONE
	.BYTE	1		;PETER
	.BYTE	6		;SLANT 8 W/TOP
;
	.BYTE	0B		;DOWN UP
	.BYTE	5		;SLANT 8
	.BYTE	0		;MINE
	.BYTE	3		;SILOS
	.BYTE	0A		;SLANT 8 W/BALCONY
	.BYTE	2		;MIKE
	.BYTE	4		;ONE
	.BYTE	7		;BENTLEY
;
	.BYTE	3		;SILOS
	.BYTE	6		;SLANT 8 W/TOP
	.BYTE	1		;PETER
	.BYTE	8		;BALCONY
	.BYTE	0		;MINE
	.BYTE	8		;BALCONY
	.BYTE	3		;SILOS
	.BYTE	0B		;DOWN UP
;
;
INITW:
;INITIALIZE WAVE
	LDA	#3E
	STA	ENDLIN
	LDY	#0
	STY	WAVST
	LDA	#5
	STA	WAVTMR
	LDA	#70
	LDX	#P1NUM
1$:
	STA	YPOS(X)
	STY	INCTBL(X)
	DEX
	BNE	1$
;
INITP0:
	LDX	MAZE
	LDA	INIP0X(X)
	STA	XPOS
	LDA	INIP0Y(X)
	STA	YPOS
	LDA	#0
	STA	GRFX
	STA	SPEED
	STA	ELVOCC
	STA	ELVCNT
	LDA	#4
	STA	ELVATR		;READY FOR NEW STATE
	LDA	STATE
	AND	#0C0
	STA	STATE
;
	RTS
;
;
WVRINI:
;WAVE REINIT
;ANY PLAYERS WITH NON-NEG POS GO TO THEIR INITIAL SPOT+40
	LDX	MAZE
	LDY	WAVE
	LDA	YPOS+1
	BMI	2$		;HES GONE
	LDA	GR1(Y)
	BPL	5$		;GOES HERE
	STA	YPOS+1
2$:
	LDA	#0
	STA	INCTBL+1
	BEQ	10$		;JMP
5$:
	STA	GRFX+1
	LDA	YP1(X)
	STA	YPOS+1
	LDA	XP1(X)
	STA	XPOS+1
;
10$:
	LDA	YPOS+2
	BMI	12$		;HES GONE
	LDA	GR2(Y)
	BPL	15$		;GOES HERE
	STA	YPOS+2
12$:
	LDA	#0
	STA	INCTBL+2
	BEQ	20$		;JMP
15$:
	STA	GRFX+2
	LDA	YP2(X)
	STA	YPOS+2
	LDA	XP2(X)
	STA	XPOS+2
;
20$:
	LDA	YPOS+3
	BMI	22$		;HES GONE
	LDA	GR3(Y)
	BPL	25$		;GOES HERE
	STA	YPOS+3
22$:
	LDA	#0
	STA	INCTBL+3
	BEQ	30$		;JMP
25$:
	STA	GRFX+3
	LDA	YP3(X)
	STA	YPOS+3
	LDA	XP3(X)
	STA	XPOS+3
;
30$:
	LDA	YPOS+4
	BMI	32$		;HES GONE
	LDA	GR4(Y)
	BPL	35$		;GOES HERE
	STA	YPOS+4
32$:
	LDA	#0
	STA	INCTBL+4
	BEQ	40$		;JMP
35$:
	STA	GRFX+4
	LDA	YP4(X)
	STA	YPOS+4
	LDA	XP4(X)
	STA	XPOS+4
;
40$:
	LDA	YPOS+5
	BMI	42$		;HES GONE
	LDA	GR5(Y)
	BPL	45$		;GOES HERE
	STA	YPOS+5
42$:
	LDA	#0
	STA	INCTBL+5
	BEQ	50$		;JMP
45$:
	STA	GRFX+5
	LDA	YP5(X)
	STA	YPOS+5
	LDA	XP5(X)
	STA	XPOS+5
;
50$:
	LDA	YPOS+6
	BMI	52$		;HES GONE
	LDA	GR6(Y)
	BPL	55$		;ON SCREEN
	STA	YPOS+6
52$:
	LDA	#0
	STA	INCTBL+6
	BEQ	60$		;JMP
55$:
	CMP	#NXHAT
	BNE	57$		;NOT HAT SO FIX IT
;HAS HAT BECOME A BALL?
	LDY	WAVST
	CPY	#1
	BNE	59$		;NOT INITIAL DRAW
57$:
	STA	GRFX+6
59$:
	LDA	YP6(X)
	STA	YPOS+6
	LDA	XP6(X)
	STA	XPOS+6
;
60$:
;
	LDA	#0
	STA	AI1+1
	STA	AI2+1
	STA	AI1+2
	STA	AI2+2
	STA	AI1+3
	STA	AI2+3
	STA	AI1+4
	STA	AI2+4
	STA	AI1+5
	STA	AI2+5
	STA	AI1+6
	STA	AI2+6
;
	RTS
;
;
;*********************************************************************
;
;ROUTINE TO DO SOUND SET UP
;
SNDST1:
	LDA	GAME
	BMI	99$		;ATTRACT
;
	LDA	SOUND(X)
	BEQ	5$		;NO SOUND GOING
	TYA
	CMP	SOUND(X)	;IS PRIORITY LOWER
	BCS	99$		;NEW SOUND NOT AS IMPORTANT AS OLD
5$:
	STY	SOUND(X)	;SOUND NUMBER
	LDA	SNDLN1(Y)
	STA	SNDNDX(X)	;LENGTH
;
	LDA	#0E0		;TO ZERO VOLUME AND SET FRACTION
	STA	AUDV0(X)
	STA	SNDTMR(X)	;STARTS FRACTION OUT OKAY
;
99$:
	RTS
;
;
;TABLE OF LENGTHS
SNDLN1:
	.INCLUDE CLEN.MAC
    