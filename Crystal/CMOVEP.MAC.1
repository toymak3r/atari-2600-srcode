;
;*********************************************************************
;
; OSC ALGORITHMS
;
;*********************************************************************
;
;
;
OS01:
;
;ONLY STATES HERE ARE 2,3(DESCENT, NORMAL)
;AND 7,8(YOU GOT IT, THEY GOT IT)
	LDA	WAVST
	CMP	#3
	BEQ	5$
	JMP	DOP0
;
5$:
;
;
;*********************************************************************
;
P0P1CX:
;COLLISION LOGIC
	LDA	YPOS
	LSR
	STA	TEM0
;
	LDA	STATE
	LDX	#P1NUM-2	;IN CASE HES WEARING HAT
	LSR			;C=1 IF WEARING HAT
	BCS	11$		;DONT CHECK BEES OR HAT
;
;CHECK FOR BEE COLLISION
	LDA	YPOS+P1NUM
	CMP	TEM0
	BNE	5$		;NO CONFLICT
;SAME Y CHECK Z
	LDA	AI1+P1NUM
	BNE	5$		;OFF GROUND
;CHECK XPOS
	LDA	XPOS+P1NUM
;	SEC
	SBC	XPOS
	BPL	1$		;<XPOS+P1NUM
	EOR	#0FF
1$:
	CMP	#4
	BCS	5$		;NO COLLISION
	LDA	JMPNDX
	BNE	5$		;OFF GROUND, HIT BEES
	JMP	DIE		;KILL LOGIC
;
;CHECK FOR HAT COLLISION
;
5$:
;NOT WEARING HAT
	LDX	#P1NUM-1
;
;CHECK AGAINST OTHER OBJECTS
;
11$:
	LDA	TEM0
	CMP	YPOS(X)
	BNE	13$		;NO HIT
;CHECK XPOS
	LDA	XPOS(X)
;	SEC
	SBC	XPOS
	BPL	12$		;<XPOS(X)
	EOR	#0FF
12$:
	CMP	#4
	BCC	15$		;COLLISION!!
;
13$:
	DEX
	BNE	11$		;CHECK OTHER GUYS
;
;NO P0P1 COLLISIONS
	JMP	99$
;
15$:
	LDY	GRFX(X)
	CPY	#NXHAT
	BNE	18$		;NOT HAT
;GRAB THE HAT, WEAR IT
	LDA	#NXHATW
	STA	GRFX+P1NUM-1
	LDA	#0
	STA	AI1+P1NUM-1
	LDA	#TIMHAT
	STA	AI2+P1NUM-1
	LDA	STATE
	ORA	#1
	STA	STATE
	LDA	#5
	CLC
	JSR	ADSCR4
;DO SOUND
	LDY	#SNDHAT
	LDX	#0
	JSR	SNDSET
	LDY	#SNDHAT+1
	LDX	#1
	JSR	SNDSET
	JMP	99$		;NO MORE COLLISIIONS
;
;
18$:
	LDA	STATE
	LSR			;IS HE WEARING HAT?
;DO WE DO ANYTHING?
	LDA	STABT3(Y)
	AND	#3E
	BEQ	199$		;DO NOTHING(SCORE)
	AND	#1E		;OTHER THAN GESW?
	BNE	20$		;YEP
;MUST BE EDIBLE ONLY WITHOUT HAT
	BCS	199$		;WEARING HAT
	LDA	JMPNDX
	BNE	199$		;OFF GROUND
;MUST BE A SWALLOWING GEM EATER
;ADD SCORE
	LDA	#NXSCR1		;500 POINTS
	STA	GRFX(X)
	LDA	#TIMSCR
	STA	AI2(X)
	LDA	#5
	CLC
	JSR	ADSCR4
;DO SOUND
	LDY	#SNDSGE
	LDX	#0
	JSR	SNDSET
	JMP	99$
;
20$:
	AND	#8		;EDIBLE WITH HAT ONLY?
	BEQ	30$		;NOT WITCH
;MUST BE WITCH
	BCC	45$		;NOT WEARING HAT, DO KILL
;ADD SCORE
	LDA	#NXSCR3		;3000
	STA	GRFX(X)
	LDA	#TIMSCR
	STA	AI2(X)
	LDA	#30
	CLC
	JSR	ADSCR4
	JMP	99$
;
30$:
;EITHER EDIBLE OR DEADLY/STUNNABLE
	LDA	STABT3(Y)
	AND	#12		;STUNNABLE OR DEADLY?
	BNE	40$		;YES
;MUST BE EDIBLE WITH OR WITHOUT HAT (HONEY)
	LDA	#NXSCR2
	STA	GRFX(X)
	LDA	#TIMSCR
	STA	AI2(X)
;UPDATE BEE LOGIC
	LDA	#0
	STA	AI2+P1NUM	;HONEY BIT
;ADD SCORE
	LDA	#10
	CLC
	JSR	ADSCR4
;DO SOUND
	LDY	#SNDHNY
	LDX	#0
	JSR	SNDSET
199$:	JMP	99$
;
40$:
;DEADLY OR STUNNABLE
	BCS	99$		;WEARING HAT, DO NEITHER
45$:
;IS HE ON THE GROUND?
	LDA	JMPNDX
	BNE	50$		;OFF GROUND
47$:
;ON THE GROUND
;ALL STUNNABLE OBJECTS ARE DEADLY SO KILL HIM
	JMP	DIE
;
50$:
;IS IT STUNNABLE
	LDA	STABT3(Y)
	AND	#10		;STUN BIT
	BEQ	99$		;DO NOTHING
	CPY	#NXGESW		;LESS THAN EATING GEM EATERS?
	BCS	60$		;GRFX INDICATES TREE
;STUN THAT GEM EATER
	LDA	#NXGEST
	STA	GRFX(X)
	LDA	#0
	STA	AI2(X)
	JMP	99$
60$:
;STUN THAT TREE
	LDA	#NXTRES		;STUNNED TREE
	STA	GRFX(X)
	LDA	#0
	STA	AI2(X)
	JMP	99$
;
;
99$:
;
;
;*********************************************************************
MVP1:
	LDA	FRMCTR
	AND	#03
	TAY
	LDX	FRMGUY(Y)
MVP1ST:
	STX	TEM0
	LDY	#NXBEES
	CPX	#P1NUM
	BEQ	1$		;DO BEES
	LDA	INCTBL(X)
	BEQ	MVP1LP		;NOBODY HOME
	LDY	GRFX(X)
1$:
	LDA	MVPTL(Y)
	STA	JMP
	LDA	MVPTH(Y)
	STA	JMP+1
;
	JMP	@JMP
;
;
MVP1LP:
	LDY	TEM0
	LDX	P1FRMT(Y)
	BNE	MVP1ST
;
99$:
;
;*********************************************************************
;
;
BEESET:
;NO BEES
;MOVE THIS
	LDA	FRMCTR
	BNE	99$
	LDY	BEECNT
	CPY	#2
	BCC	99$		;DONT DO IT YET
	LDA	BEFREQ(Y)
	CLC
	ADC	#SNDNOB
	TAY
;MIGHT WANT TO INCREASE THE PITCH HERE
	LDX	#1
	JSR	SNDSET
;
99$:
;
;
;*********************************************************************
;
DOP0:
;
	LDA	GAME
	BMI	99$		;ATTRACT MODE
;
;BUTTON WAS SET DURING SCREEN TIME
;
;ONLY ALLOW JOYSTICK OR BUTTON IF NOT JUMPING
	LDX	JMPNDX
	BEQ	30$
	DEC	JMPNDX
	JMP	99$		;NO MORE
30$:
;
	LDA	WAVST
	CMP	#7
	BCS	99$		;DONT ALLOW STICK OR JUMP
;
;
	LDA	SWCHA
	LSR
	LSR
	LSR
	LSR
	TAX
	LDA	JOYSPD(X)
;
	STA	SPEED
;
;
;
;
;IS BUTTON PUSHED?
	BIT	BLSTAT
	BPL	99$		;NO BUTTON
	BVS	99$		;STILL HELD DOWN FROM BEFORE
	LDA	FRMCTR
	STA	SEEDH
;TIME TO JUMP
	LDX	JMPNDX
	BNE	99$		;STILL JUMPING
	LDX	#26.
	STX	JMPNDX
;DO SOUND
	LDY	#SNDJMP
	LDX	#0
	JSR	SNDSET
;
99$:
;
;*********************************************************************
;
;
;*********************************************************************
;
; PLAYER 0
;
;*********************************************************************
;
MVP0:
;UPDATE HIS POSITION
	LDA	ELVATR
	AND	#0F
	BEQ	1$		;NOT ON SCREEN
	LSR
	BCC	2$		;EVEN SO MOVING
1$:
	LDA	STATE		;TURN OFF ELEVATOR BIT
	AND	#0FB
	STA	STATE
;
2$:
	LDA	STATE
	AND	#4		;ELEVATOR BIT
	BEQ	5$		;NOT ON ELEVATOR
;ON ELEVATOR
	LDA	MSTRT
	ASL
	STA	YPOS		;PUTS HIM ON TOP OF ELEVATOR
;MOVE HIM TO THE CENTER
	LDA	FRMCTR
	AND	#3
	CMP	#3
	BNE	20$		;MOVE EVERY 4TH
;TIME TO CENTER
	LDA	MHOR
;	SEC
	SBC	XPOS
	BEQ	10$		;MOVE BENTLEY LEFT
	CMP	#5
	BCC	20$		;NO MOVE
;MOVE RIGHT
	INC	XPOS
	JMP	20$
10$:
	DEC	XPOS		;MOVE LEFT
20$:
	JMP	RANDOM
5$:
;NOW CHECK FOR MOVEMENT ON MAZE
MVXP0:
	LDA	#FTCRT1&0FF
	STA	TMP
	LDA	#FTCRT1&0FF00/100
	STA	TMP+1
	LDA	YPOS
	LSR
	TAY			;SAVE FOR PLAYFIELD INDEX
	LDX	SPEED
	BNE	8$		;NOT ZERO
	JMP	LVP0		;NO X MOVE
8$:
	CLC
	LDA	P0XTMR
	ADC	HSPD-1(X)
	STA	P0XTMR
	LDA	HSPD(X)
	ADC	XPOS		;NEW XPOS
	STA	TEM0
	CMP	XPOS
	BEQ	MVYP0		;NO MOVE
	LSR
	LSR
	TAX
	LDA	PF1OR2(X)	;PF1 OR PF2?
	BMI	11$		;USE PF2
	JMP	FTCH13
11$:
	JMP	FTCH23
FTCRT1:
	AND	PFBIT(X)
	BNE	20$		;CANT MOVE HERE DO Y
	LDA	TEM0
	STA	XPOS		;NEW POSITION
	LDA	STATE
	ORA	#10
	STA	STATE
;
20$:
MVYP0:
	LDA	#FTCRT2&0FF
	STA	TMP
	LDA	#FTCRT2&0FF00/100
	STA	TMP+1
	LDX	SPEED		;NOT 0
	CLC
	LDA	P0YTMR
	ADC	VSPD-1(X)
	STA	P0YTMR
	LDA	VSPD(X)
	ADC	YPOS
	STA	TEM0		;SAVE IN CASE WE CAN MOVE
	CMP	YPOS
	BEQ	LVP0		;NO MOVE
	LSR
	TAY
	LDA	XPOS
	LSR
	LSR
	TAX
	LDA	PF1OR2(X)
	BMI	21$		;USE PF2
	JMP	FTCH13
21$:
	JMP	FTCH23
FTCRT2:
	AND	PFBIT(X)
	BNE	30$		;CANT MOVE HERE
	LDA	TEM0
	STA	YPOS
	LDA	STATE
	ORA	#10
	STA	STATE
30$:
;
;
LVP0:
;
;CHECK FOR ELEVATOR HERE
;REMEMBER MISSILE IS 1 CLOCK TO THE LEFT
	LDA	ELVATR
	AND	#0F
	BEQ	99$		;NOT HERE
	LSR
	BCC	99$		;MOVING
	LDA	XPOS
;FOR CORRECT WINDOW USE +4<4
;FOR SYMETRIC USE +5<5 OR +6<7
	ADC	#6		;C=0
	SBC	MHOR		;COMPARE WITH ELEVATOR
	CMP	#7
	BCS	99$		;NOT LINED UP
	LDA	YPOS
	LSR
	CMP	MSTRT
	BNE	99$		;NOT IN RIGHT PLACE
;HE IS TRYING TO GET ON THE ELEVATOR
	LDA	STATE
	ORA	#4
	STA	STATE
;
99$:
;
;*********************************************************************
;
OSEND:
RANDOM:
	LDA	SEEDH
	ASL
	EOR	SEEDL
	ASL
	ASL
	ROL	SEEDL
	ROL	SEEDH
	ROL	SEEDL
;
;
	LDX	#OSTIME&0FF
	STX	JMP
	LDX	#OSTIME&0FF00/100
	STX	JMP+1
	JMP	SWBN32
;
;
JOYSPD:
	.BYTE	0,0B,7,0
	.BYTE	3,1,5,3
	.BYTE	0F,9,0D,0F
	.BYTE	0,0B,7,0
;
;
;
;*********************************************************************
;
;
;
FRMGUY:
	.BYTE	7,2,6,4
P1FRMT:
	.BYTE	0,0,1,0,3,0,5,0
;
;
;*********************************************************************
;
;
DIE:
	LDX	#TIMDI4
	STX	WAVTMR
	LDX	#4
	STX	WAVST
	LDX	#8
	STX	GRFX
;DO SOUND FOR DEATH
	LDY	#SNDDTH
	LDX	#0
	JSR	SNDSET
	LDY	#0
	LDX	#1
	JSR	SNDSET
;
	JMP	OSEND
;
;
;*********************************************************************
;
;ROUTINE SCORE
;
ADSCR4:
	SED
	LDY	GAME
	BMI	ADSCR5
	ADC	SCORE+1
	STA	SCORE+1
;EXTRA LIFE CHECK HERE
	BCC	ADSCR5		;NOT TIME FOR EXTRA LIFE
	LDA	SCORE
	LSR
	BCC	20$		;NOT ODD 10
;	AND	#9
;	BEQ	20$		;NOT TIME
;	CMP	#9
;	BEQ	20$		;NOT TIME
;ODD HUNDRED EVEN 20 OR EVEN HUNDRED ODD 20
;EXTRA LIFE HERE
	INC	BNTLIV
	BPL	10$		;NO PROBLEM
	DEC	BNTLIV
10$:
	LDY	#SNDXTL
	LDX	#0
	JSR	SNDSET		;TUNE
	LDY	#SNDXTL+1
	LDX	#1
	JSR	SNDSET		;TUNE
;
20$:
	CLC
	LDA	#1
	ADC	SCORE
	STA	SCORE
;
ADSCR5:
	CLD
	RTS
;
;
;*********************************************************************
;
;
;*********************************************************************
;
; P1 LOGIC
;
;*********************************************************************
;
;
GR000:
;LOGIC FOR UNSTUNNED GEM EATERS
;
	CPX	ELVOCC
	BNE	2$		;NOT ME
	LDA	ELVATR
	AND	#0F
	BEQ	1$		;OFF
	LSR
	BCS	1$		;NOT MOVING
	LDA	MSTRT
	STA	YPOS(X)
	JMP	MVP1LP
1$:
	LDA	#0
	STA	ELVOCC
2$:
;CHECK STATUS
;8 TURNS LEFT UNTIL DOWN RIGHT THEN GOES TO SHORT
;0C GOES IN A RANDOM DIRECTION
;4 GOES STRAIGHT
;0 GOES SHORT THEN TURNS RIGHT OR LEFT
	LDY	AI2(X)
	LDA	AI1(X)
	AND	#0C
	BEQ	4$		;SHORT
	CMP	#8		;LEFT TURN GUY
	BNE	3$		;NOPE
	CPY	#9
	BCC	10$		;WAIT LONGER
	LDA	#0
	STA	TEM0+1
	BEQ	5$
3$:
	CMP	#0C		;DISTANCE GUY THEN RANDOM DIRECTION?
	BNE	10$		;STATE 4 JUST GO STRAIGHT
	CPY	#6
	BCC	10$
	LDA	SEEDL
	EOR	INTIM
	AND	#3
	EOR	AI1(X)
	BCS	6$		;JMP
4$:
	LDA	TRNPRB(Y)
	CMP	SEEDL
	BCC	10$
	LDA	SEEDL
	ASL
	ASL
	EOR	SEEDH
	SEC
	EOR	INTIM
	AND	#0E
	STA	TEM0+1
5$:
	LDA	AI1(X)
	ADC	#0
	EOR	TEM0+1
6$:
	STA	AI1(X)
	LDA	#0
	STA	AI2(X)
10$:
;
	LDA	FRMCTR
	AND	#0FC		;2 SECONDS
	BNE	50$		;NOT NOW
;TIME TO SPEED THEM UP
	CLC
	LDA	AI1(X)
	ADC	#10		;SPEED UP BY 1
	BCC	30$		;OKAY
	ORA	#0F0		;MAX SPEED
30$:
	STA	AI1(X)
;
	LDA	FRMCTR
	AND	#0FC
	BNE	50$
	LDA	FRMCTR+1
	AND	#0F		;64 SECS
	BNE	50$		;NOT NOW
;SLOW HIM BACK TO ZERO SPEED
	LDA	AI1(X)
	AND	#0F
	STA	AI1(X)
;
50$:
	LDA	AI1(X)
	AND	#0F0
	LSR
	LSR
	LSR
	LSR
	TAY
	LDA	GESPED(Y)	;SPEED UP FACTOR
	LDY	GSPEED
	ADC	GESPD(Y)
;	CMP	#3F
;	BCC	55$		;OKAY
;	LDA	#3F		;MAX
55$:
	STA	TEM0+2		;SPEED
;
GEMV0:
	LDA	AI1(X)
	AND	#3
	TAY
	LDA	DIRY(Y)
	STA	TEM0+4
	LDA	DIRX(Y)
	STA	TEM0+5
;
GEMV1:
;TIME TO UPDATE Y?
	LDA	#0
	STA	TEM0+1
	JSR	SPDCLY
	TAY
	BEQ	30$		;NO MOVE
	INC	AI2(X)
	LDA	#1
	STA	TEM0+1
	LDA	TEM0+4
	BPL	10$
	TYA
	EOR	#0FF
	CLC
	ADC	#1
	TAY
10$:
	STY	TEM0+4
;
30$:
;
GEMV2:
;TIME FOR X MOVE?
	JSR	SPDCLX
	TAY
	BEQ	60$		;NO X MOVE
	LDA	TEM0+1
	ORA	#4
	STA	TEM0+1
	LDA	TEM0+5
	BPL	40$
	TYA
	EOR	#0FF
	CLC
	ADC	#1
	TAY
40$:
	STY	TEM0+5
;
60$:
	JMP	P1MV
;
GECNFL:
;CONFLICT
	LDX	TEM0
	LDY	AI2(X)
	CPY	#2
	BCS	10$		;MORE THAN 2 COUNTS, DO 180
	LDA	TEM0+1
	CMP	#5		;BOTH FAIL
	BNE	99$
	CLC
10$:
	LDA	AI1(X)
	AND	#0F3
	ADC	#1
	AND	#0F3
	STA	AI1(X)		;90 DEG ROT.
	LDA	#0
	STA	AI2(X)
99$:
	JMP	P1LVTR
;
;
DIRX:
;TABLE OF DIRECTIONS FOR GEM EATERS
	.BYTE	1,0FF,0FF,1
DIRY:
	.BYTE	1,1,0FF,0FF
;
;TABLE OF TURN PROBABILITY
TRNPRB:
	.BYTE	0,0C,68,0C,90,10,0C8,20,0F0,40,0FF
;TABLE OF BEGINNING SPEEDS FOR GEM EATERS
GESPD:
	.BYTE	1D,1D,1D,1D,1C,1C,1C,1C
	.BYTE	1B,1B,1B,1B,1A,1A,1A,1A
	.BYTE	1A,19,19,18,18,17,16,15
	.BYTE	14,13,12,10,0E,0C,0A,8
;
	.BYTE	24,24,24,24,23,23,23,23
	.BYTE	22,22,22,22,21,21,21,21
	.BYTE	20,20,1F,1E,1D,1D,1C,1C
	.BYTE	1B,1A,19,18,17,16,15,14
;
;
;TABLE OF SPEED UP FACTOR FOR GEM EATERS
GESPED:
	.BYTE	4,5,6,7,8,9,0A,0B
	.BYTE	0C,0D,0E,0F,10,11,12,13
;
;
;
;*********************************************************************
;
GR004:
;LOGIC FOR STUNNED GEM EATERS
;
;
	INC	AI2(X)
	LDA	AI2(X)
	LDY	GSPEED
	CMP	GESTTM(Y)
	BCC	20$
;TIME TO RESTORE TO NORMAL
	LDA	#NXGE
	STA	GRFX(X)
	LDA	AI1(X)
	AND	#0F		;PRESERVE DIRECTION
	STA	AI1(X)
	LDA	#0
	STA	AI2(X)
;
20$:
	CPX	ELVOCC
	BNE	2$		;NOT ME
	LDA	ELVATR
	AND	#0F
	BEQ	1$		;OFF
	LSR
	BCS	1$		;NOT MOVING
	LDA	MSTRT
	STA	YPOS(X)
	JMP	MVP1LP
1$:
	LDA	#0
	STA	ELVOCC
2$:
;
	JMP	P1LVTR
;
GESTTM:
	.BYTE	0A,0A,0B,0B,0C,0C,0D,0D
	.BYTE	0E,0E,0F,0F,10,10,11,11
	.BYTE	12,12,13,13,14,14,15,15
	.BYTE	16,16,17,17,18,18,19,19
;
	.BYTE	7,7,8,8,8,9,9,9
	.BYTE	0A,0A,0A,0B,0B,0B,0C,0C
	.BYTE	0C,0D,0D,0D,0E,0E,0E,0F
	.BYTE	0F,0F,10,10,10,11,11,11
;
;
;*********************************************************************
;
;
;LOGIC FOR EATING GEM EATERS
GR005:
;
	INC	AI2(X)
	LDA	AI2(X)
	LDY	GSPEED
	CMP	GESWTM(Y)
	BCC	99$
;DONE WITH THIS STATE
	LDA	GRFX(X)
	CLC
	ADC	#1
	CMP	#NXGESW+4
	BCS	10$		;DONE EATING
	STA	GRFX(X)		;NEXT STATE
	LDA	#0
	STA	AI2(X)
	BEQ	99$		;JMP
;
10$:
	LDA	#NXGE		;BACK TO NORMAL
	STA	GRFX(X)
	LDA	AI1(X)
	AND	#0F
	STA	AI1(X)
	LDA	#0
	STA	AI2(X)
;
99$:
	JMP	MVP1LP		;RETURN
;
GESWTM:
	.BYTE	2,2,2,2,2,2,2,2
	.BYTE	2,2,2,2,2,2,3,3
	.BYTE	3,3,3,3,3,3,3,3
	.BYTE	4,4,4,4,4,4,4,4
;
	.BYTE	1,1,1,1,1,1,1,1
	.BYTE	2,2,2,2,2,2,2,2
	.BYTE	3,3,3,3,3,3,3,3
	.BYTE	3,3,4,4,4,4,4,4
;*********************************************************************
;
;
GR00C:
;LOGIC FOR WITCH
;
	CPX	ELVOCC
	BNE	2$		;NOT ME
	LDA	ELVATR
	AND	#0F
	BEQ	1$		;OFF
	LSR
	BCS	1$		;NOT MOVING
	LDA	MSTRT
	STA	YPOS(X)
	JMP	MVP1LP
1$:
	LDA	#0
	STA	ELVOCC
2$:
10$:
;
	LDA	WAVE
	LSR
	LSR
	TAY
	LDA	WTCHTB(Y)
	CMP	AI2(X)
	BCS	15$
	LDY	#0
	STY	AI2(X)
	LDA	GRFX(X)
	ADC	#1
	AND	#3
	STA	AI1(X)
	ADC	#NXWTCH
	STA	GRFX(X)
;
15$:
	LDY	GSPEED
	LDA	WTCSPD(Y)
	STA	TEM0+2
	JMP	GEMV0
;
;
;WITCH BOX
WTCHTB:
;INDEXED BY WAVE
	.BYTE	5,6,5,4,6,6,3,3
	.BYTE	6,5,4,4,6,5,3,1
;TABLE FOR WITCH, GHOST, SKELETON
WTCSPD:
	.BYTE	2C,2B,2B,2A,2A,29,28,27
	.BYTE	26,25,24,23,22,21,20,1F
	.BYTE	1F,1E,1E,1D,1D,1C,1C,1C
	.BYTE	1B,1B,1A,18,16,14,12,10
;
	.BYTE	3A,39,37,36,34,33,32,31
	.BYTE	30,2E,2D,2C,2B,2A,29,28
	.BYTE	27,26,25,24,23,22,21,20
	.BYTE	1E,1C,1B,1A,18,17,15,14
;
;*********************************************************************
;
GR010:
;LOGIC FOR GHOST
;
	CPX	ELVOCC
	BNE	2$		;NOT ME
	LDA	ELVATR
	AND	#0F
	BEQ	1$		;OFF
	LSR
	BCS	1$		;NOT MOVING
	LDA	MSTRT
	STA	YPOS(X)
	JMP	MVP1LP
1$:
	LDA	#0
	STA	ELVOCC
2$:
10$:
;
	LDA	SEEDL
	EOR	INTIM
	LSR
	LSR
	AND	#3
	TAY
	LDA	DIRY(Y)		;Y COMPONENT FOR THIS DIRECTION
	STA	TEM0+4
;
	EOR	SEEDL
	EOR	INTIM
	AND	#3
	TAY
	LDA	DIRX(Y)		;X COMPONENT FOR THIS DIRECTION
	STA	TEM0+5
;
;
	LDY	GSPEED
	LDA	WTCSPD(Y)
	STA	TEM0+2		;SPEED
;
	JMP	GEMV1
;
;
;
;*********************************************************************
;
GR012:
;LOGIC FOR BALL
;
	CPX	ELVOCC
	BNE	2$		;NOT ME
	LDA	ELVATR
	AND	#0F
	BEQ	1$		;OFF
	LSR
	BCS	1$		;NOT MOVING
	LDA	MSTRT
	STA	YPOS(X)
	JMP	MVP1LP
1$:
	LDA	#0
	STA	ELVOCC
2$:
10$:
;
	LDY	GSPEED
	LDA	YPOS
	LSR
	SEC
	SBC	YPOS(X)
	BCS	15$		;BALL BELOW
	LDA	#0FC
	BMI	17$		;JMP
15$:
	CLC
	BEQ	17$
	LDA	#4
17$:
	ADC	AI1(X)
	STA	AI1(X)
	STA	TEM0+1
	BPL	20$
	EOR	#0FF
	CLC
	ADC	#1
20$:
	CMP	BLSPD(Y)
	BCC	30$		;OKAY
;TOO BIG
	LDA	BLSPD(Y)
	BIT	TEM0+1
	BPL	25$		;DONT COMPL
	EOR	#0FF
	ADC	#0
25$:
	STA	AI1(X)
30$:
;
	LDA	AI1(X)
	STA	TEM0+4		;DIRECTION
	BPL	35$
	EOR	#0FF
	CLC
	ADC	#1
35$:
	STA	TEM0+2		;SPEED
;
;
	LDA	XPOS
	SEC
	SBC	XPOS(X)
	BCS	45$		;BALL LEFT
	LDA	#0FC
	BMI	47$		;JMP
45$:
	CLC
	BEQ	47$
	LDA	#4
47$:
	ADC	AI2(X)
	STA	AI2(X)
	STA	TEM0+1
	BPL	50$
	EOR	#0FF
	CLC
	ADC	#1
50$:
	CMP	BLSPD(Y)
	BCC	60$		;OKAY
;TOO BIG
	LDA	BLSPD(Y)
	BIT	TEM0+1
	BPL	55$		;DONT COMPL
	EOR	#0FF
	ADC	#0
55$:
	STA	AI2(X)
60$:
	LDA	AI2(X)
	STA	TEM0+5		;DIRECTION
	BPL	65$
	EOR	#0FF
	CLC
	ADC	#1
65$:
	STA	TEM0+3		;SPEED
;
;
BLMV:
;TEM0+2=YSPEED, TEM0+3= XSPEED
;TIME TO UPDATE Y?
	LDA	#0
	STA	TEM0+1
	LDA	TEM0+4
	BEQ	30$		;NO MOVE
	JSR	SPDCLY
	TAY
	BEQ	30$		;NO MOVE
	LDA	#1
	STA	TEM0+1
	LDA	TEM0+4
	BPL	10$
	TYA
	EOR	#0FF
	CLC
	ADC	#1
	TAY
10$:
	STY	TEM0+4
;
30$:
;TIME FOR X MOVE?
	LDA	TEM0+3
	STA	TEM0+2		;XSPEED
	LDA	TEM0+5
	BNE	40$		;MOVE
	JMP	P1MV
40$:
	JMP	GEMV2
;
;
BLSPD:
	.BYTE	2F,2E,2D,2C,2B,2A,29,28
	.BYTE	27,26,25,24,23,22,21,20
	.BYTE	1F,1E,1D,1C,1B,1A,19,18
	.BYTE	17,16,15,14,13,12,11,10
;
	.BYTE	3B,3A,39,38,37,36,35,34
	.BYTE	33,32,31,30,2F,2E,2D,2C
	.BYTE	2B,2A,29,28,27,26,25,24
	.BYTE	23,22,21,20,1F,1E,1D,1C
;
;
;
;*********************************************************************
;
GR009:
;LOGIC FOR BEES
;
	INC	WAVTMR		;LOW RES COUNT
	LDA	WAVTMR
	CMP	#110.
	BCC	50$		;NOT TIME FOR BEES
;
	LDA	#0
	STA	WAVTMR
	STA	INCTBL+P1NUM
	LDA	#0E0
	STA	YPOS+P1NUM	;IN CASE THEY HAVENT YET LEFT
	INC	BEECNT
	LDA	BEECNT
	CMP	#23.		;MAX #BEE FACTOR
	BCC	1$		;OKAY
	LDA	#23.
	STA	BEECNT
1$:
	CMP	#6
	BCS	10$		;TIME FOR BEES
;
	LSR
	BCC	2$		;NOT BEE TIME
	LDA	AI2+P1NUM	;SET BY HONEY POT
	BNE	10$		;HONEY POT HERE
2$:
	JMP	MVP1LP
;
10$:
;TIME FOR BEES TO COME ON SCREEN
	LDA	BEECNT
	CMP	#11.
	BCS	20$		;PUT ABOVE BENTLEYS HEAD
;FIND POSITION
	LDY	WAVE
	LDA	XP7(Y)
	STA	XPOS+P1NUM
	LDA	AI17(Y)
	JMP	30$
20$:
;FIND BENTLEY
	LDA	XPOS
	STA	XPOS+P1NUM
	LDA	STATE		;ON ELEVATOR?
	AND	#4
	BNE	25$		;YEP
	LDA	YPOS
	LSR
	JMP	30$
25$:
	LDA	MEND		;BOTTOM OF ELEVATOR
	CLC
	ADC	#1
30$:
	STA	TEM0+1		;YPOS OF DESTINATION
	LDA	#TOPLIN-6+1
	STA	YPOS+P1NUM
	SEC
	SBC	TEM0+1
	STA	AI1+P1NUM	;ZPOS
	LDA	#NXBEES
	STA	GRFX+P1NUM
	LDA	#80
	STA	INCTBL+P1NUM
;
50$:
	LDA	INCTBL+P1NUM
	BNE	51$		;ON SCREEN
	JMP	MVP1LP
51$:
;DO SOUND
	LDY	BEECNT
	LDA	BEFREQ(Y)
	STA	TEM0+1		;FREQ OFFSET
;
	LDY	WAVTMR
	CPY	#82.
	BCS	60$		;NO SOUND
	LDA	#SNDBEL
	CPY	#34.
	BCC	55$		;LOW FREQ
	CPY	#50.
	BCC	57$		;HI FREQ
	CPY	#66.
	BCC	55$		;LOW FREQ
57$:
	LDA	#SNDBEH		;HIGHER PITCH
55$:
	CLC
	ADC	TEM0+1		;PITCH
	TAY
	LDX	#1
	JSR	SNDSET
;
	LDX	TEM0
;
60$:
;CHECK BEE STATES
	LDA	WAVTMR
	BNE	67$		;BEES NOT JUST STARTED
65$:
	JMP	MVP1LP
;
67$:
	CMP	#82.		;TIME TO GO UP
	BCC	70$		;NOPE
	INC	YPOS+P1NUM
	INC	YPOS+P1NUM
	INC	AI1+P1NUM
	INC	AI1+P1NUM
	LDA	#TOPLIN-6+1
	CMP	YPOS+P1NUM
	BCS	69$		;STILL ON SCREEN
;BEES TOO HIGH
	LDA	#0E0
	STA	YPOS+P1NUM
	LDA	#0
	STA	INCTBL+P1NUM
;
69$:
	JMP	MVP1LP		;DONE
;
70$:
;ARE THEY ON THE GROUND?
	LDA	AI1+P1NUM	;ZPOS
	BEQ	80$		;ON THE GROUND
	LDA	WAVTMR
	BEQ	69$		;JUST STARTED DOWN
	LDY	BEECNT
	LDA	BESPDF(Y)	;FALLING SPEED OFFSET
	STA	TEM0+2
	JSR	SPDCLX		;PRETEND X
	ASL			;FASTER, FASTER
	CMP	AI1+P1NUM	;ZPOS
	BCC	74$		;Z>#PIXELS
	LDA	AI1+P1NUM	;#PIXELS
74$:
	STA	TEM0+1
	LDA	YPOS+P1NUM
	SEC
	SBC	TEM0+1
	STA	YPOS+P1NUM
	LDA	AI1+P1NUM
	SEC
	SBC	TEM0+1
	STA	AI1+P1NUM
;
	LDX	TEM0		;RESTORE INDEX
	JMP	MVP1LP
;
80$:
;BEES ON THE GROUND
	LDY	GSPEED
	LDA	BESPDW(Y)
	CLC
	LDY	BEECNT		;SPEED THEM UP
	ADC	BESPDG(Y)	;GROUND SPEED FOR BEES
	CMP	#3F
	BCC	82$		;BELOW MAX
	LDA	#3F
82$:
	STA	TEM0+2
	STA	TEM0+3
;
	JMP	TREBEE		;TRACK P0
;
BESPDW:
;OVERALL WAVE SPEED FOR THESE BEES
	.BYTE	7,7,7,7,6,6,6,6
	.BYTE	5,5,5,5,4,4,4,4
	.BYTE	3,3,3,3,2,2,2,2
	.BYTE	1,1,1,1,0,0,0,0
;
	.BYTE	0A,0A,0A,9,9,9,8,8
	.BYTE	8,7,7,7,6,6,6,5
	.BYTE	5,5,4,4,4,3,3,3
	.BYTE	3,2,2,2,1,1,1,1
;
BESPDF:
;FALLING SPEED BY TRIP #
	.BYTE	0A,0A,0A,0B,0B,0B,0C,0C
	.BYTE	0C,0D,0D,0E,0F,10,11,12
	.BYTE	13,14,16,18,1A,1C,1E,36
;
BESPDG:
;TRACKING SPEED BY TRIP #
	.BYTE	1A,1A,1C,1C,1E,1E,20,22
	.BYTE	24,25,26,27,28,29,2A,2B
	.BYTE	2C,2D,2E,2F,30,34,38,3F
;
;
BEFREQ:
;PITCHES FOR BEE RATES
	.BYTE	0,0,0,0,0,1,1,2
	.BYTE	2,3,3,4,4,5,5,6
	.BYTE	6,6,7,7,7,7,7,7
;
;
;*********************************************************************
;
GR016:
;LOGIC FOR NORMAL HAT
;
;
	CPX	ELVOCC
	BNE	2$		;NOT ME
	LDA	ELVATR
	AND	#0F
	BEQ	1$		;OFF
	LSR
	BCS	1$		;NOT MOVING
	LDA	MSTRT
	STA	YPOS(X)
	JMP	MVP1LP
1$:
	LDA	#0
	STA	ELVOCC
2$:
;
;MAKE HAT MOVE IF GSPEED < 24 AND
;CHANGE DIRECTIONS WHENEVER HATNDXAND3C=3C
;AND TURN INTO BALL IF HATNDX>=0FC AND GSPEED <8
	LDY	GSPEED
	LDA	HATSPD(Y)
	STA	TEM0+2
	STA	TEM0+3		;SPEEDS TO BOUNCE
	LDA	GSPEED
	AND	#1F
	TAY
	CPY	#HTBNCE
	BCS	10$		;DONT MOVE
;HAT IS BOUNCING, IS IT TIME
	LDA	HATNDX
	AND	#3C
	CMP	#3C
	BNE	30$		;NOT NEW DIRECTION
;TIME FOR BALL?
	CPY	#HTBALL
	BCS	20$		;DONT MAKE BALL
	LDA	HATNDX
	AND	#0FC
	CMP	#0FC
	BNE	20$		;DONT CREATE BALL
;BALL TIME
	LDA	#0
	STA	AI1+P1NUM-1
	STA	AI2+P1NUM-1
	LDA	#NXBALL
	STA	GRFX+P1NUM-1
10$:
	JMP	P1LVTR		;DONT MOVE
20$:
;NEW DIRECTION
	LDA	SEEDL
	EOR	XPOS
	AND	#0F
	STA	AI2(X)
;
30$:
;MOVE IT
	LDA	AI2(X)
	AND	#0F
	TAY
	LDA	DIRHTY(Y)
	STA	TEM0+4
	LDA	DIRHTX(Y)
	STA	TEM0+5
;
	JMP	BLMV
;
DIRHTY:
;DIRECTIONS FOR HAT BOUNCE
	.BYTE	1,1,0,0FF,0,0,0FF,1
	.BYTE	0FF,1,0,0,1,0FF,0,0FF
;
DIRHTX:
;DIRECTIONS FOR HAT BOUNCE
	.BYTE	0FF,0,0,0FF,1,0,1,1
	.BYTE	0,0FF,0,0FF,1,0FF,0,1
;
HATSPD:
	.BYTE	18,17,17,16,16,15,14,14
	.BYTE	13,12,12,11,11,10,0F,0F
	.BYTE	0E,0D,0D,0C,0C,0B,0A,0A
	.BYTE	9,9,8,8,7,7,6,6
;
	.BYTE	2B,2A,29,28,27,26,25,24
	.BYTE	23,22,21,20,1F,1E,1D,1C
	.BYTE	1B,1A,19,18,17,16,15,14
	.BYTE	13,12,11,10,0F,0E,0D,0C
;
;
;
;
;*********************************************************************
;
GR017:
;LOGIC FOR WORN HAT
;
	DEC	AI2+P1NUM-1	;TIMER FOR HAT
	BNE	99$		;STAY LONGER
;TURN OFF THAT HAT
	LDA	STATE
	AND	#0FE
	STA	STATE		;HES NOT WEARING IT NOW
	LDA	#0E0
	STA	YPOS(X)		;OFF SCREEN
	LDA	#0
	STA	INCTBL(X)
;
99$:
;
	JMP	MVP1LP
;
;
;*********************************************************************
;
GR018:
;LOGIC FOR HONEY POT
;
	CPX	ELVOCC
	BNE	2$		;NOT ME
	LDA	ELVATR
	AND	#0F
	BEQ	1$		;OFF
	LSR
	BCS	1$		;NOT MOVING
	LDA	MSTRT
	STA	YPOS(X)
	JMP	MVP1LP
1$:
	LDA	#0
	STA	ELVOCC
2$:
;
	LDA	#1
	STA	AI2+P1NUM	;SET BEE BIT SO THEY COME
	JMP	P1LVTR
;
;*********************************************************************
;
GR019:
;LOGIC FOR TREES
;
	LDY	GSPEED
	LDA	TRSPD(Y)
	STA	TEM0+2
	STA	TEM0+3
;
;
TREBEE:
;
	CPX	ELVOCC
	BNE	2$		;NOT ME
	LDA	ELVATR
	AND	#0F
	BEQ	1$		;OFF
	LSR
	BCS	1$		;NOT MOVING
	LDA	MSTRT
	STA	YPOS(X)
	JMP	MVP1LP
;
1$:
	LDA	#0
	STA	ELVOCC
;
2$:
;
10$:
;
	LDA	YPOS
	LSR
	SEC
	SBC	YPOS(X)
	BCS	5$		;TREE BELOW
	LDA	#0FF
	BMI	7$
5$:
	BEQ	7$		;NO MOVE
	LDA	#1
7$:
	STA	TEM0+4
;
30$:
;TIME FOR X MOVE?
	LDA	XPOS
	SEC
	SBC	XPOS(X)
	BCS	50$		;TREE TO LEFT
	CMP	#0FC		;(<3?
	BCC	60$		;OKAY
;TREE CLOSE, SLOW DOWN
	LDA	TEM0+3
	CMP	#10
	BCC	45$		;SPEED OKAY
	LDA	#0F
	STA	TEM0+3
45$:
	LDA	#0FF
	BMI	60$		;JMP
50$:
	BEQ	60$		;NO MOVE
	CMP	#3
	BCS	60$		;HIGH ENOUGH
;TREE CLOSE, SLOW IT DOWN
	LDA	TEM0+3
	CMP	#10
	BCC	60$		;OKAY
	LDA	#0F
	STA	TEM0+3
60$:
	STA	TEM0+5
;
	JMP	BLMV
;
;
TRSPD:
	.BYTE	30,2F,2E,2D,2C,2B,2A,29
	.BYTE	28,27,26,25,24,23,22,21
	.BYTE	20,1F,1E,1D,1C,1B,1A,19
	.BYTE	18,17,16,15,14,13,12,11
;
	.BYTE	38,37,36,35,34,33,32,31
	.BYTE	30,2F,2E,2D,2C,2B,2A,29
	.BYTE	28,27,26,25,24,23,22,21
	.BYTE	20,1F,1E,1D,1C,1B,1A,19
;
;*********************************************************************
;
GR01B:
;LOGIC FOR STUNNED TREES
;
	INC	AI2(X)		;TIMER FOR STUN
	LDA	AI2(X)
	LDY	GSPEED
	CMP	TRSTTM(Y)
	BCC	20$
;STUN OVER
	LDA	#NXTREE
	STA	GRFX(X)
;FIX AI BITS
;
20$:
;
	CPX	ELVOCC
	BNE	2$		;NOT ME
	LDA	ELVATR
	AND	#0F
	BEQ	1$		;OFF
	LSR
	BCS	1$		;NOT MOVING
	LDA	MSTRT
	STA	YPOS(X)
	JMP	MVP1LP
1$:
	LDA	#0
	STA	ELVOCC
2$:
	JMP	P1LVTR
;
;
TRSTTM:
	.BYTE	0B,0B,0B,0C,0C,0C,0D,0D
	.BYTE	0E,0E,0F,0F,10,10,11,11
	.BYTE	12,12,13,13,14,14,15,15
	.BYTE	16,16,17,17,18,18,19,19
;
	.BYTE	7,7,7,7,8,8,8,8
	.BYTE	9,9,9,9,0A,0A,0A,0A
	.BYTE	0B,0B,0B,0B,0C,0C,0D,0D
	.BYTE	0E,0E,0F,0F,10,10,11,11
;
;*********************************************************************
;
GR022:
;LOGIC FOR SCORE
	DEC	AI2(X)		;TIMER
	BNE	20$		;WAIT
;MAKE IT GO AWAY
	LDA	#0E0
	STA	YPOS(X)		;OFFSCREEN
	LDA	#0
	STA	INCTBL(X)
	JMP	MVP1LP
;
20$:
;
	CPX	ELVOCC
	BNE	2$		;NOT ME
	LDA	ELVATR
	AND	#0F
	BEQ	1$		;OFF
	LSR
	BCS	1$		;NOT MOVING
	LDA	MSTRT
	STA	YPOS(X)
	JMP	MVP1LP
1$:
	LDA	#0
	STA	ELVOCC
2$:
	JMP	P1LVTR
;
;
;*********************************************************************
;
GR01C:
;LOGIC FOR BOILING POT
;
	JMP	MVP1LP
;
GR01E:
;LOGIC FOR SKELETON
	CPX	ELVOCC
	BNE	2$		;NOT ME
	LDA	ELVATR
	AND	#0F
	BEQ	1$		;OFF
	LSR
	BCS	1$		;NOT MOVING
	LDA	MSTRT
	STA	YPOS(X)
	JMP	MVP1LP
1$:
	LDA	#0
	STA	ELVOCC
2$:
;CHECK STATUS
	LDY	AI1(X)
	LDA	AI2(X)
	CMP	#3
	BCC	20$
;NEW DIRECTION
	LDA	SEEDL
	EOR	INTIM
	AND	#3
	STA	AI1(X)
;DONT LET HIM REVERSE
	STY	TEM0+1
	EOR	#2
	CMP	TEM0+1
	BNE	10$
	EOR	#2
	STA	AI1(X)
;
10$:
	LDA	#0
	STA	AI2(X)
;
20$:
	LDY	GSPEED
	LDA	WTCSPD(Y)
	STA	TEM0+2
;
	JMP	GEMV0
;
;
;*********************************************************************
;
;
SPDCLX:
	LDA	TEM0+2		;SPEED
	LSR
	LSR
	LSR
	LSR
	AND	#3
	STA	TEM0+6
	LDA	TEM0+2		;SPEED
	AND	#0F		;40-4F
	ORA	#40
	TAX
	JMP	SPDCLC
;
SPDCLY:
	LDA	TEM0+2
	AND	#3F
	TAX
	LDA	#0
	STA	TEM0+6
;
SPDCLC:
	LDA	YFRCTB(X)
	LSR
	TAY
	AND	#7C
	AND	FRMCTR
	BCS	60$		;>1/2
;DO EVERY 4TH
	BNE	50$		;DONT MOVE
	TYA
	AND	#3		;TYPE OF COUNT
	BEQ	20$		;MOVE IT
	TAY
	LDA	CNT3-1(Y)
	BEQ	20$		;MOVE IT
	BNE	50$		;JMP DONT MOVE
;
60$:
;ALL BUT EVERY 4TH
	BNE	20$		;MOVE IT
	TYA
	AND	#3		;TYPE OF COUNT
	BEQ	50$		;DONT MOVE
	TAY
	LDA	CNT3-1(Y)
	BNE	20$		;MOVE IT
50$:
;DONT MOVE
	CLC
	BCC	30$		;JMP
20$:
;MOVE IT
	SEC
;
30$:
	LDX	TEM0		;RESTORE INDEX
	LDA	TEM0+6
	ADC	#0		;NOW=POSITIVE # PIXELS TO MOVE
;FLAGS ARE SET
;
	RTS
;
;
;*********************************************************************
;
P1MV:
;
	LDA	#FTCRT3&0FF
	STA	TMP
	LDA	#FTCRT3&0FF00/100
	STA	TMP+1
;
	LDA	TEM0+1
	BNE	5$
	JMP	P1LVTR
5$:
	AND	#1
	BEQ	NOMVY		;NO Y MOVE
	LDA	TEM0+4
	CLC
	ADC	YPOS(X)
	STA	TEM0+2		;Y TMP
	TAY
	LDA	XPOS(X)
	LSR
	LSR
	TAX			;X/4
	LDA	PF1OR2(X)	;PF1 OR PF2?
	BMI	11$		;USE PF2
	JMP	FTCH13
11$:
	JMP	FTCH23
FTCRT3:
	AND	PFBIT(X)	;IF 0 CAN MOVE
	BNE	30$		;CANT MOVE
	LDX	TEM0		;INDEX
	LDA	TEM0+1
	ORA	#2
	STA	TEM0+1
	BNE	P1MVX
30$:
NOMVY:
;DIDNT MOVE Y
	LDX	TEM0		;INDEX
	LDA	YPOS(X)
	STA	TEM0+2		;Y TMP
;
40$:
P1MVX:
;
	LDA	#FTCRT4&0FF
	STA	TMP
	LDA	#FTCRT4&0FF00/100
	STA	TMP+1
	LDA	TEM0+1
	AND	#4
	BEQ	NOMVX		;NO X MOVE
	LDA	TEM0+5
	CLC
	ADC	XPOS(X)
	STA	TEM0+3		;X TMP
	LSR
	LSR
	TAX			;X/4
	LDY	TEM0+2		;Y TMP
	LDA	PF1OR2(X)	;PF1 OR PF2?
	BMI	51$		;USE PF2
	JMP	FTCH13
51$:
	JMP	FTCH23
FTCRT4:
	AND	PFBIT(X)	;IF 0 CAN MOVE
	BNE	60$		;CANT MOVE!
	LDA	TEM0+1
	ORA	#8
	STA	TEM0+1
	BNE	YESMVX
60$:
NOMVX:
	LDX	TEM0		;INDEX
	LDA	XPOS(X)
	STA	TEM0+3		;DIDNT MOVE IN X
70$:
YESMVX:
	LDX	TEM0
	LDA	TEM0+1
	AND	#0A
	BEQ	P1CNFL
;
;
;MOVED, CHECK FOR CONFLICTS
;
P1CHEK:
;
;CHECK AGAINST BEES
	LDX	TEM0
	CPX	#P1NUM
	BEQ	65$
	LDA	YPOS+P1NUM
	CMP	TEM0+2		;Y TMP
	BNE	65$		;NO COMFLICT
;ONLY CONFLICT IF THEY ARE ON THE GROUND
	LDA	AI1+P1NUM	;BEE ZPOS
	BNE	65$		;OFF GROUND
;CHECK XPOS
;	SEC
	LDA	XPOS+P1NUM
	SBC	TEM0+3		;X TMP
	BPL	62$		;<XPOS+P1NUM
	EOR	#0FF
62$:
	CMP	#4		;BEE WIDTH
	BCC	P1CNFL		;CONFLICT!
65$:
;CHECK AGAINST HAT
	LDX	#P1NUM		;TO DO HAT
	LDA	STATE		;IS HE WEARING IT?
	LSR
	BCC	72$		;NOPE CHECK CONFLICT
;CHECK AGAINST ALL OTHER P1 INCS
	DEX
72$:
	DEX
	BEQ	P1MVOK		;DONE, NO CONFLICT
	CPX	TEM0		;CURRENT INDEX
	BEQ	72$		;DONT COMPARE AGAINST YOURSELF
;
	LDA	TEM0+2		;Y TMP
	CMP	YPOS(X)
	BNE	72$		;NO CONFLICT
;CHECK XPOS
;	SEC
	LDA	XPOS(X)
	SBC	TEM0+3		;X TMP
	BPL	74$		;<XPOS(X)
	EOR	#0FF
74$:
	CMP	#4		;P1 WIDTH
	BCS	72$		;NO CONFLICT!
;
;
P1CNFL:
;CONFLICT HERE
	LDX	TEM0
	LDY	GRFX(X)
	CPY	#4
	BCS	P1LVTR
	JMP	GECNFL
;
P1MVOK:
;NO CONFLICTS
	LDX	TEM0		;INDEX
	LDA	TEM0+3		;X TMP
	STA	XPOS(X)
	LDA	TEM0+2		;Y TMP
	STA	YPOS(X)
95$:
P1LVTR:
;CHECK FOR ELEVATOR
	LDA	ELVATR
	AND	#0F
	BEQ	99$		;OFF
	LSR
	BCC	99$
	LDA	XPOS(X)
	ADC	#4
	SBC	MHOR
	CMP	#4
	BCS	99$		;NOT LINED UP
	LDA	YPOS(X)
	CMP	MSTRT
	BNE	99$		;NOT LINED UP
;PUT HIM ON
	STX	ELVOCC
;
;
99$:
	JMP	MVP1LP
;
;*********************************************************************
                                                                                                                                                  