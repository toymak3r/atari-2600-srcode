;*********************************************************************
;
; KERNEL FOR CRYSTAL CASTLES
;
;*********************************************************************
;
;
;
;
;*********************************************************************
;
;
;	.=.&0FF00+100
;	NOP
;
P13ACT:
;
;CYCLE 26 HERE
	LDY	P1LINE		;(29)
	BNE	OUT1		;(31 OR 32) P1 STILL GOING
;
	STY	GRP1		;(34) P1 OFF
	STY	P10		;(37)
	LDX	P1NDX		;(40)
	LDY	INCTBL(X)	;(44)
	STY	P1NDX		;(47)
	LDX	XPOS(Y)		;(51)
	LDA	HPOS(X)		;(55)
	STA	P1TMP		;(58)
	AND	#0F		;(60)
	TAX			;(62)
	LDA	HP11RS(X)	;(66)
	STA	ODDLIN		;(69)
	LDY	LINE		;(72)
	JMP	P02LIN		;(75)
OUT1:
	BNE	P13ON		;(35) JMP
P13WAT:
	LDY	A,P1LINE	;(30)
	CPY	P1TMP		;(33) THIS HAS LINE-YPOS(X)
	BNE	OUT4		;(35 OR 36) NOT YET TIME
P13ON:
	LDA	@P10PTR(Y)	;(40)
	STA	P10		;(43)
	LDA	@P11PTR(Y)	;(48)
	STA	GRP1		;(51)
	LDA	@P1CPTR(Y)	;(56)
	DEC	P1LINE		;(61)
	LDY	#P11ACT&0FF	;(63)
	STY	ODDLIN		;(66)
	LDY	LINE		;(69)
	STA	COLUP1		;(72)
	JMP	P02LIN		;(75)
;
OUT4:
	JMP	OUT3		;(39)
;
P13RS2:
	STA	RESP1		;(29)
	LDX	#P11SET&0FF	;(31)
	JMP	R3S34		;(34)
P13RS3:
	JMP	OUT5		;(29)
P13RS4:
	JMP	OUT6		;(29)
P13RS5:
	JMP	OUT7		;(29)
P13RS6:
	JMP	OUT8		;(29)
P13RS7:
	JMP	OUT9		;(29)
P13RS8:
	JMP	OUT10		;(29)
P13RS9:
	JMP	OUT11		;(29)
;
;
P13SET:
	JMP	OUT2		;(29)
;
;*********************************************************************
;
;
P11ACT:
;
;CYCLE 26 HERE
	LDY	P1LINE		;(29)
	BNE	OUT21		;(31 OR 32) P1 STILL GOING
;
	STY	GRP1		;(34) P1 OFF
	STY	P10		;(37)
	LDX	P1NDX		;(40)
	LDY	INCTBL(X)	;(44)
	STY	P1NDX		;(47)
	LDX	XPOS(Y)		;(51)
	LDA	HPOS(X)		;(55)
	STA	P1TMP		;(58)
	AND	#0F		;(60)
	TAX			;(62)
	LDA	HP13RS(X)	;(66)
	STA	ODDLIN		;(69)
	LDY	LINE		;(72)
	JMP	@EVNLIN		;(1)
OUT21:
	BNE	P11ON		;(35) JMP
P11WAT:
	LDY	A,P1LINE	;(30)
	CPY	P1TMP		;(33) THIS HAS LINE-YPOS(X)
	BNE	OUT24		;(35 OR 36) NOT YET TIME
P11ON:
	LDA	@P10PTR(Y)	;(40)
	STA	P10		;(43)
	LDA	@P11PTR(Y)	;(48)
	STA	GRP1		;(51)
	LDA	@P1CPTR(Y)	;(56)
	DEC	P1LINE		;(61)
	LDY	#P13ACT&0FF	;(63)
	STY	ODDLIN		;(66)
	LDY	LINE		;(69)
	STA	COLUP1		;(72)
	JMP	@EVNLIN		;(1)
;
OUT24:
	JMP	OUT23		;(39)
;
;
P11RS2:
	STA	RESP1		;(29)
	LDX	#P13SET&0FF	;(31)
	JMP	R1S34		;(34)
P11RS3:
	JMP	OUT25		;(29)
P11RS4:
	JMP	OUT26		;(29)
P11RS5:
	JMP	OUT27		;(29)
P11RS6:
	JMP	OUT28		;(29)
P11RS7:
	JMP	OUT29		;(29)
P11RS8:
	JMP	OUT30		;(29)
P11RS9:
	JMP	OUT31		;(29)
;
;
P11SET:
	JMP	OUT22		;(29)
;
;
;*********************************************************************
;
;
;
;
;
OUT2:
	STA	HMCLR		;(32)
	LDX	P1NDX		;(35)
	LDY	GRFX(X)		;(39)
	LDA	P1HGT(Y)	;(43)
	STA	P1LINE		;(46)
	LDA	P1PTL(Y)	;(50)
OUT12:	STA	P10PTR		;(53)
	STA	P11PTR		;(56)
	STA	P1CPTR		;(59)
	LDA	#P11WAT&0FF	;(61)
	DEC	P1TMP		;(66)
	STA	ODDLIN		;(69)
	LDY	LINE		;(72)
	JMP	P02LIN		;(75)
;
;
;
OUT3:
;HERE WEE ARE WAITING FOR P1 START SO WE FIX P1TMP WITH
;LINE-YPOS(X)
	INC	ODDLIN		;(44) WASTE 5
	LDA	P10PTR		;(47)
	JMP	OUT12		;(50) CONVENIENTLY ENDS THE SAME WAY
;
;	LDX	#P11SET&0FF
;	STX	ODDLIN
;	LDY	P1TMP
;	STY	HMP1
;	LDA	LINE
;	TAY
;	LDX	P1NDX
;	SEC
;	SBC	YPOS(X)
;	STA	P1TMP
;	JMP	P02LIN
OUT5:
	LDX	#P11SET&0FF	;(31)
	STA	RESP1		;(34)
R3S34:
	LDY	P1TMP		;(37)
	STX	ODDLIN		;(40)
	JMP	R3S43		;(43)
OUT6:
	LDY	P1TMP		;(32)
	LDX	#P11SET&0FF	;(34)
	SEC			;(36)
	STA	RESP1		;(39)
	STX	A,ODDLIN	;(43)
R3S43:
	STY	HMP1		;(46)
	JMP	R3S49		;(49)
OUT7:
	LDX	#P11SET&0FF	;(31)
	STX	ODDLIN		;(34)
	LDY	P1TMP		;(37)
	STY	A,HMP1		;(41)
	STA	RESP1		;(44)
	SEC			;(46)
	BCS	R3S49		;(49)
OUT8:
	LDX	#P11SET&0FF	;(31)
	STX	ODDLIN		;(34)
	LDY	P1TMP		;(37)
	STY	A,HMP1		;(41)
	INC	SEEDH		;(46)
	STA	RESP1		;(49)
R3S49:
	LDA	LINE		;(52)
	TAY			;(54)
	SEC			;(56)
	BCS	R3S59		;(59)
OUT9:
	LDX	#P11SET&0FF	;(31)
	STX	ODDLIN		;(34)
	LDY	P1TMP		;(37)
	STY	HMP1		;(40)
	LDA	LINE		;(43)
	TAY			;(45)
	NOP			;(47)
	NOP			;(49)
	NOP			;(51)
	STA	RESP1		;(54)
	SEC			;(56)
	BCS	R3S59		;(59)
OUT10:
	LDX	#P11SET&0FF	;(31)
	STX	ODDLIN		;(34)
	LDY	P1TMP		;(37)
	STY	HMP1		;(40)
	LDA	LINE		;(43)
	TAY			;(45)
	SEC			;(47)
	NOP			;(49)
	NOP			;(51)
	INC	SEEDH		;(56)
	STA	RESP1		;(59)
R3S59:
	LDX	P1NDX		;(62)
	SBC	YPOS(X)		;(66)
	STA	P1TMP		;(69)
	JMP	R3S72		;(72)
OUT11:
	LDX	#P11SET&0FF	;(31)
	STX	ODDLIN		;(34)
	LDY	P1TMP		;(37)
	STY	HMP1		;(40)
	LDA	LINE		;(43)
	TAY			;(45)
	SEC			;(47)
	LDX	A,P1NDX		;(51)
	INC	SEEDH		;(56)
	INC	SEEDH		;(61)
	STA	RESP1		;(64)
	SBC	YPOS(X)		;(68)
	STA	A,P1TMP		;(72)
R3S72:
	JMP	P02LIN		;(75)
;
;
;
OUT22:
	STA	HMCLR		;(32)
	LDX	P1NDX		;(35)
	LDY	GRFX(X)		;(39)
	LDA	P1HGT(Y)	;(43)
	STA	P1LINE		;(46)
	LDA	P1PTL(Y)	;(50)
OUT32:	STA	P10PTR		;(53)
	STA	P11PTR		;(56)
	STA	P1CPTR		;(59)
	LDA	#P13WAT&0FF	;(61)
	DEC	P1TMP		;(66) ADJUST FOR NEXT LINE
	STA	ODDLIN		;(69)
	LDY	LINE		;(72)
	JMP	@EVNLIN		;(1)
;
;
;
OUT23:
;HERE WEE ARE WAITING FOR P1 START SO WE FIX P1TMP WITH
;LINE-YPOS(X)
	INC	ODDLIN		;(44)
	LDA	P10PTR		;(47)
	JMP	OUT32		;(50) CONVENIENTLY ENDS THE SAME WAY
;
;
;	LDX	#P13SET&0FF
;	STX	ODDLIN
;	LDY	P1TMP
;	STY	HMP1
;	LDA	LINE
;	TAY
;	LDX	P1NDX
;	SEC
;	SBC	YPOS(X)
;	STA	P1TMP
;	JMP	@EVNLIN
OUT25:
	LDX	#P13SET&0FF	;(31)
	STA	RESP1		;(34)
R1S34:
	LDY	P1TMP		;(37)
	STX	ODDLIN		;(40)
	JMP	R1S43		;(43)
OUT26:
	LDY	P1TMP		;(32)
	LDX	#P13SET&0FF	;(34)
	SEC			;(36)
	STA	RESP1		;(39)
	STX	A,ODDLIN	;(43)
R1S43:
	STY	HMP1		;(46)
	JMP	R1S49		;(49)
OUT27:
	LDX	#P13SET&0FF	;(31)
	STX	ODDLIN		;(34)
	LDY	P1TMP		;(37)
	STY	A,HMP1		;(41)
	STA	RESP1		;(44)
	SEC			;(46)
	BCS	R1S49		;(49)
OUT28:
	LDX	#P13SET&0FF	;(31)
	STX	ODDLIN		;(34)
	LDY	P1TMP		;(37)
	STY	A,HMP1		;(41)
	INC	SEEDH		;(46)
	STA	RESP1		;(49)
R1S49:
	LDA	LINE		;(52)
	TAY			;(54)
	SEC			;(56)
	JMP	R1S59		;(59)
OUT29:
	LDX	#P13SET&0FF	;(31)
	STX	ODDLIN		;(34)
	LDY	P1TMP		;(37)
	STY	HMP1		;(40)
	LDA	LINE		;(43)
	TAY			;(45)
	NOP			;(47)
	NOP			;(49)
	NOP			;(51)
	STA	RESP1		;(54)
	SEC			;(56)
	JMP	R1S59		;(59)
OUT30:
	LDX	#P13SET&0FF	;(31)
	STX	ODDLIN		;(34)
	LDY	P1TMP		;(37)
	STY	HMP1		;(40)
	LDA	LINE		;(43)
	TAY			;(45)
	SEC			;(47)
	NOP			;(49)
	NOP			;(51)
	INC	SEEDH		;(56)
	STA	RESP1		;(59)
R1S59:
	LDX	P1NDX		;(62)
	SBC	YPOS(X)		;(66)
	STA	P1TMP		;(69)
	JMP	R1S72		;(72)
OUT31:
	LDX	#P13SET&0FF	;(31)
	STX	ODDLIN		;(34)
	LDY	P1TMP		;(37)
	STY	HMP1		;(40)
	LDA	LINE		;(43)
	TAY			;(45)
	SEC			;(47)
	LDX	A,P1NDX		;(51)
	INC	SEEDH		;(56)
	INC	SEEDH		;(61)
	STA	RESP1		;(64)
	SBC	YPOS(X)		;(68)
	STA	A,P1TMP		;(72)
R1S72:
	JMP	@EVNLIN		;(75)
;
;
;
;*********************************************************************
;
;
;P0 LINE KERNELS
;
;
;*********************************************************************
;
;KERNEL HERE GOES
;FROM LINE = 3E TO 0
;P13LINE X ODD
;P02LINE X ODD (DOTS HERE)
;P11LINE X EVEN (ENTER KERNEL HERE)
;P00LINE X EVEN (P0 START AND EXIT LINE HERE)
;
;THIS PART OF THE KERNEL HANDLES P0 YPOS(P0STRT) OF 77 (3E) TO 10 (0C)
;
;
EVNKRN:
;
;
P02LIN:
;
;CYCLE 75 HERE
	LDX	P02		;(2)
	STX	GRP0		;(5)
	LDA	@PF1PTR(Y)
	STA	P02		;TEMP
	ORA	DOTR0(Y)
	STA	PF1		;(20) A BIT EARLY
	LDA	@PF2PTR(Y)
	TAX
	ORA	DOTR2(Y)
	STA	PF2		;(34)
	DEC	LINE		;(39)
	TXA
	ORA	DOTR3(Y)
	STA	PF2		;(48) ON TIME
	LDA	P02		;TEMP
	ORA	DOTR1(Y)
	STA	PF1		;(58) EARLY
	STX	PF2		;(61) EARLY ENOUGH?
	LDX	P02		;TEMP
	LDA	P10		;NEXT P1
	STX	PF1		;(70)
;
	LDX	P01		;(73)
	STA	GRP1		;(76)
;
	STA	HMOVE
	STX	GRP0
;
;MISSILE NOW
	CPY	MEND		;(9)
	BEQ	OUT42		;(11 OR 12) TURN ON ELEVATOR
	CPY	MSTRT
	BNE	OUT43		;(16 OR 17) DONT DO ANYTHING
	LDA	#2
	STA	ENABL		;ON
;
	JMP	@ODDLIN		;(26)
;
;
;
;
;*********************************************************************
;
;	.=.&0FF00+100
;
;THESE ARE THE CASES THAT DO P0 STARTUP AND NORMAL PLAYFIELD
;
;	NOP
;
P0ACT:
;
;CYCLE 1 HERE
;
	LDA	P00		;(4)
	STA	GRP0
	LDA	@PF1PTR(Y)
	STA	PF1
	LDA	@PF2PTR(Y)
	STA	PF2		;(23)
	LDY	P0LINE		;(26)
	BMI	P0END		;(28 OR 29) NO MORE P0
	LDA	@P00PTR(Y)
	STA	P00
	LDA	@P01PTR(Y)
	STA	P01
	LDA	@P02PTR(Y)
	STA	P02
	LDA	@P03PTR(Y)
	DEC	P0LINE		;(62)
OUT00:
;
	LDY	LINE		;(65)
OUT03:
	DEC	LINE
	LDX	P10
	STX	GRP1		;(76)
	STA	HMOVE
	STA	GRP0		;(6)
	CPY	MEND		;(9)
	BEQ	OUT42		;(11 OR 12) END OF MISSILE
	CPY	MSTRT
	BNE	OUT43		;(16 OR 17) DO NOTHIG
	LDA	#2
	STA	ENABL
	JMP	@ODDLIN		;(26)
;
;
OUT42:
	LDA	#0
	STA	ENABL
OUT43:
	NOP
	NOP
	JMP	@ODDLIN		;(26)
;
;
;*********************************************************************
;
;
;INACTIVE P0 HERE
;
P0INAC:
;CYCLE 1 HERE
	LDA	P00		;(4)
	STA	GRP0		;(7)
	LDA	@PF1PTR(Y)
	STA	PF1
	LDA	@PF2PTR(Y)
	STA	A,PF2		;(24)
	LDY	LINE		;(27)
	CPY	P0STRT		;(30)
	BNE	OUT02		;(32 OR 33) NOT YET READY
	LDX	#P0ACT&0FF	;(34)
	LDA	#P0HGT/4	;(36)
	STA	A,P0LINE	;(40) SAME AS P0STRT
OUT01:
	LDA	#0		;(42)
	STA	P02		;(45)
	STA	P01		;(48)
	STA	P00		;(51)
	LDY	LINE		;(54)
	CPY	ENDLIN		;(57)
	BEQ	ENDPFK		;(59 OR 60)
	STX	EVNLIN		;(62)
	BNE	OUT03		;(65) JMP
;
P0END:
	NOP			;(31)
	NOP			;(33)
OUT02:
	LDX	#P0INAC&0FF	;(35)
	NOP			;(37)
	BNE	OUT01		;(40) JMP
;
;
;*********************************************************************
;
;
;*********************************************************************
;
;
ENDPFK:
	STA	WSYNC
	LDA	#0
	STA	GRP0
	STA	GRP1
	STA	GRP0
	STA	ENABL
	STA	REFP0
	LDX	#FINPFK&0FF
	STX	JMP
	LDX	#FINPFK&0FF00/100
	STX	JMP+1		;(27)
	LDX	#10
	STX	HMP1
	LDX	#3
	STX	NUSIZ0		;(37)
	STA	RESP0		;(40)
	STA	RESP1		;(43)
	STX	NUSIZ1
	STX	VDELP1
	STX	VDELP0
	JMP	SWBN02		;(55)
;
;
;
;*********************************************************************
;
KRN0:
;
	LDX	#0
	STA	WSYNC
	STA	HMOVE
	STX	VBLANK
	STX	P00
	STX	P01
	LDA	#TOPLIN
	STA	LINE
;HERE IS THE TOP KERNEL WHICH STARTS OUT ON P1 LINES
;AND EXITS TO P11LIN
;
KLOOP:
	LDY	P1LINE
	BEQ	1$		;P1 DONE TURN OFF
	CPY	P1TMP		;HEAD POS
	BNE	1$		;P1 MUST STILL BE OFF
;P1 ON
	LDA	@P10PTR(Y)
	STA	P10
	LDA	@P11PTR(Y)
	STA	GRP1
	LDA	@P1CPTR(Y)
	DEC	P1LINE
	LDY	#P11ACT&0FF	;INDICATES STARTED UP
	STY	ODDLIN
;
	STA	WSYNC
	STA	COLUP1
	BNE	2$		;JMP TO P0 STUFF
;
1$:
	LDY	#0
	STY	GRP1		;TURN HIM OFF
	STY	P10		;OFF
	STA	WSYNC
;
2$:
	DEC	P1TMP		;START CALC FOR NEXT LINE
	LDA	LINE
	AND	#1		;ODD OR EVEN?
	BNE	10$		;DOT LINE (NO DOTS)
;
	LDA	P00
	STA	GRP0
	LDA	#P0ACT&0FF
	CMP	EVNLIN		;HAS P0 BEEN STARTED YET?
	BNE	3$		;NO, STILL WAITING
;P0 WAS STARTED (OR AT LEAST READIED) EARLIER
	LDY	P0LINE
	BMI	4$		;DONE WITH P0
	LDA	@P00PTR(Y)
	STA	P00
	LDA	@P01PTR(Y)
	STA	P01
	LDA	@P02PTR(Y)
	STA	P02
	LDA	@P03PTR(Y)
	DEC	P0LINE
6$:
	STA	WSYNC
	STA	HMOVE
	DEC	LINE
	LDX	P10
	STX	GRP1
	STA	GRP0
	JMP	KLOOP		;DO P1
;
3$:
;STILL WAITING FOR START
	LDY	P0STRT
	CPY	LINE
	BNE	5$		;NOT YET
;START P0
	STA	EVNLIN		;STARTED
	LDA	#P0HGT/4
	STA	P0LINE
5$:
;KEEP P0 OFF
4$:
;TURN P0 OFF
	LDA	#0
	STA	P02
	STA	P01
	STA	P00
	BEQ	6$		;JMP
;
;
10$:
;THIS IS A DOTLIN WITH NO DOTS
	LDA	P02
	STA	GRP0
	LDA	P10
;TIME THIS FOR ENTRY TO P11LIN
	STA	GRP1
	LDX	P01
;
	STA	WSYNC
	STA	HMOVE		;(3)
	STX	GRP0		;(6)
	DEC	LINE		;(11)
	LDY	LINE		;(14)
	CPY	#3E		;(16) LINE # FOR TOP OF DOT KERNEL
	BEQ	15$		;(18 OR 19)
	JMP	KLOOP		;DO P1 AGAIN
15$:
;
	NOP			;(21)
	JMP	@ODDLIN		;(26)
;STARTS ON EVEN LINE P11
;
;
;
;*********************************************************************
;
HP11RS:
	.BYTE	P11WAT&0FF
	.BYTE	P11WAT&0FF
	.BYTE	P11RS2&0FF
	.BYTE	P11RS3&0FF
	.BYTE	P11RS4&0FF
	.BYTE	P11RS5&0FF
	.BYTE	P11RS6&0FF
	.BYTE	P11RS7&0FF
	.BYTE	P11RS8&0FF
	.BYTE	P11RS9&0FF
	.BYTE	P11WAT&0FF
	.BYTE	P11WAT&0FF
	.BYTE	P11WAT&0FF
	.BYTE	P11WAT&0FF
	.BYTE	P11WAT&0FF
	.BYTE	P11WAT&0FF
HP13RS:
	.BYTE	P13WAT&0FF
	.BYTE	P13WAT&0FF
	.BYTE	P13RS2&0FF
	.BYTE	P13RS3&0FF
	.BYTE	P13RS4&0FF
	.BYTE	P13RS5&0FF
	.BYTE	P13RS6&0FF
	.BYTE	P13RS7&0FF
	.BYTE	P13RS8&0FF
	.BYTE	P13RS9&0FF
	.BYTE	P13WAT&0FF
	.BYTE	P13WAT&0FF
	.BYTE	P13WAT&0FF
	.BYTE	P13WAT&0FF
	.BYTE	P13WAT&0FF
	.BYTE	P13WAT&0FF
                                                                                                                