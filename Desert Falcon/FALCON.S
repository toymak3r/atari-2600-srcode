	.6502
	.TITLE "DESERT FALCON-BOB POLARO-09/17/87"
;
;	************ FINAL NTSC ************
;
;	COPYRIGHT 1987 ATARI
;
;  PIA AND TIMER (6532) LOCATIONS
;
SWCHA	= $280	;P0,P1 JOYSTICKS
; LEFT RITE
; 7654 3210
; ---- ----
; R RLDU
;
SWCHB	= $282	;CONSOLE SWITCHES
; D7-RITE DIFFICULTY
; D6-LEFT DIFFICULTY
; D5/D4 NOT USED
; D3-COLOR - B/W
; D2-NOT USED
; D1-GAME SELECT
; D0-GAME RESET
;
CTLSWA  = $281	;SWCHA DATA DIRECTION (0=INPUT)
CTLSWB  = $283	;SWCHB DATA DIRECTION (0=INPUT)
;
INTIM	= $284	;INTERVAL TIMER READ
TIM1T	= $294	;SET 1    CLK INTERVAL (838   NSEC/INTERVAL)
TIM8T   = $295	;SET 8    CLK INTERVAL (6.7   USEC/INTERVAL)
TIM64T	= $296	;SET 64   CLK INTERVAL (53.6  USEC/INTERVAL)
T1024T	= $297	;SET 1024 CLK INTERVAL (858.2 USEC/INTERVAL)
;
;  STELLA (TIA) REGISTER ADDRESSES
;
VSYNC	= $00	; BIT	     1	VERTICAL SYNC SET-CLR
VBLANK	= $01	; BIT	     1	VERTICAL BLANK SET-CLR
WSYNC	= $02	; STROBE	WAIT FOR HORIZ BLANK
RSYNC	= $03	; STROBE	RESET HORIZ SYNC COUNTER
NUSIZ0	= $04	; BITS   54 210	NUMBER-SIZE PLAYER/MISSILE 0
NUSIZ1	= $05	; BITS   54 210	NUMBER-SIZE PLAYER/MISSILE 1
COLUP0	= $06	; BITS 7654321	COLOR(4)-LUM(3) PLAYER 0
COLUP1	= $07	; BITS 7654321	COLOR(4)-LUM(3) PLAYER 1
COLUPF	= $08	; BITS 7654321	COLOR(4)-LUM(3) PLAYFIELD
COLUBK	= $09	; BITS 7654321	COLOR(4)-LUM(3) BACKGROUND
CTRLPF	= $0A	; BITS 7 54 210	PLAYFIELD CONTROL
REFP0	= $0B	; BIT      3	REFLECT PLAYER 0
REFP1	= $0C	; BIT      3	REFLECT PLAYER 1
PF0	= $0D	; BITS 7654	PLAYFIELD REG BYTE 0
PF1	= $0E	; BITS ALL	PLAYFIELD REG BYTE 1
PF2	= $0F	; BITS ALL	PLAYFIELD REG BYTE 2
	.eject
RESP0	= $10	; STROBE	RESET PLAYER 0
RESP1	= $11	; STROBE	RESET PLAYER 1
RESM0	= $12	; STROBE	RESET MISSILE 0
RESM1	= $13	; STROBE	RESET MISSILE 1
RESBL	= $14	; STROBE	RESET BALL
AUDC0	= $15	; BITS	   3210	AUDIO CONTROL 0
AUDC1	= $16	; BITS	   3210	AUDIO CONTROL 1
AUDF0	= $17	; BITS	   3210	AUDIO FREQUENCY 0
AUDF1	= $18	; BITS	   3210	AUDIO FREQUENCY 1
AUDV0	= $19	; BITS	   3210	AUDIO VOLUME 0
AUDV1	= $1A	; BITS	   3210	AUDIO VOLUME 1
GRP0	= $1B	; BITS ALL	GRAPHICS FOR PLAYER 0
GRP1	= $1C	; BITS ALL	GRAPHICS FOR PLAYER 1
ENAM0	= $1D	; BIT	     1	ENABLE MISSILE 0
ENAM1	= $1E	; BIT	     1	ENABLE MISSILE 1
ENABL	= $1F	; BIT	     1	ENABLE BALL
HMP0	= $20	; BITS 7654	HORIZ MOTION PLAYER 0
HMP1	= $21	; BITS 7654	HORIZ MOTION PLAYER 1
HMM0	= $22	; BITS 7654	HORIZ MOTION MISSILE 0
HMM1	= $23	; BITS 7654	HORIZ MOTION MISSILE 1
HMBL	= $24	; BITS 7654	HORIZ MOTION BALL
VDELP0	= $25	; BIT	      0	VERTICAL DELAY PLAYER 0
VDELP1	= $26	; BIT	      0	VERTICAL DELAY PLAYER 1
VDELBL	= $27	; BIT	      0	VERTICAL DELAY BALL
RESMP0	= $28	; BIT	     1	RESET MISSILE TO PLAYER 0
RESMP1	= $29	; BIT	     1	RESET MISSILE TO PLAYER 1
HMOVE	= $2A	; STROBE	ACT ON HORIZ MOTION
HMCLR	= $2B	; STROBE	CLEAR ALL HM REGISTERS
CXCLR	= $2C	; STROBE	CLEAR COLLISION LATCHES
;
;  READ ADDRESSES -	BIT 7	BIT 6 (ONLY)
;
CXM0P	= $30	;	M0.P1	M0.P0
CXM1P	= $31	;	M1.P0	M1.P1
CXP0FB	= $32	;	P0.PF	P0.BL
CXP1FB	= $33	;	P1.PF	P1.BL
CXM0FB	= $34	;	M0.PF	M0.BL
CXM1FB	= $35	;	M1.PF	M1.BL
CXBLPF	= $36	;	BL.PF	NOT USED
CXPPMM	= $37	;	P0.P1	M0.M1
;
;  PADDLE INPUT PORTS..
;
INPT0	= $38	;
INPT1	= $39	;
INPT2	= $3A	;
INPT3	= $3B	;
;		 WHEN D6=0 IS WRITTEN TO VBLANK..
INPT4	= $3C	;D7=0 WHEN LEFT JOYSTICK FIREBUTTON PRESSED
INPT5	= $3D	;D7=0 WHEN RITE JOYSTICK FIREBUTTON PRESSED
	.eject
;	**** EQUATES ****
;
BANK0	= $5000
BANK1	= $7000
;
;BANK0	= $9000		;change to 9000 + b000 for eprom
;BANK1	= $B000
BANK2	= $D000
BANK3	= $F000
;
SARAH	= 0
;
;SARAH	= $80		; change to $80 for eprom
;
WHITE	= $FE
W	= WHITE
BLACK	= $00
RED	= $45
R	= RED
PINK	= $58
P	= PINK
DKRED	= $43
DR	= DKRED
BLUE	= $86
BL	= BLUE
LTBLUE	= $8C
LB	= LTBLUE
DKBRN	= $32
BROWN	= $35
B	= BROWN
LTBRN	= $28
DKBL	= $85
DB	= DKBL
GREY	= $03
GR	= GREY
MGREY	= $07
MG	= MGREY
LTGREY	= $0C
LG	= LTGREY
YELLOW	= $1C
Y	= YELLOW
GREEN	= $13
G	= GREEN
LTGRN   = $17
GOLD	= $1A
GD	= GOLD
;
SIZEP0	= 33
HOPSPD	= 3
MAXALN	= 14
MAXLIF	= 4
PTT	= 8
PTM	= 127
BONTMR	= $20
SCROLL1	= 99
RSTHP1  = 60
SWIM2	= 8
RUNNING2= 20
MAXHP0	= 83
MINHP0	= 32
MINVP	= 55
NOHIT	= 0
;
	.ORG $80
;		         
;		**** RAM VARIABLE ****
HP0:	.DS.B	1		;HORIZ POSITION PLAYER 0
HP1:	.DS.B	1		;HORIZ POSITION PLAYER 1
HPM0:	.DS.B	1		;HORIZ POSITION MISSILE 0
HPM1:	.DS.B	1		;HORIZ POSITION MISSILE 1
HPBL:	.DS.B	1		;HORIZ POSITION BALL
VENM:		
HSPH:	.DS.B	1		;HORIZ POSITION OF SPHINX
HPENM:	
SPANIM:	.DS.B	1		;SPHINX ANIMATION VALUE
TTIMER:	.DS.B	1		;TITLE SCREEN TIMER
VPOSP1:	
JEWEL:	.DS.B	1		;PICKED UP JEWEL
GAME:	.DS.B	1		;GAME NUMBER
RSTFLG:	.DS.B	1		;GAME RESET FLAG
POWER:	.DS.B	2		;POWER TYPE
SPSHOW:	.DS.B	1		;SCREEN FOR SPHINX
VPOSP0:	.DS.B	1		;VERTICAL POSITION PLAYER 0
VM0:	.DS.B	1		;VERTICAL POSITION OF MISSILE
SCROLL:	.DS.B	1		;SCROLL COUNTER
PWRTMR:	.DS.B	1		;POWER TIMER	
BONTIM:	.DS.B	1		;BONUS ROUND TIMER
BONUSR:	.DS.B	1		;BONUS ROUND FLAG
WAVE:	.DS.B	2		;WAVE NUMBERS
SCREEN:	.DS.B	2		;SCREEN NUMBER
STARTG:	.DS.B	1		;START GAME FLAG
HIEROC:	.DS.B	2		;HIERO COUNT
;
ALIND:	.DS.B	1		;ALIEN INDEX
ALIEN:	.DS.B	1		;ALIEN NUMBER
ALSHAD:	.DS.B	1		;ALIEN SHADOW HEIGHT
NEWLAK:	.DS.B	1		;LAKE NUMBER
COLP0T:	.DS.B	2		;COLOR FALCON POINTER
;
ATT:	.DS.B	1		;ATTRACT MODE FLAG 
TRIG:	.DS.B	1		;TRIGGER FLAG 
RANDH:	.DS.B	1
RAND:	.DS.B	1		;RANDOM NUMBER 
SPEED:	.DS.B	1		;GAME SPEED 
BUMP:	.DS.B	1		;BUMP SPEED FLAG
LKMASK:	.DS.B	1		;LAKE MASK
JMPPTR:
WORK:	.DS.B	1		;WORK VARIABLE 
WORK0:	.DS.B	1		;WORK VARIABLE0 
WORK1:	.DS.B	1		;WORK VARIABLE 1 
WORK2:	.DS.B	1		;WORK VARIABLE 2 
WORK3:	.DS.B	1		;WORK VARIABLE 3 
WORK4:	.DS.B	1
PLAY0:
RESTP0:	.DS.B	23
NUM00:	.DS.B	5
NUM11:	.DS.B	5
S0:	.DS.B	1		;SCORE DIGIT 0
S1:	.DS.B	1
S2:	.DS.B	1
S3:	.DS.B	1
NJEWL:	.DS.B	1
NEGG:	.DS.B	1
NBAR:	.DS.B	1
;
NEWOBE:	.DS.B	1		;NEW OBELISK FLAG
OBMASK:	.DS.B	1		;OBELISK MASK
LIVES0:	.DS.B	1		;NUMBER OF LIVES PLAYER 0
LIVES1:	.DS.B	1
PLAYER:	.DS.B	1		;PLAYER NUMBER
ALMASK:	.DS.B	1		;ALIEN MASK
SHADHT:	.DS.B	1		;SHADOW HEIGHT
HORDIR:	.DS.B	1		;HOR DIRECTION OF FALCON
VERDIR:	.DS.B	1		;VER DIRECTION OF FALCON
GPFAL:	.DS.B	1		;FALCON GRAPHICS POSITION
;
;SOUNDS 
DCH0:	   .DS.B      1
DCH1:	   .DS.B      1
TUNNUM:    .DS.B      1                      ;ONE BYTE--TUNE NUM FOR CHAN. 0
TUNNUM1:   .DS.B      1
TINDEX0:   .DS.B      1
TINDEX1:   .DS.B      1
CINDEX0:   .DS.B      1                      ;CONTROL INDEX
CINDEX1:   .DS.B      1
VINDEX0:   .DS.B      1                      ;VOLUME INDEX
VINDEX1:   .DS.B      1
VRUN:	   .DS.B      2
;
PTR0:	.DS.B	2
PTR1:	.DS.B	2
PTR2:	.DS.B	2
PTR3:	.DS.B	2
PTR4:	.DS.B	2
PTR5:	.DS.B	2
;
FRMCNT:	.DS.B	1
;
	.EJECT
	.ORG BANK0
;

	.ORG BANK0+$100
BEGIN0:
	LDA	#$FD
	STA	COLUPF
;
	SEI
	CLD
	LDX     #$FF 
	TXS      
	LDA     #0
LOOP:	STA     WSYNC,X 
	DEX      
	BNE     LOOP
	DEX
	STX	RAND
	STX	TUNT0
	STX	TUNT1
	STX	TUNNUM
	STX	TUNNUM+1
;
	LDA	#120
	STA	TTIMER
	JSR	RANDOM
	JSR	INITVR
	LDA	#$FF
	STA	ATT
	STA	STARTG
	LDA	#1
	STA	RSTFLG
	STA	GAME
NEWFRM:
	JSR	BEGFRM
	JSR	SETFAL
	JSR	SETOBE
	JSR	HORIZZ
;
LOOP1:	LDA	INTIM
	BNE	LOOP1
	STA	WSYNC
	STA	VBLANK
	STA	CXCLR
	LDA	ATT
	BEQ	NOTITL
	LDA	FRMCNT
	AND	#$03
	BNE	NOCHTI
	DEC	TTIMER
NOCHTI:
	LDA	TTIMER
	BMI	NOTITL
;
	LDA	#0
	STA	WSYNC
	STA	COLUBK
	LDA	#TITLEK&$FF
	STA	JMPPTR
	LDA	#TITLEK&$FF00/256
	STA	JMPPTR+1
	JMP	EXIT0
NOTITL:	
	JSR     MAINK		;MAIN KERNAL CALCULATIONS
;
	LDA	#SCOREK&$FF
	STA	JMPPTR
	LDA	#SCOREK&$FF00/256
	STA	JMPPTR+1
	JMP	EXIT0
OUTBK1:
	LDX	#$3E
	LDA	ATT
	BNE	IS3E
	INX
IS3E:
	STX	TIM64T
;
	LDA	RSTFLG
	CMP	#1
	BEQ	OUTBK3
	JSR	SETALN
	JSR	BONUSC
;
	LDA	#BANK1C&$FF
	STA	JMPPTR
	LDA	#BANK1C&$FF00/256
	STA	JMPPTR+1
	JMP	EXIT0	;JOYSTK
OUTBK2:
	LDA	#BANK2C&$FF
	STA	JMPPTR
	LDA	#BANK2C&$FF00/256
	STA	JMPPTR+1
	JMP	EXIT3
OUTBK3:
	LDA	#BANK3C&$FF
	STA	JMPPTR
	LDA	#BANK3C&$FF00/256
	STA	JMPPTR+1
	JMP	EXIT5
OUTBK4:
	JSR	SETLAK
	LDA	RSTFLG
	CMP	#1
	BNE	LOOP2
	JSR	INITVR
;
LOOP2:	LDA     INTIM 
	BNE     LOOP2
	STA	WSYNC 
	JMP     NEWFRM 
	.eject
;---------------------------------
BEGFRM:	LDA     #02
	STA     WSYNC
	STA	VBLANK
	STA	VSYNC
	STA	WSYNC
	STA	WSYNC
	LDX	#0
	STX	VSYNC
	LDA     #$1B
	STA     TIM64T 
	INC     FRMCNT		;INCREMENT FRAME COUNT	
;
RANDOM:
	LDA	RANDH
	ASL
	EOR	RANDH
	ASL
	ASL
	ROL	RAND
	ROL	RANDH
	ROL	RAND
	LDA	RAND
	RTS
;---------------------------------
;	MAIN KERNEL
;---------------------------------
;
MAINK:
	LDX	PLAYER
	LDA	WAVE,X
	AND	#$07
	TAY
	LDX	WAVECL,Y
	LDA	BONTIM
	CMP	#1
	BNE	NOCHBK
	LDA	FRMCNT
	ASL
	ASL
	TAX
NOCHBK:
	STA	WSYNC
	STX	COLUBK
;
	LDX	#147
	LDA	SCROLL
	CMP	#110
	BNE	NOSPH
	LDY	SPSHOW
	BEQ	NOSPH
;
	LDA	#SPHINX&$FF
	STA	JMPPTR
	LDA	#SPHINX&$FF00/256
	STA	JMPPTR+1
	JMP	EXIT0		;GOTO SPHINX BANK
;
OUTBK0:
	JSR	HORIZ1
	LDA	#124
	SEC
	SBC     HSPH
	TAX
;
	LDA	SPSHOW
	BPL	NOSPEC
	JSR	ALIEN2
	JMP	BOTKER2
NOSPEC:
	LDA	#34
	BNE	AFSPH
NOSPH:
	CMP	#127
	BCS	DOLK
NOLK:	CMP	#60
	BCC	DOAL
DOPY:
	SEC
	SBC	#60
AFSPH:
	JSR	TOPK	JSR	ALIENK
	JMP	BOTKER
DOLK:
	SEC
	SBC	#127
	JSR	TOPK
	JSR	PRMIDK
	JMP	BOTKER
DOAL:
	JSR	TOPK
NOSCRL:
	JSR	LAKEK
	JMP	BOTKER
;
OUTKER:
	RTS
LAKEK:
	LDA	COLLAK+SARAH
	STA	COLUP1
	LDA	LAKENZ+SARAH
	STA	NUSIZ1
	LDY	#46
LAKEL:
	TXA
	BEQ	OUTKER
	STY	WORK0
	STA	WSYNC
	LDA	PLAY1+SARAH,Y
	AND	LKMASK
	STA	GRP1
	TXA
	BEQ	OUTKER
	SEC
	SBC	VPOSP0
	CMP	#SIZEP0
	BCS	SKP00
	TAY
	LDA	PLAY0,Y
	STA	GRP0
	LDA	(COLP0T),Y
	STA	COLUP0
SKP00:
	DEX
	BEQ	OUTKER
	DEC	WORK0
	BMI	PRMIDK
	LDY	#0
	CPX	VM0
	BNE	NOM0
	DEY
NOM0:
	STY	ENAM0
	LDY	WORK0
	LDA	LAKHMT,Y
	STA	HMP1
	STA	WSYNC
	STA	HMOVE
	LDA	PLAY1+SARAH,Y
	AND	LKMASK
	STA	GRP1
	TXA
	SEC
	SBC	VPOSP0
	CMP	#SIZEP0
	BCS	SKP0
	TAY
	LDA	PLAY0,Y
	STA	GRP0
SKP0:
	LDY	WORK0
	DEX
	DEY
	BPL	LAKEL
PRMIDK:
	LDY	#64
PRMIDL:
	TXA
	BEQ	OUTKER
	SEC
	SBC	VPOSP0
	CMP	#SIZEP0
	STA	WSYNC
	STA	HMOVE
	DEX
	BEQ	OUTKER
	STY	WORK0
	TAY
	LDA	WORK2
	STA	NUSIZ1
	BCS	SKP01
	LDA	PLAY0,Y
	STA	GRP0
	LDA	(COLP0T),Y
	STA	COLUP0
SKP01:
	DEC	WORK0
	BMI	ALIENK
	LDY	#0
	CPX	VM0
	BNE	NOM1
	DEY
NOM1:
	STY	ENAM0
;
	TXA
	SEC
	SBC	VPOSP0
	CMP	#SIZEP0
	TAY
	STA	WSYNC
	BCS	SKP1
	LDA	PLAY0,Y
	STA	GRP0
SKP1:
	DEX
	LDY	WORK0
	LDA	(PTR0),Y
	STA	ENAM1
	STA	HMM1
	LDA	(PTR1),Y
	AND	OBMASK
	STA	WORK2
	LDA	CLRPYR,Y
	STA	COLUP1
	DEY
	BPL	PRMIDL
ALIENK:
	LDY	#60
	TXA
	BEQ	OUTK2
	LDA	#5
	STA	NUSIZ1
ALIENL:
	CPY	#15
	BCC	ALIEH
	CPY	#45
	BCS	ALIEH
	LDA	ALIENT+SARAH,Y
	STA	WSYNC
	STA	GRP1
	TXA
	SEC
	SBC	VPOSP0
	CMP	#SIZEP0
	STY	WORK0
	TAY
	BCS	SKP02
	LDA	PLAY0,Y
	STA	GRP0
	LDA	(COLP0T),Y
	STA	COLUP0
SKP02:
	DEX
	BEQ	OUTALK
	DEC	WORK0
	BMI	OUTALK
;
	LDY	WORK0
	LDA	#GREY
	CPY	ALSHAD
	BCC	NOSHA2
	LDA	ALMASK
NOSHA2:
	STA	COLUP1
	LDA	ALIENT+SARAH,Y
	STA	WSYNC
	STA	GRP1
	TXA
	SEC
	SBC	VPOSP0
	CMP	#SIZEP0
	TAY
	BCS	SKP2
	LDA	PLAY0,Y
	STA	GRP0
SKP2:
	LDY	#0
	CPX	VM0
	BNE	DOM4
	DEY
DOM4:
	STY	ENAM0
	LDY	WORK0
	DEX
	BEQ	OUTK2
	DEY
	BPL	ALIENL
OUTALK:
	JMP	LAKEK
OUTK2:
	RTS
;---------------------------
ALIEH:
	STA	WSYNC
	STA	HMOVE
	TXA
	SEC
	SBC	VPOSP0
	CMP	#SIZEP0
	STY	WORK0
	TAY
	BCS	SKP02H
	LDA	PLAY0,Y
	STA	GRP0
	LDA	(COLP0T),Y
	STA	COLUP0
SKP02H:
	DEX
	BEQ	OUTALK
	DEC	WORK0
	BMI	OUTALK
;
	LDY	WORK0
	LDA	ALIENT+SARAH,Y
	STA	HMP1
	STA	WSYNC
	STA	HMOVE
	TXA
	SEC
	SBC	VPOSP0
	CMP	#SIZEP0
	TAY
	BCS	SKP2H2
	LDA	PLAY0,Y
	STA	GRP0
SKP2H2:
	LDY	#0
	CPX	VM0
	BNE	DOM4H
	DEY
DOM4H:
	STY	ENAM0
	LDY	WORK0
	DEX
	BEQ	OUTKH2
	DEY
	BPL	OUTALH
	JMP	LAKEK
OUTKH2:
	RTS
OUTALH:
	JMP	ALIENL	
;---------------------------
ALIEN2:
	LDA	#5
	STA	NUSIZ1
	LDA	ALMASK
	STA	COLUP1
ALINL2:
	TXA
	SEC
	SBC	VPOSP1
	STA	WSYNC
	CMP	#60
	TAY
	BCS	SKPAL5
	LDA	ALIENT+SARAH,Y
	STA	GRP1
SKPAL5:
	TXA
	SEC
	SBC	VPOSP0
	CMP	#SIZEP0
	TAY
	BCS	SKP022
	LDA	PLAY0,Y
	STA	GRP0
	LDA	(COLP0T),Y
	STA	COLUP0
SKP022:
	DEX
	TXA
	SEC
	SBC	VPOSP1
	CMP	#60
	TAY
	STY	WORK0
	STA	WSYNC
	BCS	SKPAL7
	LDA	ALIENT+SARAH,Y
	STA	GRP1
SKPAL7:
	TXA
	SEC
	SBC	VPOSP0
	CMP	#SIZEP0
	TAY
	BCS	SKP22
	LDA	PLAY0,Y
	STA	GRP0
SKP22:
	LDY	WORK0
	LDA	#GREY
	CPY	ALSHAD
	BCC	DOSHA3
	LDA	ALMASK
DOSHA3:
	STA	COLUP1
	LDY	#0
	CPX	VM0
	BNE	DOM42
	DEY
DOM42:
	STY	ENAM0
	DEX
	BNE	ALINL2
OUTAL2:
	RTS
;---------------------------------
TOPK:
	STA	WORK0
	INC	WORK0
TOPL:
	STA	WSYNC
	DEX
	DEC	WORK0
	BEQ	OUTTOP
	STA	WSYNC
	LDY	#0
	CPX	VM0
	BNE	DOM4T
	DEY
DOM4T:
	STY	ENAM0
	DEX
	DEC	WORK0
	BNE	TOPL
OUTTOP:
	RTS
BOTKER:
	LDA	WORK4
	STA	VM0
BOTKER2:
	STA	WSYNC
	LDA	#0
	STA	COLUBK
	STA	GRP1
	STA	GRP0
	STA	ENAM0
	STA	ENAM1
	STA	HMP1
	STA	HMM1
	RTS
;----------------------------
;	INITIALIZE VARIABLES
;------------------------------------
INITVR:
	STA	CXCLR
	LDA	#0
	STA	S0
	STA	S1
	STA	S2
	STA	S3
	STA	PLAYER
	STA	COLUPF
	STA	STARTG
	STA	BONUSR
;
	STA	D00
;
	STA	WAVE+1
;
	STA	POWER
	STA	POWER+1
	LDX	#4
	STX	LIVES0
	STX	LIVES0+1
RR5:
	LDA	#0
	STA	D01,X
	DEX
	BPL	RR5
;
	LDA	#$FF
	STA	WAVE
	STA	BONUSR
	STA	SPEED
	LDA	#$70
	STA	PF0
	JSR	CLRALN
	LDX	#2
	LDA	#0
RSTVV0:
	STA	NJEWL,X
	STA	VV0,X
	DEX
	BPL	RSTVV0
	LDA	#$55
	STA	VV1
	JSR	RSTWAV
	JSR	RSTHIR
	STA	SCREEN+1
	STA	HIERO0+1
	STA	HIERO1+1
	STA	HIERO2+1
	STA	HIEROC+1
	JSR	RSTMIS
RSTWVZ:
	LDA	#160
	STA	HPENM
	LDA	#0
	STA	MISHOT
	LDA	#$80
	STA	NEWLAK
	STA	FRMCNT
	JSR	RSTBEG
RSTALL:
;-------------------------------
RSTFAL:
	LDA	#3
	STA	SHADHT
	LDA	#0
	STA	GPFAL
	LDA	#1
	STA	HORDIR
RSTPOS:
	LDX	#MINHP0
	DEX
	LDY	#MINVP
	INY
	STX	HP0
	STY	VPOSP0
	RTS
;-------------------------
RSTBEG:
	LDA	#RSTHP1
	STA	HP1
	LDA	#$FF
	STA	LKMASK
	RTS
;---------------------
RSTSCO:
	LDY	#2
	LDX	PLAYER
CLRNJ:
	LDA	NJEWL,Y
	AND	OFFSET,X
	STA	NJEWL,Y
	LDA	VV0+SARAH,Y
	AND	OFFSET,X
	STA	VV0,Y
	DEY
	BPL	CLRNJ
	LDA	VV1+SARAH
	ORA	FIVET,X
	STA	VV1
;------------------------------------
RSTWAV:
	LDA	ATT
	BEQ	NOSWBN
	LDA	#$FF
	STA	BONUSR
NOSWBN:
	JSR	RSTPOS
	LDA	BONUSR
	EOR	#$FF
	STA	BONUSR
	BNE	RSTWV1
	STA	STARTG
	LDX	PLAYER
	INC	WAVE,X
	LDA	#3
	STA	SHADHT
	LDA	#BONTMR
	STA	BONTIM
RSTWV1:
	LDX	PLAYER
	LDA	#0
	STA	SCREEN,X
	STA	ALIEN
RSTWV2:
	LDX	#0
	LDA	PWRTMR
	BEQ	NORSTPOW
	LDY	PLAYER
	STX	POWER,Y
NORSTPOW:
	STX	ALMASK
	STX	SPSHOW
	STX	TRIG
	STX	PWRTMR
	LDA	#SCROLL1
	STA	SCROLL
;
BUMPS:
	LDA	BONUSR
	BNE	BONBUMP
	LDX	PLAYER
	LDA	WAVE,X
	CMP	#2
	BCC	NOFASTBUMP
BONBUMP:
	LDA	#0
	STA	BUMP
	RTS
NOFASTBUMP:
	LDA	GAME
	AND	#$0F
	LSR	
	EOR	#1
	STA	BUMP
	RTS
;
;--------------------
RSTALN:
	JSR	RANDOM
	AND	#$07
	TAX
	LDA	SPSHOW
	BEQ	NOWIPE
	LDA	SPALN,X
	STA	ALIEN
	BPL	NOFISH
NOWIPE:
	LDY	ALIEN
	LDA	NEXTALN,Y
	BNE	AFTRANAL
	LDA	TYPALN,X
AFTRANAL:
	STA	ALIEN
	CMP	#3
	BNE	NOFISH
ISFISH:
	LDA	GAME
	AND	#$0F
	BEQ	RSTALN
NOFISH:
	LDY	PLAYER
	JSR	DOWAVE
	STA	WORK
	DEC	WORK
	LDY	PLAYER
	LDA	SCREEN,Y
	CMP	WORK
	BNE	NOSPAA
	LDA	#9
	STA	ALIEN
NOSPAA:
	LDY	#4
	LDA	ALIEN
	CMP	#10
	BEQ	NOBLANKHM
	LDA	#0
AHLPP:
	STA	ALIENT,Y
	STA	ALIENT+5,Y
	STA	ALIENT+10,Y
	STA	ALIENT+45,Y
	STA	ALIENT+50,Y
	STA	ALIENT+55,Y
	DEY
	BPL	AHLPP
NOBLANKHM:
	LDA	#0
	STA	ALMASK
	LDA	#20
	STA	ALSHAD
	LDA	#$FF
	STA	ALIND
	RTS
;------------------------------
DOWAVE:
	LDA	WAVE,Y
	CMP	#8
	BCC	DOWORK3
	LDA	#7
DOWORK3:
	STA	WORK3
	LDA	GAME
	AND	#$0F
	CLC
	ADC	WORK3
	TAY
DOWAVE7:
	LDA	WAVET,Y
	LDY	PLAYER
OUTWAVE:
	RTS
;-----------------------------------
RSTLAK:
	LDA	HP1
	CMP	#RSTHP1
	BEQ	NOHP1
	LDA	#160
	STA	HP1
NOHP1:
	LDA	NEWLAK
	AND	#$0F
	STA	NEWLAK
	TAY
;
	BEQ	OUTWAVE
;
	LDA	H1H
	STA	WORK+1
	STA	WORK+3
;
	CPY	#5
	BCS	HIROS
	RTS
;
HIROS:
	CPY	#6
	BNE	NOGETZ
	LDA	#10
	STA	TYPHIR
	STA	TYPHIR+1
	LDA	#JEWLG&$FF
	STA	WORK
	STA	WORK1
	LDA	GNH
	STA	WORK+1
	STA	WORK+3
	JMP	DOHIRLP
NOGETZ:
	JSR	RANDOM
	AND	#$07
	STA	TYPHIR
	TAY
	LDA	HRL,Y
	STA	WORK
;
	JSR	RANDOM
	AND	#$07
	STA	TYPHIR+1
	TAY
	LDA	HRL,Y
	STA	WORK1
;
DOHIRLP:
	LDY	#13
HPLOOP:
	LDA	(WORK),Y
	STA	PLAY1+1,Y
	LDA	(WORK1),Y
	STA	PLAY1+32,Y
	DEY
	BPL	HPLOOP
OUTHIR:
	RTS
;-------------------------------
SETFAL:
	LDA	ATT
	BNE	NOATTC
	LDA	STARTG
	BEQ	GROUND
	BMI	NOATTC
;
	JSR	DOHP00
;	BEQ	GRPFAL
TRYVER:
	LDA	SHADHT
	CLC
	ADC	VERDIR
	CMP	#18
	BEQ	GRPFAL
	CMP	#2
	BNE	NOSIT
	LDA	#0
	STA	STARTG
	BEQ	GRPFAL
NOSIT:
	STA	SHADHT
	BPL	GRPFAL
;
NOATTC:				;ATTRACT MODE MOVEMENT
	LDA	FRMCNT
	AND	#$03
	BNE	NOHP0
	JSR	DOHP0
	BNE	NOHP0
REVFAL:
	LDX	HORDIR
	INX
	LDA	REVTBL,X
	STA	HORDIR
NOHP0:
	LDA	VPOSP0
	LSR
	LSR
	CLC
	ADC	#3
	STA	SHADHT
	BNE	GRPFAL
GROUND:
	LDA	SHADHT
	BNE	NODETH
	LDX	FRMCNT
	CPX	#3
	BNE	NO58
	LDA	#CUCKOO1
	STA	TUNT0
	LDA	#CUCKOO2
	STA	TUNT1
NO58:
	CPX	#128
	BNE	GRPFAL
	LDA	#ENDGAM0
	CMP	TUNNUM
	BNE	DOREGSWP
	JMP	AFTENDTUN
DOREGSWP:
	JMP	SWPLR
;	JSR	SWPLR
;	JMP	GRPFAL
NODETH:
	LDA	FRMCNT
	AND	#$01
	BNE	NOMOVS
	LDA	VERDIR
	BPL	VER00
	LDA	#HOPSPD
	STA	SPEED
;	BNE	NOMOVS
VER00:
	JSR	DOHP00
NOMOVS: 
	LDX	#4
	LDA	HORDIR
	BNE	GRPFAL
	LDA	VERDIR
	BEQ	AFTSIT
GRPFAL:
	LDA	FRMCNT
	AND	#$07
	BNE	DOGRP
	LDA	#1
	CMP	BONTIM
	BEQ	DOGRP
	CMP	SHADHT
	BEQ	DOGRP
	INC	GPFAL
	LDA	GPFAL
	CMP	#4
	BNE	DOGRP
	LDA	#0
	STA	GPFAL
DOGRP:
	LDX	GPFAL
	LDA	STARTG
	BNE	AFTSIT
	TXA
	CLC
	ADC	#5
	TAX
AFTSIT:
	LDA	SHADHT
	BNE	NODET2
	TXA
	CLC
	ADC	#4
	TAX
NODET2:
	LDA	G0L,X
	STA	WORK
	LDA	G0H
	STA	WORK+1
;
	LDX	#0
	LDA	#7
	SEC
	SBC	SHADHT
	BMI	NOCHSH
	TAX
NOCHSH:
	TXA
	CLC
	ADC	#COLP0&$FF
	STA	COLP0T
	LDA	#COLP0&$FF00/256
	STA	COLP0T+1
;
	LDA	SHADHT
	CMP	#2
	BCC	NOSHAD
	LDY	#14
	LDX	#5
PLOOP:
	LDA	(WORK),Y
	STA	PLAY0,X
	DEY
	DEY
	DEY
	DEX
	BNE	PLOOP
NOSHAD:
	LDA	GPFAL
	CMP	#20
	BCS	OUTFAL
	LDY	#15
	LDA	SHADHT
	CLC
	ADC	#15
	TAX
PLOOP1:
	LDA	(WORK),Y
	STA	PLAY0,X
	DEX
	DEY
	BPL	PLOOP1
OUTFAL:
	RTS
;----------------
SWPLR:
	LDA	#3
	STA	SHADHT
	LDA	BONUSR
	BNE	OUTFAL
;
	LDX	PLAYER
	LDA	#1
	STA	WORK
	LDA	#0
	STA	MISHOT
	DEC	LIVES0,X
	BPL	CHKLV2
	DEC	WORK
	LDA	#ENDGAM0	;END GAME TUNE
	STA	TUNT0
	LDA	#ENDGAM1
	STA	TUNT1
	LDA	#225
	STA	FRMCNT
	LDA	#0
	STA	SHADHT
	RTS
AFTENDTUN:
	JSR	RSTHIR
CHKLV2:
	LDY	PLAYER
;	TYA
;	EOR	#1
;	TAX
;	LDA	LIVES0,X
;	BEQ	NODECS
;	BMI	NODECS
	LDA	GAME
	BPL	NODECS
	LDA	SCREEN,Y
	BEQ	NODECS
	SEC
	SBC	#1
	STA	SCREEN,Y
NODECS:
	BNE	NOALIEN0
	STA	ALIEN
NOALIEN0:
	LDA	SPSHOW
	BEQ	NODECS2
	JSR	DOWAVE
	SEC
	SBC	#2
	STA	SCREEN,Y
NODECS2:
	LDA	#160
	STA	HPENM
	LDA	#$80
	STA	FRMCNT
	LDA	GAME
	BPL	NOSWPL
	TYA
	EOR	#$01
	TAX
	TAY
	LDA	LIVES0,X
	BMI	NOSWPL
	STY	PLAYER
	BPL	RSTW
NOSWPL:
	DEC	WORK
	BPL	RSTW
	DEC	ATT
	LDA	#127
	STA	TTIMER
	STA	STARTG
RSTW:
	LDA	SPSHOW
	BNE	DOSWP
	LDA	GAME
	BPL	OUTSWP
DOSWP:
	JSR	RSTWV2
	JSR	RSTWVZ
OUTSWP:
	RTS
RSTHIR:
	LDY	PLAYER
	LDA	#0
	STA	HIERO0,Y
	STA	HIERO1,Y
	STA	HIERO2,Y
	STA	HIEROC,Y
	RTS
;---------------------------------
DOHP00:
	LDX	#0
	LDA	GAME
	AND	#$0F
	CMP	#2
	BCS	DOHP0
	LDA	FRMCNT
	AND	#$01
	BNE	BADHOR
DOHP0:
	LDA	HP0
	CLC
	ADC	HORDIR
	CMP	#MINHP0
	BCC	BADHOR
	CMP	#MAXHP0
	BCS	BADHOR
	STA	HP0
	DEX
	LDA	SPSHOW
	BPL	DOHOR
	LDA	FRMCNT
	AND	#$07
	BNE	BADHOR
DOHOR:
	LDA	VPOSP0
	SEC
	SBC	HORDIR
	STA	VPOSP0
BADHOR:
	TXA
	RTS
;-----------------------------
RSTMIS:
	LDA	#0
	STA	MISHOT
	LDA	#160
	STA	HPENM
RSTMI:
	LDA	HP0
	CLC
	ADC	#3
	STA	HPM0
	LDA	VPOSP0
	CLC
	ADC	#10
	ADC	SHADHT
	STA	VM0
OUTMIS:
	RTS
;------------------------------
SETLAK:
	LDA	SPSHOW
	BMI	OUTLAK
;
	LDA	NEWLAK
	BPL	LAKEON
	CMP	#$FF
	BNE	DORSTLAKE
	LDA	SCROLL
	CMP	#172
	BNE	OUTLAK
	LDA	#$80
	STA	NEWLAK
	RTS
DORSTLAKE:
	JSR	RSTLAK
LAKEON:
	LDA	SPEED
	BMI	OUTLAK
	AND	FRMCNT
	BNE	OUTLAK
DOHLAK:
	LDA	HP1
	CMP	#6
	BCC	NODECL
NOWAIT:
	DEC	HP1
	LDX	BUMP
	CPX	#3
	BNE	NODECH
	DEC	HP1
NODECH:
	LDA	HP1
	AND	#$03
	BNE	NOMKL
NOSTAL:
	LDA	NEWLAK
	CMP	#10
	BEQ	NOMKL
	LDA	LKMASK
	LSR
	ORA	#$80
CHNMSK:
	STA	LKMASK
NOMKL:
	RTS
NODECL:
	LDA	#0
	STA	ALMASK
	LDA	#$FF
	STA	NEWLAK
OUTLAK:
	RTS
;------------------------
BONUSC:
	LDA	BONUSR
	BEQ	OUTBON
	LDA	BONTIM
	BNE	OUTBON
	JSR	RSTSCO
	JMP	RSTWVZ
OUTBON:
	RTS
;----------------------------
SETALN:
	LDA	BONUSR
	BNE	OUTBON
	LDX	ALIEN
;
	BNE	NOGLIDER
	LDA	ALIND
	CMP	#1
	BEQ	NOUWETS
NOGLIDER:
	CPX	#1
	BNE	NOSWCH
	LDA	SPSHOW
	BNE	NOSWCH
	JSR	RANDOM
	AND	#$7F
	BNE	NOSWCH
NOUWETS:
	TXA
	CLC
	ADC	#7
	STA	ALIEN
	TAX
	LDA	#0
	STA	ALIND
NOSWCH:	
	LDA	A1L,X
	STA	WORK
	LDA	A1H
	STA	WORK+1
;
	LDA	ALMASK
	BNE	NOCLRALN
	LDA	FRMCNT
	AND	#$01
	BNE	GONOSHOW
	JSR	CLRALN
GONOSHOW:
	JMP	NOSHOW
NOCLRALN:
	CPX	#10
	BEQ	NOSHOW
	LDA	ANIMT,X
	AND	FRMCNT
	BNE	AL00
;
	LDA	ALIND
	CMP	#4
	BEQ	AL000
	INC	ALIND
	CMP	STPMAX,X
	BCC	AL00
SET0:
	LDA	#0
	STA	ALIND
	BEQ	AL00
AL000:
	LDA	#0
	STA	ALMASK
AL00:
	LDX	ALIND
	CPX	#$FF
	BEQ	SET0
	LDA	STEPT,X
	TAY
AL01:
	STY	WORK3
;
	LDX	ALIEN
	CPX	#1
	BEQ	NOSHD
	CPX	#8
	BEQ	NOSHD
;
	LDX	#5
AL1:
	LDA	(WORK),Y
	STA	ALIENT+16,X
	DEY
	DEY
	DEY
	DEX
	BPL	AL1
NOSHD:
	LDA	#15
	STA	WORK4
	LDY	WORK3
	LDX	ALSHAD
AL0:
	LDA	(WORK),Y
	STA	ALIENT+15,X
	DEY
	DEX
	DEC	WORK4
	BPL	AL0
;
NOSHOW:
	LDA	SPSHOW
	BPL	NOATTA
	JMP	ATTACK
;
NOATTA:
	LDA	ALMASK		;SEND OUT ALIEN?
	BNE	CHKNA
	LDA	SHADHT
	BEQ	CHKNA
	LDA	STARTG
	BNE	NORANDCHK
	LDA	HP1
	CMP	#RSTHP1
	BNE	NORANDCHK
DORANDCHK:
	LDA	TUNNUM		;SONG STILL PLAYING
	CMP	#4
	BEQ	CHKNA
	CMP	#3
	BEQ	CHKNA
	CMP	#$0A
	BEQ	CHKNA
	LDA	FRMCNT
	BMI	CHKNA
	LDA	RAND
	BPL	CHKNA
NORANDCHK:
	LDX	SCROLL
	CPX	#SCROLL1
	BEQ	RSTAL2
	INX
	CPX	#SCROLL1
	BEQ	RSTAL2
CHKNA:
	LDX	ALIEN
	LDA	ALNSPD,X
	LDY	BUMP
	CPY	#1
	BEQ	NOLSR
	LSR
NOLSR:
	CPY	#3
	BNE	NOFRMX
	LDA	#0
NOFRMX:
	AND	FRMCNT
	BNE	OUTALN
;
	LDA	ALIND
	CMP	#2
	BCS	OUTALN
;
	LDA	ALMASK
	BEQ	OUTALN
	LDX	ALIEN
	LDA	STOPAL,X
	BPL	NOSTOPAL
DOSTOPAL:
	CMP	FRMCNT
	BCC	OUTALN
NOSTOPAL:
	LDX	PLAYER
	LDA	POWER,X
	CMP	#10
	BNE	GOLEFT
	LDA	ALIEN
	CMP	#1
	BEQ	GOLEFT
	LDA	PWRTMR
	BNE	OUTALN
GOLEFT:
	LDX	#61
DEXH:
	DEX
	DEX
	LDA	ALIENT+SARAH,X
	CLC
	ADC	#$10
	CMP	#$80
	BEQ	DEXH
	STA	ALIENT,X
	LDX	#1
INXH:
	INX
	INX
	LDA	ALIENT+SARAH,X
	SEC
	SBC	#$10
	CMP	#$80
	BEQ	INXH
	STA	ALIENT,X
OUTALN:
	RTS
RSTAL2:
	JSR	RSTALN
	LDA	ALIEN
	CMP	#9
	BEQ	OUTALN
	LDA	#$90
	STA	ALIENT+59
	STA	ALIENT+57
	STA	ALIENT+55
	STA	ALIENT+53
	STA	ALIENT+51
	STA	ALIENT+49
	LDA	#$70
	STA	ALIENT+1
	STA	ALIENT+3
	STA	ALIENT+5
	STA	ALIENT+7
	STA	ALIENT+9
	STA	ALIENT+11
	STA	ALMASK
	JMP	DOSPTUNE
ATTACK:
	LDA	SPANIM
	CMP	#9
	BEQ	CHKKIL
	LDX	ALIEN
	LDA	GAME
	AND	#$0F
	BNE	NOBEGG
	LDX	#1
NOBEGG:
	LDA	ALNSPD,X
	AND	FRMCNT
	BNE	CHKKIL
	LDA	ALIEN
	CMP	#1
	BEQ	NOAFRZ
	LDX	PLAYER
	LDA	POWER,X
	CMP	#10
	BNE	NOAFRZ
	LDA	PWRTMR
	BNE	CHKKIL
NOAFRZ:
	DEC	HP1
	LDA	ALIEN
	CMP	#10
	BEQ	DOHP110
;	
	LDA	RAND
	AND	#$0F
	BEQ	OUTHP1
DOHP110:
	LDA	FRMCNT
	AND	#$03
	BNE	OUTHP1
	INC	HP1
OUTHP1:
	LDA	VPOSP1
	CMP	#$D7
	BEQ	RSTATT
	CMP	#$D8
	BEQ	RSTATT
	DEC	VPOSP1
	DEC	VPOSP1
CHKKIL:
	LDA	TTIMER	;KILL SPHINX FLAG
	CMP	#200
	BNE	OUTATT
	INC	TTIMER
	LDA	STARTG
	BNE	NOINCS
	INC	STARTG
NOINCS:
	JSR	RSTWAV
	JSR	CLRALN
	LDA	#10
	STA	SHADHT
	STA	STARTG	
	LDA	#20
	STA	NEWLAK
	RTS
RSTATT:
	LDA	SHADHT
	BEQ	OUTATT
	JSR	RSTALN
	LDA	SPANIM
	CMP	#9
	BEQ	NOALMASK
	STY	ALMASK
NOALMASK:
	LDA	#65
	STA	HP1
	LDA	#85
	STA	VPOSP1
	JSR	CLRALN
DOSPTUNE:
	LDA	BONUSR
	BNE	OUTATT
	LDA	PWRTMR
	BMI	OUTATT
	LDY	#SPTUNE
	LDX	ALIEN
	CPX	#10
	BNE	NOAL10
	LDY	#MINISHOT
NOAL10:
	CPX	#1
	BNE	DOSTUNE
	LDY	#UWERUN
DOSTUNE:
	LDA	TUNT1+SARAH
	CMP	#ENDGAM1
	BEQ	OUTATT
	STY	TUNT1
OUTATT:
	RTS
;------------------------------------------
CLRALN:
	LDY	#4
	LDA	#0
CLRALNL:
	STA	ALIENT+15,Y
	STA	ALIENT+20,Y
	STA	ALIENT+25,Y
	STA	ALIENT+30,Y
	STA	ALIENT+35,Y
	STA	ALIENT+40,Y
	DEY
	BPL	CLRALNL
	RTS
;-------------------
SETOBE:
	LDA	SPSHOW
	BMI	OUTOBE
	LDA	NEWOBE
	BMI	OUTOBE
OBEON:
	TAX
	TAY
	LDA	JEWEL
	BEQ	NOCHJW
	LDX	#0
NOCHJW:
	LDA	GML,X
	STA	PTR0
	LDA	GMH
	STA	PTR0+1
	LDA	GNH
	STA	PTR1+1
;
	LDA	GNL,Y
	STA	PTR1
;
	LDA	SPEED
	BMI	OUTOBE
	AND	FRMCNT
	BNE	OUTOBE
DOOBE:
	LDA	HPM1
	CMP	#4
	BCC	NOOBE
	DEC	HPM1
	LDX	BUMP
	CPX	#3
	BNE	NODECO
	DEC	HPM1
NODECO:
	LDA	HPM1
	LDX	#$F0
	CMP	#128
	BCS	DOBM
	LDX	#$F2
	CMP	#98
	BCS	DOBM
	LDX	#$FF
DOBM:
	STX	OBMASK
	RTS
NOOBE:
	LDA	#$FF
	STA	NEWOBE
	LDA	#0
	STA	OBMASK
OUTOBE:
	RTS
;-----------------------------
HORIZZ:
	LDA	VM0
	STA	WORK4
	LDA	HPM0
	STA	WORK3
	LDA	SHADHT
	BEQ	ZEROMIS
	LDA	SPSHOW
	BNE	HORIZ
	LDA	MISHOT+SARAH
	BPL	HORIZ
	AND	#$0F
	BEQ	DOENMI
	LDA	FRMCNT
	AND	#$01
	BNE	HORIZ
DOENMI:
	LDA	VENM
	STA	VM0
	LDA	HPENM
ZEROMIS:
	STA	HPM0
HORIZ:	LDX     #4 
HORIZ1:
	LDA	HP0,X
	CLC      
	ADC     #$2E 
	TAY      
	AND     #$0F 
	STA     WORK 
	TYA      
	LSR      
	LSR      
	LSR      
	LSR      
	TAY      
	CLC      
	ADC     WORK 
	CMP     #15 
	BCC     HORIZ2 
	SBC     #15 
	INY      
HORIZ2:	EOR     #7 
	ASL      
	ASL      
	ASL      
	ASL      
	STA     HMP0,X
	STA     WSYNC 
HORIZ3:	DEY      
	BPL     HORIZ3 
	STA     RESP0,X 
	DEX      
	BPL     HORIZ1 
	STA     WSYNC 
	STA     HMOVE 
	LDX     #3 
WASTIT:	DEX      
	BPL     WASTIT 
	STA     HMCLR
	LDA	SPSHOW
	BNE	ENDHOR 
	LDA	WORK3
	STA	HPM0
ENDHOR:	RTS      
;
	.EJECT
;	BANK0 TABLES
G0H:	DC.B	FLY0&$FF00/256
;-------------------------------
;	.ORG	BANK0+$B80
FLY1:
	DC.B	$60,$E0,$E0,$60,$70,$38,$3C,$3C
	DC.B	$7E,$7F,$7F,$70,$70,$60,$60,$40
FLY2:
	DC.B	$00,$00,$04,$6C,$EC,$EC,$FC,$7C
	DC.B	$FC,$FC,$78,$38,$08,$00,$00
FLY0:
	DC.B	$00,$60,$E0,$E7,$E7,$7F,$3E,$3E
	DC.B	$7C,$78,$F8,$E0,$40,$00
;
HOP0:
	DC.B	$00,$00,$60,$F0,$F0,$78,$78,$7C
	DC.B	$7C,$7C,$7C,$7C,$38,$38,$08,$0C
SIT:
HOP1:
	DC.B	$40,$E0,$F0,$F0,$78,$78,$7C,$7C,$7C,$3C
	DC.B	$38,$38,$18,$0C,$00
DED0:
	DC.B	$00,$02,$7F,$3E,$7C,$FE,$E7,$06
	DC.B	$40,$02,$00,$42,$08,$00,$00
DED1:	
	DC.B	$00,$02,$7F,$3E,$7C,$FE,$E7,$06
	DC.B	$08,$00,$80,$02,$40,$08,0,0
;
	.ORG	BANK0+$C00
;
FISH:
	DC.B	0,$10,$18,$98,$68,$F0,$B8,$FD,$7F,$1D,$07,$09,$0D,$0C,$00,$00
	DC.B	$00,$00,$86,$66,$F4,$B8,$FD,$7F,$1D,$67,$61,$41
GLFOLD:
	DC.B	$00,$00,$00,$00,$40,$40,$60,$7C,$FF,$FF,$FC,$00,$00,$00,$00,$00
	DC.B	$00,$00,$00,$00,$40,$40,$60,$30,$FC,$FC,$FC,$E0,$A8,$00,$00,$00
GLIDER:
	DC.B	0,$40,$60,$30,$FC,$80,$9C,$A0,$A8,$A0,$A8,$A8,$B0,$A0,$C0,0
FLEA:
	DC.B	0,$C0,$E0,$F0,$EC,$FC,$7C,$FC,$38,$D0,$C8,$0C,$0C,$0C,$0C,0
	DC.B	0,$D8,$F8,$F0,$F8,$F0,$70,$30,$38,$78,$7C,$6C,$74,$04,$04,0
SCAROB:
	DC.B	0,$04,$66,$F6,$DA,$7C,$FE,$7E,$36,$5A,$6C,$70,$20,$00,$00,$00
	DC.B	$00,$68,$F4,$DA,$7C,$FE,$7E,$36,$5A,$2C,$10,$00,$00,0
;
VULTUR:
	DC.B	$00,$00,$C0,$C0,$E0,$E0,$6C,$3F,$3F,$3C,$3C,$3E,$32,$32,$30,0
	DC.B	$00,$00,$00,$64,$7E,$7C,$78,$38,$38,$18,$1C,$04,$06,$00,$00,0
UWE:
	DC.B	0,$38,$38,$58,$BC,$FC,$F6,$DE,$FA,$6F,$7E,$3B,$39,$0D,$0E,$00
	DC.B	0,$38,$38,$5A,$BC,$FC,$F6,$CE,$DE,$7F,$7F,$BD,$3C,$06,$00,$00
	DC.B	0,$38,$38,$5B,$BC,$FC,$E6,$C6,$CE,$7E,$7E,$BC,$BC,$80,$00,$00
UWEDIG:
	DC.B	$FA,$6F,$7E,$3B,$39,$0D,$0E
SPACES:	
	DC.B	$00,$00,$00,$00,$00,$00,$00,$00,$00
	DC.B	$00,$00,$00,$00,$00,$00,$00,$00,$00,0,0,0,0,0,$39,$0D
	DC.B	$01,$04,$40,$10,$80
;
	.ORG	BANK0+$D00
;-----------------------------------
A1L:	DC.B	GLFOLD&$FF,UWE&$FF,FLEA&$FF,FISH&$FF
	DC.B	SCAROB&$FF,VULTUR&$FF,0,GLIDER&$FF,UWEDIG&$FF,SPACES&$FF
A1H:	DC.B	GLFOLD&$FF00/256
;	FALCON TABLES
REVTBL:	DC.B	1,1,$FF
G0L:	DC.B	FLY0&$FF,FLY1&$FF,FLY0&$FF,FLY2&$FF,SIT&$FF,HOP0&$FF,HOP1&$FF
	DC.B	HOP0&$FF,HOP1&$FF,DED0&$FF,DED1&$FF,DED0&$FF,DED1&$FF
;
;	WAVE TABLES
WAVECL:	DC.B	LTGRN,$C7,$07,$00,$37,$87,$47,$05
WAVET:	DC.B	8,11,15,23,28,33,37,40,40,40,40
;WAVET:	DC.B	4,4,4,4,4,4,4,4,4,4,4,4,4,4,4,4
;
HRL:
	DC.B	CANE&$FF,MAN&$FF,FEATH&$FF,EYE&$FF
	DC.B	ANKH&$FF,BIRD&$FF,SUN&$FF,BOWL&$FF
H1H:
	DC.B	CANE&$FF00/256
;
STOPAL:	DC.B	0,0,$F0,0,0,$F0
COLP0:
	DC.B	GR,GR,GR,GR,GR,GR,GR,GR
	DC.B	G,G,G,G,G,G,G,G,G,G,G,G,G,G,G,G,G,G,G,G
	DC.B	G,G,G,G,G,G,G,G,G,G,G,G
;
CANE:
	DC.B	$80,$40,$20,$10,$08,$04,$02,$01,$09,$0B,$06,$00,$00
MAN:
	DC.B	$00,$40,$22,$B2,$7C,$3C,$1B,$97,$63,$18,$00,$00,$00,$00
FEATH:
	DC.B	$80,$40,$30,$38,$3C,$1E,$0F,$07,$00,$00,$00,$00,$00
EYE:
	DC.B	$00,$01,$03,$06,$7C,$85,$B5,$B5,$85,$F9,$02,$7C,$00,$00
ANKH:
	DC.B	$80,$44,$28,$10,$2E,$5D,$1D,$11,$11,$0E,$00,$00,$00,$00
BIRD:
	DC.B	$08,$02,$3A,$6E,$9E,$0E,$0E,$0E,$06,$06,$03,$03,$02,$04
SUN:
	DC.B	$0C,$08,$1C,$20,$34,$20,$EE,$9E,$DF,$1F,$1F,$0E,$0E
BOWL:
	DC.B	$00,$26,$21,$49,$42,$92,$84,$24,$08,$C8,$30,$00,$00,$00
;
STEPT:	DC.B	15,31,46,36,26
;
	.ORG	BANK0+$E00
;
;	ALIEN TABLES
;
;0	GLIDER
;1	UWE
;2	FLEA
;3	FISH
;4	SCAROB
;5	VULTURE

FIVET:	DC.B	$05,$50
OFFSET:	DC.B	$F0,$0F
ANIMT:	DC.B	$1F,$07,1,1,1,1,1,0,$07,0
SPALN:	DC.B	0,1,2,10,4,5,10,3
STPMAX:	DC.B	1,2,1,1,1,1,1,0,4,0
ALNSPD:	DC.B	0,1,0,0,1,1,1,1,$FF,0,0
TYPALN:
	DC.B	0,0,1,1,2,3,4,5
;
NEXTALN:
	DC.B	1,5,0,0,0,0,0,1,5
OBE0:		;JUST EGG
	DC.B	0
	DC.B	$00,$00,$00,$00,$00,$00,$00,$00
	DC.B	$FF,$FF,$0F,$0F,$1F,$10,$70,$70
	DC.B	$70,$70,$00,$0F,$0F,$0F,$0F,$0F,$0F
	DC.B	$0F,$0F,$0F,$0F,$0F,$0F,$0F,$0F,$0F
	DC.B	$0F,$0F,$0F,$0F,$0F,$0F,$0F,$0F,$0F,$0F
	DC.B	$0F,$0F,$0F,$0F,$0F,$2F,$2F
	DC.B	$0F,$0F,$1F,$1F,$0F,$0F,$00
	DC.B	$00,$00,$00
;
OBE1:		;JUST BAR
	DC.B	0
	DC.B	$00,$0F,$0F,$0F,$0F,$0F,$00,$00
	DC.B	$70,$70,$70,$70,$00,$00,$00,$00
	DC.B	$00,$00,$00,$0F,$0F,$0F,$0F,$0F,$0F
	DC.B	$0F,$0F,$0F,$0F,$0F,$0F,$0F,$0F,$0F
	DC.B	$0F,$0F,$0F,$0F,$0F,$0F,$0F,$0F,$0F,$0F
	DC.B	$0F,$0F,$0F,$0F,$0F,$2F,$2F
	DC.B	$0F,$0F,$1F,$1F,$0F,$0F,$00
	DC.B	$00
;
LAKHMT:
	DC.B	0,0,0,0,0
OBE2:		;JUST OBELISK
	DC.B	0
	DC.B	$00,$00,$00,$00,$00,$00,$00,$00
	DC.B	$00,$00,$00,$00,$00,$00,$70,$70
	DC.B	$00,$00,$00,$0F,$0F,$0F,$0F,$0F,$0F
	DC.B	$0F,$0F,$0F,$0F,$0F,$0F,$0F,$0F,$0F
	DC.B	$0F,$0F,$0F,$0F,$0F,$0F,$0F,$0F,$0F,$0F
	DC.B	$0F,$0F,$0F,$0F,$0F,$2F,$2F
	DC.B	$0F,$0F,$1F,$1F,$0F,$0F,$00
	DC.B	$00,$00,$00,0,0,0
;
	.ORG	BANK0+$F00
JEWLG:
	DC.B	0,$10,$38,$7C,$D6,$D6,$BA,$7C,$38,0,0,0,0,0
;
OBNZ0:
	DC.B	0
	DC.B	$30,$30,$30,$30,$30,$30,$30,$30
	DC.B	$10,$10,$20,$20,$20,$20,$10,$10
	DC.B	$00,$00,$00,$F4,$F4,$F4,$F4,$F4,$F4
	DC.B	$F4,$F4,$F4,$F4,$F4,$F4,$F4,$F4,$F4
	DC.B	$F4,$F4,$F4,$F4,$F4,$F4,$F4,$F4,$F4
	DC.B	$F4,$F4,$F4,$F4,$F4,$F4,$F4,$F4
	DC.B	$24,$24,$24,$24,$14,$14,$14,$14
	DC.B	$00,$00,$00
OBNZ1:
	DC.B	0
	DC.B	$30,$30,$30,$30,$30,$30,$30,$30
	DC.B	$10,$10,$20,$20,$20,$10,$10,$00
	DC.B	$00,$00,$00,$00,$F6,$F6,$F6,$F6,$F6,$F6,$F6,$F6
	DC.B	$F6,$F6,$F6,$F6,$F6,$F6,$F6,$F6
	DC.B	$F6,$F6,$F6,$F6,$F6,$F6,$F6,$F6
	DC.B	$F6,$F6,$F6,$F6,$F6,$F6,$F6
	DC.B	$26,$26,$26,$26,$16,$16,$16,$16
	DC.B	$00,$00,$00
CLRPYR:
	DC.B	0
	DC.B	LG,LG,LG,LG,LG,LG,LG	
	DC.B	GD,GD,GD,GD,GD,GD,GD,GD,GD,GD,GD
	DC.B	W,W,W,W,W,W
	DC.B	W,W,W,W,W,W,W,W
	DC.B	W,W,W,W,W,W,W,W
	DC.B	W,W,W,W,W,W,W,W
	DC.B	W,W,W,W,W,W,W,W
	DC.B	W,W,W,W,W,W,W,W
;
;	OBELISK TABLES
GML:	DC.B	OBE2&$FF,OBE0&$FF,OBE1&$FF
GMH:	DC.B	OBE0&$FF00/256
GNH:	DC.B	OBNZ1&$FF00/256
GNL:	DC.B	OBNZ1&$FF,OBNZ0&$FF,OBNZ1&$FF
;
	.ORG	BANK0+$FD8
;
	LDA	$FFF6
	JMP	BEGIN0
EXIT5:
	LDA	$FFF9
	JMP	(JMPPTR)
EXIT3:
	LDA	$FFF8
	JMP	(JMPPTR)
EXIT0:
	LDA	$FFF7
	JMP	(JMPPTR)
;
	LDA	$FFF6
	JMP	(JMPPTR)
;
	DC.B	0,0,0,0
;
	.DC.W	BEGIN0
	.DC.W	BEGIN0
	.DC.W	BEGIN0

	.EJECT
	.ORG BANK1
;	SARAH WRITE
;--------------------------------------------
PLAY1:	.DS.B	46		;LAKE GRAPHICS
ALIENT:	.DS.B	60		;ALIEN GRAPHICS
TYPHIR:	.DS.B	2		;TYPE OF HIEROS SHOWN
HIERO0:	.DS.B	2		;TYPE OF HIERO PICKED UP	
HIERO1:	.DS.B	2
HIERO2:	.DS.B	2
COLLAK:	.DS.B	1
LAKENZ:	.DS.B	1
VV0:	.DS.B	1
VV1:	.DS.B	1
VV2:	.DS.B	1
D00:	.DS.B	1		;DIGITS FOR SCORE
D01:	.DS.B	1
D02:	.DS.B	1
D03:	.DS.B	1
D04:	.DS.B	1
D05:	.DS.B	1
TUNT0:	.DS.B	1
TUNT1:	.DS.B	1
MISHOT:	.DS.B	1		;MISSILE SHOT
;
	.ORG	BANK1+$100
;------------------------
DOTEXT:
	JSR	DOSCO
DOTEX:
	LDA	#4
BEFSCO:
	STA	WORK0
	STY	COLUP0
	STY	COLUP1
	BNE	SCOREL
SETSC0:
	LDX	#0
	STX	WORK4
	STX	WORK3
SETSCO:
	STA	WORK0
	LDA     #$FF 
;	STA	WSYNC
	STA     VDELP0 
	STA     VDELP1 
	STY	COLUP0
	STY	COLUP1
	LDA     #3 
	STA     NUSIZ0 
	STA     NUSIZ1
	LDX	#6
	STA     WSYNC
ST1:	DEX      
	BPL     ST1 
	NOP      
	STA     RESP0 
	STA     RESP1 
	LDA     #$F0 
	STA     HMP0 
	STA     WSYNC 
	STA     HMOVE 
;
SCOREL:	LDY     WORK0		;SCORE KERNEL LOOP	
	LDA     @PTR5(Y) 
	STA     GRP0 
	STA     WSYNC 
	LDA     @PTR4(Y) 
	STA     GRP1 
	LDA     @PTR3(Y) 
	STA     GRP0 
	LDA     @PTR2(Y) 
	STA     WORK 
	LDA     @PTR1(Y) 
	TAX      
	LDA     @PTR0(Y) 
	TAY      
	LDA     WORK 
	STA     GRP1 
	STX     GRP0 
	STY     GRP1 
	STA     GRP0 
	DEC     WORK0 
	BPL     SCOREL
	LDA	WORK4
	AND	#$80
	STA	HMP0
	STA	HMP1
	STA	WSYNC
	STA	HMOVE
	LDY	WORK4
	STY	GRP1
	LDA	WORK3
	STA	COLUP0
	STA	COLUP1
	TYA
	LSR
	AND	#$05
	STA	GRP0
	STY	GRP1
	LDA	#0
	RTS
;-------------------
SPHINX:
	LDA	#$9B
	STA	WORK4
	STA	NEWOBE
	LDX	PLAYER
	LDA	WAVE,X
	AND	#$07
	TAX
	LDA	SPCOLOR2,X
	STA	WORK3
	TAY
	LDA	HSPH
	JSR	SETSCO
	LDA	#$20
	STA	NUSIZ0
	STA	NUSIZ1
	LDA	#11
	STA	WORK
	LDX	#92
TOPML:
	LDY	WORK
	LDA	SPHH,Y
	STA	GRP1
	STA	WSYNC
	LDA	SPHL,Y
	STA	GRP0
	LDY	#0
	CPX	VM0
	BNE	DOMS4
	DEY
DOMS4:
	STY	ENABL
	DEC	WORK
	DEX
	LDY	WORK
	LDA	SPHH,Y
	STA	GRP1
	STA	WSYNC
	LDA	SPHL,Y
	STA	GRP0
	DEC	WORK
	DEX
	CPX	#80
	BNE	TOPML
	STA	WSYNC
	STX	ENABL
;
	LDA	#0
	JSR	ENDS
	LDA	#$25
	STA	NUSIZ0
	LDA	#GREEN
	STA	COLUP0
	LDX	#1
;
ENDSPK:
	LDA	#OUTBK0&$FF
	STA	JMPPTR
	LDA	#OUTBK0&$FF00/256
	STA	JMPPTR+1
	JMP	EXIT1
TITLEK:
	LDY	#7
	JSR	DOHIGH
;
	LDY	#30
	JSR	LINES
;
	LDX	#138
	JSR	DOSCO
	LDY	#LTGRN
	LDA	#25
	JSR	SETSC0
;
	LDX	#144
	JSR	DOSCO
	LDY	#YELLOW
	LDA	#14
	JSR	BEFSCO
;
	LDY	#30
	JSR	LINES
	LDY	#4
	JSR	DOHIGH
	LDX	#$FF
	STX	STARTG
	INX
	STX	POWER
	STX	POWER+1
	STX	PLAYER
	STX	BONUSR
	LDA	GAME
	BPL	NOINXX
	INX
NOINXX:
	LDY	PLAYT0,X
	LDX	#156
	JSR	DOTEXT
	LDY	#3
	JSR	LINES
;
	LDA	GAME
	AND	#$0F
	TAX
	LDY	SELNOV,X
	LDX	#168
	JSR	DOTEXT
;
	LDA	GAME
	AND	#$0F
	TAX
	LDY	SELSTA,X
	LDX	#174
	JSR	DOTEXT
;
	LDA	GAME
	AND	#$0F
	TAX
	LDY	SELADV,X
	LDX	#180
	JSR	DOTEXT
;
	LDA	GAME
	AND	#$0F
	TAX
	LDY	SELEXP,X
	LDX	#186
	JSR	DOTEXT
;
	LDY	#2
	JSR	LINES
	LDX	#0
	LDA	GAME
	BPL	NOINXX2
	INX
NOINXX2:
	LDY	PLAYT1,X
	LDX	#162
	JSR	DOTEXT
;
	LDY	#20
	JSR	LINES
;
	LDY	#5
	JSR	DOHIGH
	LDX	#150
	LDY	#WHITE
	JSR	DOTEXT
	JSR	ENDS
;
	LDX	#59
	LDA	#0
CLRALL:
	STA	ALIENT,X
	DEX
	BPL	CLRALL
;
	LDY	#5
	JSR	LINES
	JMP	GOOUT
LINES:
	STA	WSYNC
	LDA	#0
	STA	GRP0
	STA	GRP1
	DEY
	BPL	LINES
	RTS
SCOREK:
	LDY	#0
	JSR	DOHIGH
;
	LDX	#$0A
;
	LDA	D05+SARAH
	AND	#$0F
	TAY
	BEQ	DOOAD1
	TAX
DOOAD1:
	LDA	NTAB2,X
	STA	PTR5
;
	LDA	D04+SARAH
	AND	#$0F
	BNE	NOOAD2
	CPY	#0
	BEQ	DOOAD2
NOOAD2:
	INY
	TAX
DOOAD2:
	LDA	NTAB2,X
	STA	PTR4
;
	LDA	D03+SARAH
	AND	#$0F
	BNE	NOOAD3
	CPY	#0
	BEQ	DOOAD3
NOOAD3:
	INY
	TAX
DOOAD3:
	LDA	NTAB2,X
	STA	PTR3
;
	LDA	D02+SARAH
	AND	#$0F
	BNE	NOOAD4
	CPY	#0
	BEQ	DOOAD4
NOOAD4:
	INY
	TAX
DOOAD4:
	LDA	NTAB2,X
	STA	PTR2
;
	LDA	D01+SARAH
	AND	#$0F
	BNE	NOOAD5
	CPY	#0
	BEQ	DOOAD5
NOOAD5:
	INY
	TAX
DOOAD5:
	LDA	NTAB2,X
	STA	PTR1
;
	LDA	D00+SARAH
	AND	#$0F
	TAX
	LDA	NTAB2,X
	STA	PTR0	
;
	LDX	PLAYER
	LDA	PLAYT0,X
	TAY
	LDA	#5
	JSR	SETSC0
;
	LDX	#$0A
;
	LDA	D05+SARAH
	LSR
	LSR
	LSR
	LSR
	TAY
	BEQ	DOOAD12
	TAX
DOOAD12:
	LDA	NTAB2,X
	STA	PTR5
;
	LDA	D04+SARAH
	LSR
	LSR
	LSR
	LSR
	BNE	NOOAD22
	CPY	#0
	BEQ	DOOAD22
NOOAD22:
	INY
	TAX
DOOAD22:
	LDA	NTAB2,X
	STA	PTR4
;
	LDA	D03+SARAH
	LSR
	LSR
	LSR
	LSR
	BNE	NOOAD32
	CPY	#0
	BEQ	DOOAD32
NOOAD32:
	INY
	TAX
DOOAD32:
	LDA	NTAB2,X
	STA	PTR3
;
	LDA	D02+SARAH
	LSR
	LSR
	LSR
	LSR
	BNE	NOOAD42
	CPY	#0
	BEQ	DOOAD42
NOOAD42:
	INY
	TAX
DOOAD42:
	LDA	NTAB2,X
	STA	PTR2
;
	LDA	D01+SARAH
	LSR
	LSR
	LSR
	LSR
	BNE	NOOAD52
	CPY	#0
	BEQ	DOOAD52
NOOAD52:
	INY
	TAX
DOOAD52:
	LDA	NTAB2,X
	STA	PTR1
;
	LDA	D00+SARAH
	LSR
	LSR
	LSR
	LSR
	TAX
	LDA	NTAB2,X
	STA	PTR0
;
	LDY	#0
	LDA	GAME
	BPL	NOSHOW1PL
	LDX	PLAYER
	LDA	PLAYT1,X
	TAY
NOSHOW1PL:
	LDA	#5
	JSR	BEFSCO
;
SCORK1:
	LDA	BONUSR
	BEQ	NOBONUSRND
	LDX	#23
	BNE	NOHITB
NOBONUSRND:
	LDY	PLAYER
	LDX	POWER,Y
;
	LDA	POWERT2,X
	BNE	NOHITB
	LDA	PWRTMR
	BNE	NOHITB
	LDA	FRMCNT
	BPL	NOHITB
	LDX	#24
NOHITB:
	LDA	GRLO2,X
	TAY
	INY
	LDA	GRLOC,X
	TAX
	JSR	DOHIGH
NOCHHB:
	LDY	#WHITE
	STX	WORK
	LDA	BONUSR
	BNE	NOBLHI
	LDA	PWRTMR
	CMP	#30
	BCS	NOBLHI
GLOWTM:
	LDY	FRMCNT
NOBLHI:
	LDX	PLAYER
	LDA	POWER,X
	BPL	NOBLANK
	LDY	#0
NOBLANK:
	LDX	WORK
	JSR	DOTEXT
;
	LDY	#0
	JSR	DOHIGH
	LDX	LIVES0
	INX
	CPX	#6
	BCC	NOMAXL
	LDX	#5
NOMAXL:
	LDA	GAME
	BPL	NOLV1
	LDY	LIVES1
	INY
	CPY	#6
	BCC	NOLV1
	LDY	#5
NOLV1:
	LDA	BONUSR
	BNE	BONUSK
	JMP	NOBONS
BONUSK:
	LDA	BONTIM
	SED
	SEC
	SBC	#1
	CLD
	STA	WORK
	AND	#$0F
	TAX
	LDA	NTABL,X
	STA	PTR0
;
	LDA	WORK
	LSR
	LSR
	LSR
	LSR
	TAX
	LDA	NTABL,X
	STA	PTR1
	LDY	#4
LOAD2X2:
	LDA	(PTR1),Y
	ASL
	ASL
	ASL
	ASL
	ORA	(PTR0),Y
	STA	NUM00,Y
	DEY
	BPL	LOAD2X2
;
	LDY	#0
	STY	NUM11
	JSR	DOHIGH
;
	LDA	#NUM00&$FF
	STA	PTR5
	LDA	#NUM00&$FF00/256
	STA	PTR5+1
	LDA	#NADA&$FF
	STA	PTR4
	LDX	#$0A
;
	LDA	S3
	CMP	#$0A
	BCC	NOMASKS4
	LDY	#9
	LDA	FRMCNT
	AND	#$03
	BNE	NOMASKS3
	LDY	#$0A
NOMASKS3:
	TYA
NOMASKS4:
	TAY
	BEQ	DOOA1
NOOA1:
	TAX
DOOA1:
	LDA	NTAB2,X
	STA	PTR3
	LDA	S2
	BNE	NOOA2
	CPY	#0
	BEQ	DOOA2
NOOA2:
	INY
	TAX
DOOA2:
	LDA	NTAB2,X
	STA	PTR2
;
	LDA	S1
	BNE	NOOA3
	CPY	#0
	BEQ	DOOA3
NOOA3:
	INY
	TAX
DOOA3:
	LDA	NTAB2,X
	STA	PTR1
	LDX	S0
	LDA	NTAB2,X
	STA	PTR0	
	LDY	#LTGRN
	BNE	AFTHIR
NOBONS:
	LDA	L1L,X
	STA	PTR5
	LDA	L1L,Y
	STA	PTR0
	LDA	#NADA&$FF
	STA	PTR1
	LDY	PLAYER
	LDX	HIERO0+SARAH,Y
	LDA	H1L,X
	STA	PTR4
	LDX	HIERO1+SARAH,Y
	LDA	H1L,X
	STA	PTR3
	LDX	HIERO2+SARAH,Y
	LDA	H1L,X
	STA	PTR2
;
	LDY	#LTGREY
AFTHIR:
	LDA	#5
	JSR	BEFSCO
VALUEK:
	LDA	HIGHS
	STA	PTR0+1
	STA	PTR1+1
;
	LDA	VV0+SARAH
	STA	WORK
	LDA	VV1+SARAH
	STA	WORK+1
	LDA	VV2+SARAH
	STA	WORK+2
	JSR	DOWORK
;
	LDX	WORK
	LDA	NTABL,X
	STA	PTR0
;
	LDX	WORK0
	LDA	NTABL,X
	STA	PTR1
	LDY	#4
LOAD2X:
	LDA	(PTR1),Y
	ASL
	ASL
	ASL
	ASL
	ORA	(PTR0),Y
	STA	NUM00,Y
	DEY
	BPL	LOAD2X
;
	LDY	#0
	JSR	DOHIGH
	LDX	WORK1
	TXA
	CLC
	ADC	WORK0
	ADC	WORK
	BEQ	NOZEROOUT
;
	CPX	#0
	BNE	NOZEROOUT
	LDX	#$0A
NOZEROOUT:
	LDA	NTABL,X
	STA	PTR5
	LDA	#NUM00&$FF
	STA	PTR4
	LDA	#NUM00&$FF00/256
	STA	PTR4+1
	LDA	#NADA&$FF
	STA	PTR3
;
	LDA	NJEWL
	STA	WORK
	LDA	NJEWL+1
	STA	WORK+1
	LDA	NJEWL+2
	STA	WORK+2
;
	JSR	DOWORK
;
	LDX	WORK
	LDA	JTAB,X
	STA	PTR2
	LDA	JTABH
	STA	PTR2+1
	LDX	WORK0
	LDA	ETAB,X
	STA	PTR1
	LDX	WORK1
	LDA	BTAB,X
	STA	PTR0
;
AFTJ1:
	LDY	#PINK
	JSR	DOTEX
	JSR	ENDS
GOOUT:
	LDA	#OUTBK1&$FF
	STA	JMPPTR
	LDA	#OUTBK1&$FF00/256
	STA	JMPPTR+1
	JMP	EXIT1
;
;----------------------
DOWORK:
	LDX	#2
	LDA	PLAYER
	BNE	WORKP1
LOOPOF:
	LDA	WORK,X
	AND	#$0F
	STA	WORK,X
	DEX
	BPL	LOOPOF
	RTS
WORKP1:
LOOPLS:
	LDA	WORK,X
	LSR
	LSR
	LSR
	LSR
	STA	WORK,X
	DEX
	BPL	LOOPLS
OUTWRK:
	RTS
	
DOHIGH:
	LDA	HIGHS,Y
	STA	PTR5+1
	STA	PTR4+1
	STA	PTR3+1
	STA	PTR2+1
	STA	PTR1+1
	STA	PTR0+1
	RTS
;------------
DOSCO:
	LDA	N1L,X
	STA	PTR5
	LDA	N1L+1,X
 	STA	PTR4
	LDA	N1L+2,X
	STA	PTR3
	LDA	N1L+3,X
	STA	PTR2
	LDA	N1L+4,X
	STA	PTR1
	LDA	N1L+5,X
	STA	PTR0
	RTS
;
ENDS:
	STA	GRP0
	STA	GRP1
	STA	VDELP0
	STA	VDELP1
	LDA	#LTGRN
	STA	COLUP0
	STA	COLUP1
	RTS
;-------------
BANK1C:
	LDA	SPSHOW
	BEQ	NOSPANIM
SETSPH:
	LDA	#SPH5&$FF
	STA	PTR0
	LDA	#SPH4&$FF
	STA	PTR1
	LDA	#SPH3&$FF
	STA	PTR2
	LDA	#SPH0&$FF
	STA	PTR5
	LDA	#SPH5&$FF00/256
	STA	PTR0+1
	STA	PTR5+1
	LDA	#SPH4&$FF00/256
	STA	PTR1+1
	STA	PTR2+1
;
	LDX	SPANIM
	LDA	SPL,X
	STA	PTR4
	LDA	SPH,X
	STA	PTR4+1
	DEX
	LDA	SPL,X
	STA	PTR3
	LDA	SPH,X
	STA	PTR3+1
;
NOSPANIM:
	LDA	#OUTBK2&$FF
	STA	JMPPTR
	LDA	#OUTBK2&$FF00/256
	STA	JMPPTR+1
	JMP	EXIT1
;-------------------------------------
;INCLIV:
;	STX	WORK2
;	LDY	PLAYER
;	LDX	LIVES0,Y
;	CPX	#MAXLIF
;	BEQ	NOEXL6
;	LDX	PLAYER
;	INC	LIVES0,X
;NOEXL6:
;	LDX	WORK2
;	RTS
	.EJECT
;----------------------
;	BANK1 TABLES
;----------------------------
;0	= GET 3 HIROS *
;1	= EXTRA LIFE		BIRD,BIRD,BIRD
;2	= POINTS *
;3	= AIR BOMB *		CANE,BIRD,BOWL  1+NOV,STD
;4	= KILL MINIS *		SUN,CANE,BOWL
;5  	= KILL POTS *		EYE,BIRD,FEATHER
;6	= KILL SPHINX *		SUN,FEATHER,CANE
;7	= EXTRA BONUS *		MAN,MAN,CANE	3+NOV,2+STD
;8	= KILL UWES *		ANKH,SUN,CANE
;9	= WARP *		BIRD,CANE,BOWL  1+NOV,STD
;10	= AIR FREEZE *		EYE,BOWL,BOWL	4+NOV,3+STD,2+ADV
;11	= DECOY *		BOWL,MAN,MAN	3+NOV,2+STD
;12	= SHACKLES *		BOWL,BOWL,SUN	4+NOV,3+STD,2+ADV
;13	= POLYWATER *		MAN,BOWL,BIRD	     " "
;14	= BROKEN WING *		CANE,SUN,FEATHER     " "
;15	= HOLD SPHINX *		EYE,FEATHER,BIRD
;16	= HOVER *		EYE,MAN,ANKH	     " "
;17	= SPEED UP *		MAN,SUN,MAN     1+NOV,STD
;18	= SLOW DOWN *		EYE,EYE,FEATHER 3+NOV,2+STD
;19	= ROULETTE *		ANKH,FEATHER,BOWL
;20	= OMNICIDE *		SUN,MAN,MAN
;21	= QUICK SHOTS *		BOWL,MAN,ANKH
;22	= INVINCIBLE *		FEATHER,EYE,BOWL
;
;
POWERT2:
	DC.B	$01,PTT,50,$00,$00,$00,$00,PTT,$00,$00,$00,$00,PTM,PTM
;		    XLF PTS  AB  KM  KP  KS  EB  KU  WP  AF DEC  SH PLY
	DC.B	PTM,$00,PTM,$00,PTM,PTT,$00,$00,$00,1,1,1,1,1
;		 BW  HS HOV  SU  SD ROU OMN  QS INV				
JTAB:	DC.B	J1&$FF,J2&$FF,J3&$FF,J4&$FF,J5&$FF
JTABH:	DC.B	GRLO2&$FF00/256
ETAB:	DC.B	E1&$FF,E2&$FF,E3&$FF,E4&$FF,E5&$FF
BTAB:	DC.B	B1&$FF,B2&$FF,B3&$FF,B4&$FF,B5&$FF
SPCOLOR2:
	DC.B	$21,$00,$41,$C2,$02,$82,$03,$C2
OFFSE1:	DC.B	$01,$10
PLAYT0:
SELNOV:	DC.B	WHITE,MGREY,MGREY
PLAYT1:
SELSTA:	DC.B	MGREY,WHITE
SELADV:	DC.B	MGREY,MGREY,WHITE
SELEXP:	DC.B	MGREY,MGREY,MGREY,WHITE
;
N1L:
	DC.B	GH0&$FF,GH1&$FF,GH2&$FF,GH3&$FF,GH4&$FF,GH5&$FF ;6
	DC.B	EX0&$FF,EX1&$FF,EX2&$FF,EX3&$FF,EX4&$FF,EX5&$FF ;12
	DC.B	SPACE&$FF,PT0&$FF,PT1&$FF,PT2&$FF,PT3&$FF,SPACE&$FF ;18
	DC.B	SPACE&$FF,AB0&$FF,AB1&$FF,AB2&$FF,AB3&$FF,AB4&$FF	;24
	DC.B	KL0&$FF,KL1&$FF,KM0&$FF,KM1&$FF,KM2&$FF,KM3&$FF ;30
	DC.B	KL0&$FF,KL1&$FF,KP0&$FF,KP1&$FF,KP2&$FF,KP3&$FF ;36
	DC.B	KL0&$FF,KL1&$FF,KS0&$FF,KS1&$FF,KS2&$FF,KS3&$FF ;42
	DC.B	EB0&$FF,EB1&$FF,EB2&$FF,EB3&$FF,EB4&$FF,EB5&$FF ;48
	DC.B	KU0&$FF,KU1&$FF,KU2&$FF,KU3&$FF,KU4&$FF,KU5&$FF ;54
	DC.B	SPACE1&$FF,WP0&$FF,WP1&$FF,WP2&$FF,SPACE1&$FF,SPACE1&$FF ;60
	DC.B	AF0&$FF,AF1&$FF,AF2&$FF,AF3&$FF,AF4&$FF,AF5&$FF ;66
	DC.B	SPACE1&$FF,DC0&$FF,DC1&$FF,DC2&$FF,SPACE1&$FF,SPACE1&$FF ;72
	DC.B	SH0&$FF,SH1&$FF,SH2&$FF,SH3&$FF,SH4&$FF,SPACE1&$FF ;78
	DC.B	PL0&$FF,PL1&$FF,PL2&$FF,PL3&$FF,PL4&$FF,PL5&$FF	;84
	DC.B	BK0&$FF,BK1&$FF,BK2&$FF,BK3&$FF,BK4&$FF,BK5&$FF ;90
	DC.B	HS0&$FF,HS1&$FF,HS2&$FF,HS3&$FF,HS4&$FF,HS5&$FF ;96
	DC.B	SPACE2&$FF,HV0&$FF,HV1&$FF,HV2&$FF,SPACE2&$FF,SPACE2&$FF ;102
	DC.B	SU0&$FF,SU1&$FF,SU2&$FF,SU3&$FF,SU4&$FF,SPACE2&$FF ;108
	DC.B	SD0&$FF,SD1&$FF,SD2&$FF,SD3&$FF,SD4&$FF,SD5&$FF ;114
	DC.B	RL0&$FF,RL1&$FF,RL2&$FF,RL3&$FF,RL4&$FF,SPACE2&$FF ;120
	DC.B	OM0&$FF,OM1&$FF,OM2&$FF,OM3&$FF,OM4&$FF,SPACE2&$FF ;126
	DC.B	QS0&$FF,QS1&$FF,QS2&$FF,QS3&$FF,QS4&$FF,QS5&$FF ;132
	DC.B	IN0&$FF,IN1&$FF,IN2&$FF,IN3&$FF,IN4&$FF,IN5&$FF ;138
	DC.B	TOP0&$FF,TOP1&$FF,TOP2&$FF,TOP3&$FF,TOP4&$FF,TOP5&$FF; 144
	DC.B	BOT0&$FF,BOT1&$FF,BOT2&$FF,BOT3&$FF,BOT1&$FF,BOT5&$FF ;150
	DC.B	CPY0&$FF,CPY1&$FF,CPY2&$FF,CPY3&$FF,CPY4&$FF,CPY5&$FF ;156
	DC.B	PL10&$FF,PL11&$FF,PL12&$FF,PL13&$FF,PL14&$FF,SPACE3&$FF ;162
	DC.B	PL20&$FF,PL11&$FF,PL12&$FF,PL13&$FF,PL14&$FF,PL25&$FF ;168
	DC.B	SPACE3&$FF,DF00&$FF,DF01&$FF,DF02&$FF,DF03&$FF,SPACE3&$FF ;174
	DC.B	DF10&$FF,DF11&$FF,DF12&$FF,DF13&$FF,DF14&$FF,SPACE3&$FF ;180
	DC.B	DF20&$FF,DF21&$FF,DF22&$FF,DF23&$FF,DF24&$FF,SPACE3&$FF ;186
	DC.B	SPACE3&$FF,DF30&$FF,DF31&$FF,DF32&$FF,DF33&$FF,SPACE3&$FF ;192
	DC.B	TM0&$FF,TM1&$FF,TM2&$FF,TM3&$FF,TM4&$FF,TM5&$FF ;192
	DC.B	HB0&$FF,HB1&$FF,HB2&$FF,HB3&$FF,HB4&$FF,HB5&$FF ;
	DC.B	GO0&$FF,GO1&$FF,GO2&$FF,GO3&$FF,GO4&$FF,GO5&$FF 
	DC.B	PL11&$FF,PL12&$FF,PL13&$FF,PL14&$FF,SPACE&$FF,PL10&$FF
	DC.B	PL11&$FF,PL12&$FF,PL13&$FF,PL14&$FF,SPACE&$FF,PL20&$FF
NTABL:
	DC.B	NUM0&$FF,NUM1&$FF,NUM2&$FF,NUM3&$FF,NUM4&$FF,NUM5&$FF
	DC.B	NUM6&$FF,NUM7&$FF,NUM8&$FF,NUM9&$FF,NADA&$FF
;
HIGHS:
	DC.B	NUM0&$FF00/256,GH0&$FF00/256,KU0&$FF00/256,HV0&$FF00/256
	DC.B	PL10&$FF00/256,CPY0&$FF00/256,DF00&$FF00/256,TOP0&$FF00/256
NTAB2:
	DC.B	NM0&$FF,NM1&$FF,NM2&$FF,NM3&$FF,NM4&$FF,NM5&$FF
	DC.B	NM6&$FF,NM7&$FF,NM8&$FF,NM9&$FF
L1L:
	DC.B	NADA&$FF,NADA&$FF,LIFE1&$FF,LIFE2&$FF,LIFE3&$FF,LIFE4&$FF
H1L:
	DC.B	NADA&$FF,CANET&$FF,MANT&$FF,FEATHT&$FF,EYET&$FF
	DC.B	ANKHT&$FF,BIRDT&$FF,SUNT&$FF,BOWLT&$FF
;
GRLOC:	DC.B	0,6,12,18,24,30,36,42,48,54,60,66,72
	DC.B	78,84,90,96,102,108,114,120,126,132,192,198,204,210,216
;
	.ORG	BANK1+$840
SPACE3:
	DC.B	0,0,0,0,0
DF00:
	DC.B	$93,$94,$B4,$D4,$93
DF01:	
	DC.B	$08,$94,$94,$A2,$22
DF02:
	DC.B	$E7,$48,$48,$48,$E7
DF03:
	DC.B	$78,$40,$70,$40,$78
DF10:
	DC.B	$E2,$12,$62,$82,$77
DF11:
	DC.B	$4A,$4A,$7A,$4B,$32
DF12:
	DC.B	$5C,$52,$D2,$52,$5C
DF13:
	DC.B	$94,$94,$F7,$94,$67
DF14:
	DC.B	$B8,$A4,$24,$A4,$38
DF20:
	DC.B	$97,$94,$F4,$94,$67
DF21:
	DC.B	$08,$94,$94,$A2,$22
DF22:
	DC.B	$94,$94,$F5,$96,$64
DF23:
	DC.B	$9D,$A1,$A1,$A1,$9D
DF24:
	DC.B	$EE,$09,$C9,$09,$EE
DF30:
	DC.B	$F4,$82,$E1,$82,$F4
DF31:
	DC.B	$50,$90,$1C,$92,$5C
DF32:
	DC.B	$F4,$84,$E7,$84,$F7
DF33:
	DC.B	$90,$90,$10,$90,$38
;
PL10:
	DC.B	$E0,$40,$40,$C0,$40
PL11:
	DC.B	$43,$42,$72,$4A,$72
PL12:
	DC.B	$D2,$12,$1E,$12,$0C
PL13:
	DC.B	$23,$22,$23,$52,$8B
PL20:
	DC.B	$F0,$40,$20,$90,$60
PL14:
	DC.B	$D2,$12,$9C,$12,$DC
PL25:
	DC.B	$E0,$10,$60,$80,$70
CPY0:
	DC.B	$79,$B5,$A5,$B5,$78
CPY1:
	DC.B	$24,$24,$E4,$24,$CE
CPY2:
	DC.B	$94,$94,$F7,$94,$67
CPY3:
	DC.B	$B9,$90,$10,$91,$38
CPY4:
	DC.B	$D8,$85,$9C,$A5,$98
CPY5:
	DC.B	$C2,$22,$C2,$23,$CF
;
GO0:	DC.B	$64,$94,$B7,$84,$63	;GAME OVER
GO1:	DC.B	$A2,$A2,$AA,$B6,$22
GO2:	DC.B	$F0,$80,$E0,$80,$F0
GO3:	DC.B	$61,$92,$94,$94,$64
GO4:	DC.B	$1E,$90,$5C,$50,$5E
GO5:	DC.B	$90,$A0,$E0,$90,$E0
;
	.ORG	BANK1+$900
SPL:
	DC.B	SPH5&$FF,SPH4&$FF,SPH3&$FF,SPH2&$FF,SPH1&$FF,SPH0&$FF
	DC.B	ROT0&$FF,ROT1&$FF,SPH2&$FF,SPH6&$FF
SPH:
	DC.B	SPH5&$FF00/256,SPH4&$FF00/256,SPH3&$FF00/256
	DC.B	SPH2&$FF00/256,SPH1&$FF00/256,SPH0&$FF00/256
	DC.B	ROT0&$FF00/256,ROT1&$FF00/256,SPH2&$FF00/256,SPH6&$FF00/256
;
BOT0:
	DC.B	$00,$00,$00,$00,$00,$00
	DC.B	$01,$00,$3F,$00,$FF,$00,$3F,$00,$01
TOP0:	DC.B	$00
	DC.B	$E0,$E0,$E0,$E0,$E0,$E0,$FC,$FC
	DC.B	$E0,$E0,$FF,$FF,$00,$F8,$EC,$E6
	DC.B	$E6,$E6,$E6,$E6,$E6,$E6,$EE,$FC
	DC.B	$F8
BOT1:
	DC.B	$00,$00,$00,$00,$00,$00
	DC.B	$FF,$00,$FF,$00,$FF,$00,$FF,$00,$FF
TOP1:	DC.B	$00
	DC.B	$E7,$E7,$FF,$FF,$E7,$E7,$E7,$E7
	DC.B	$E7,$67,$7E,$3C,$00,$FE,$FE,$E0
	DC.B	$E0,$E0,$E0,$FC,$FC,$E0,$E0,$FE
	DC.B	$FE
BOT2:
	DC.B	$0F,$00,$3F,$00,$0F,$00
	DC.B	$FF,$00,$FF,$00,$FF,$00,$FF,$00,$FF
TOP2:	DC.B	$00
	DC.B	$7F,$7F,$70,$70,$70,$70,$70,$70
	DC.B	$70,$70,$70,$70,$00,$3C,$7E,$FF
	DC.B	$F7,$1E,$3C,$78,$F7,$FF,$FF,$7E
	DC.B	$3C
BOT3:
	DC.B	$F0,$00,$FC,$00,$F0,$00
	DC.B	$FF,$00,$FF,$00,$FF,$00,$FF,$00,$FF
TOP3:	DC.B	$00
	DC.B	$3F,$7F,$F0,$E0,$E0,$E0,$E0,$E0
	DC.B	$E0,$F0,$7F,$3F,$00,$7F,$7F,$70
	DC.B	$70,$70,$70,$7E,$7E,$70,$70,$7F
	DC.B	$7F
TOP4:	DC.B	$00
	DC.B	$3C,$7E,$FE,$C6,$C6,$C6,$C6,$C6
	DC.B	$C6,$FE,$FE,$7C,$00,$71,$71,$73
	DC.B	$7F,$7E,$7F,$7F,$73,$73,$77,$7E
	DC.B	$7C
BOT5:
	DC.B	$00,$00,$00,$00,$00,$00
	DC.B	$80,$00,$FC,$00,$FF,$00,$FC,$00,$80
TOP5:	DC.B	$00
	DC.B	$E3,$E3,$E3,$E3,$E7,$EF,$FF,$FF
	DC.B	$FF,$FB,$F3,$E3,$00,$DC,$DC,$9C
	DC.B	$1C,$1C,$1C,$9C,$9C,$9C,$1C,$7F
	DC.B	$7F
;
	.ORG	BANK1+$A00
;
SPH3:	DC.B	$00,$00,$C0,$C0
	DC.B	$C0,$C0,$C0,$C0,$C0,$C0,$F0,$F0,$FC,$FC,$FF,$FF,$FF,$FF,$FF,$FF
	DC.B	$EF,$EF,$FF,$FF,$BB,$BB,$EE,$EE,$AB,$AB,$6A,$6A,$9B,$9B,$66,$66
	DC.B	$9A,$9A,$E6,$E6,$B9,$B9,$7E,$7E,$6F,$6F,$9A,$9A,$25,$25,$00
SPH4:	DC.B	$00,$00,$00,$00
	DC.B	$00,$00,$00,$00,$00,$00,$3E,$3E,$FA,$FA,$F7,$F7,$DE,$DE,$77,$77
	DC.B	$5A,$5A,$DF,$DF,$FE,$FE,$FA,$FA,$EB,$EB,$EA,$EA,$EA,$EA,$E6,$E6
	DC.B	$EA,$EA,$F9,$F9,$6E,$6E,$9B,$9B,$56,$56,$95,$95,$AA,$AA,$28,$28
;
SPHL:
	DC.B	0,$00,$03,$0F,$0F,$0E,$0E,$09,$09,$07,$07,$05
SPHH:
	DC.B	0,$F0,$F0,$BC,$BC,$AF,$AF,$FB,$FB,$6E,$6E,$9B
ROT0:
	DC.B	$6E,$6E,$EB,$EB,$FA,$FA,$FF,$FF,$BF,$BF
	DC.B	$BE,$BE,$EE,$EE,$EF,$EF,$AF,$AF,$AF,$AF,$AB,$AB,$AB,$AB
	DC.B	$AB,$FB,$FB,$DB,$DB,$FF,$FA,$FA,$AE,$AE,$FF,$FF
	DC.B	$FB,$FB,$6F,$6F,$DA,$DA,$F6,$F6,$A0,$A0,$00,$00,$00,$00,$00,$00
;
SPH2:	DC.B	$6E,$6E,$EB,$EB
	DC.B	$FA,$FA,$FF,$FF,$FF,$FF,$BE,$BE,$BE,$BE,$BF,$BF,$BF,$BF,$BF,$BF
	DC.B	$AE,$AE,$AE,$AE,$EF,$EF,$6E,$6E,$FF,$FF,$AB,$AB,$BA,$BA,$FD,$FD
	DC.B	$FB,$FB,$6F,$6F,$DA,$DA,$F6,$F6,$A0,$A0,$00,$00,$00,$00,$00,$00
;
	.ORG	BANK1+$B00
SPH6:	DC.B	$01,$01,$C3,$C3
	DC.B	$FE,$FE,$C7,$C7,$83,$01,$00,$00,$00,$00,$00,$00,$00,$00,$00,$00
	DC.B	$00,$00,$00,$00,$00,$00,$00,$00,$00,$00,$00,$00,$01,$83,$C7,$C7
	DC.B	$FE,$FE,$2F,$2F,$2B,$2B,$0A,$0A,$02,$02,$00,$00,$00,$00,$00,$00
;
ROT1:
	DC.B	$01,$01,$C3,$C3,$FF,$FF,$BF,$BF,$BF,$BE
	DC.B	$FE,$FE,$FB,$FB,$FB,$FB,$FA,$FA,$FF,$FF,$F6,$F6,$F6,$F6
	DC.B	$FA,$FA,$FE,$FE,$F7,$F7,$FE,$FE,$EA,$EA,$FA,$FA
	DC.B	$FE,$FE,$2F,$2F,$2B,$2B,$0A,$0A,$02,$02,$00,$00,$00,$00,$00,$00
	
SPH1:	DC.B	$01,$01,$C3,$C3
	DC.B	$FF,$FF,$BF,$BF,$BF,$BE,$FA,$FA,$EF,$EF,$FE,$FE,$EA,$EA,$FE,$FE
	DC.B	$DA,$DA,$DA,$DA,$EB,$EB,$FB,$FB,$DF,$DF,$FA,$FA,$AA,$AA,$EB,$EB
	DC.B	$FE,$FE,$2F,$2F,$2B,$2B,$0A,$0A,$02,$02,$00,$00,$00,$00,$00,$00
;
SPH0:	DC.B	$0F,$3F,$FB,$FB
	DC.B	$EA,$EA,$9F,$9F,$76,$76,$59,$59,$16,$16,$05,$05,$03,$03,$02,$02
	DC.B	$01,$01,$03,$03,$03,$03,$02,$02,$01,$01,$03,$03,$03,$03,$01,$01
	DC.B	$02,$02,$00,$00,$00
;
SPH5:	DC.B	$00,$00,$00,$00
	DC.B	$00,$00,$00,$00,$00,$00,$B0,$B0,$6C,$6C,$AB,$AB,$FA,$FA,$BF,$BF
	DC.B	$FB,$FB,$FE,$FE,$BF,$BF,$EE,$EE,$BB,$BB,$AF,$AF,$BB,$BB,$AE,$AE
	DC.B	$AB,$AB,$99,$99,$56,$56,$98,$98,$60,$60,$80,$80,$00,$00,$00,$00
;
	.ORG BANK1 +$C00
;
LIFE1:	DC.B	$80,$80,$80,$80,$80,$00
LIFE2:	DC.B	$A0,$A0,$A0,$A0,$A0,$00
LIFE3:	DC.B	$A8,$A8,$A8,$A8,$A8,$00
LIFE4:  DC.B	$AA,$AA,$AA,$AA,$AA,$00		;X
LIFE5:	DC.B	$02,$02,$02,$02,$02
NADA:
	DC.B	0,0,0,0,0,0,0
;
CANET:
	DC.B	$08,$08,$08,$48,$48,$30
MANT:
	DC.B	$54,$5E,$7E,$5E,$4C,$EC
FEATHT:
	DC.B	$08,$08,$08,$18,$18,$10
EYET:
	DC.B	$3C,$53,$44,$38,$03,$3C
;	DC.B	$78,$84,$B4,$78,$06,$78
ANKHT:
	DC.B	$30,$30,$FC,$30,$48,$30
BIRDT:
	DC.B	$0A,$0E,$1E,$1C,$38,$18
;	DC.B	$28,$3C,$78,$70,$E0,$60
SUNT:
	DC.B	$DB,$6C,$18,$3C,$3C,$18
BOWLT:
	DC.B	$38,$7C,$FE,$FE,$FE,$00
;
E1:	DC.B	$46,$4F,$4F,$46,$40
E2:	DC.B	$E6,$8F,$6F,$26,$C0
E3:	DC.B	$C6,$2F,$CF,$26,$C0
E4:	DC.B	$26,$2F,$EF,$A6,$80
E5:	DC.B	$C6,$2F,$CF,$86,$E0
;
B1:	DC.B	$4F,$4F,$4F,$40,$40
B2:	DC.B	$EF,$8F,$6F,$20,$C0
B3:	DC.B	$CF,$2F,$CF,$20,$C0
B4:	DC.B	$2F,$2F,$EF,$A0,$80
B5:	DC.B	$CF,$2F,$CF,$80,$E0
;
NUM0:	DC.B	$0E,$0A,$0A,$0A,$0E		;0 RIGHT
NUM1:	DC.B	$04,$04,$04,$04,$04		;1 RIGHT
NUM2:	DC.B	$0E,$08,$06,$02,$0C		;2 RIGHT
NUM3:	DC.B	$0C,$02,$0C,$02,$0C		;3 RIGHT
NUM4:	DC.B	$02,$02,$0E,$0A,$08		;4
NUM5:	DC.B	$0C,$02,$0C,$08,$0E		;5
NUM6:	DC.B	$04,$0A,$0C,$08,$06		;6
NUM7:	DC.B	$04,$04,$04,$02,$0E		;7
NUM8:	DC.B	$0E,$0A,$0E,$0A,$0E
NUM9:	DC.B	$02,$02,$0E,$0A,$0E
;
NM0:	DC.B  $3C,$66,$66,$66,$66,$3C	;ZERO 
NM1:	DC.B  $7E,$18,$18,$18,$38,$18	;ONE 
NM2:	DC.B  $7E,$30,$18,$0C,$66,$3C  ;TWO 
NM3:	DC.B  $3C,$66,$0C,$18,$0C,$7E	;THREE 
NM4:	DC.B  $0C,$7E,$6C,$3C,$1C,$0C	;FOUR 
NM5:	DC.B  $3C,$66,$06,$7C,$60,$7E	;FIVE 
NM6:	DC.B  $3C,$66,$66,$7C,$60,$3C	;SIX 
NM7:	DC.B  $30,$30,$18,$0C,$06,$7E	;SEVEN 
NM8:	DC.B  $3C,$66,$66,$3C,$66,$3C	;EIGHT 
NM9:	DC.B  $38,$0C,$06,$3E,$66,$3C	;NINE 
	.ORG BANK1+$D00
GRLO2:
SPACE:
	DC.B	0,0,0,0,0,0,0,0,1,1,1,1,1,1,1,1,2,2,2,2,2,2,2,2,1,5,4,4
;
GH0:	DC.B	$77,$94,$B7,$84,$77	;GET 3 HIEROS
GH1:	DC.B	$91,$10,$10,$10,$B9
GH2:	DC.B	$89,$49,$8F,$49,$89
GH3:	DC.B	$77,$24,$27,$24,$77
GH4:	DC.B	$A4,$25,$39,$25,$B8
GH5:	DC.B	$CF,$21,$2F,$28,$CF
;
EX0:	DC.B	$F4,$82,$E1,$82,$F4	;EXTRA LIFE
EX1:	DC.B	$44,$84,$04,$84,$5F
EX2:	DC.B	$4A,$4A,$73,$4A,$71
EX3:	DC.B	$47,$44,$C4,$44,$84
EX4:	DC.B	$BA,$12,$13,$12,$3B	
EX5:	DC.B	$1E,$10,$9C,$10,$DE
;
PT0:	DC.B	$83,$84,$E4,$94,$E3	;POINTS
PT1:	DC.B	$3A,$92,$92,$93,$3A
PT2:	DC.B	$44,$44,$C4,$44,$5F
PT3:	DC.B	$78,$08,$78,$40,$78
;
AB0:	DC.B	$97,$92,$F2,$92,$67	;AIR BOMB
AB1:	DC.B	$48,$48,$70,$48,$70
AB2:	DC.B	$E3,$94,$E4,$94,$E3
AB3:	DC.B	$2A,$AA,$AA,$B6,$22
AB4:	DC.B	$E0,$90,$E0,$90,$E0
;
KL0:	DC.B	$97,$A2,$C2,$A2,$97	;KILL
KL1:	DC.B	$7B,$42,$42,$42,$42
;
KM0:	DC.B	$C2,$02,$02,$03,$02	;MINIS
KM1:	DC.B	$AE,$A4,$A4,$64,$2E
KM2:	DC.B	$97,$B2,$B2,$D2,$97
KM3:	DC.B	$78,$08,$78,$40,$78
;
KP0:	DC.B	$C2,$02,$03,$02,$03	;POTS
KP1:	DC.B	$0C,$12,$92,$52,$8C
KP2:	DC.B	$23,$20,$23,$22,$FB
KP3:	DC.B	$C0,$40,$C0,$00,$C0
;
KS0:	DC.B	$C7,$00,$07,$04,$07	;SPHINX
KS1:	DC.B	$A2,$A2,$B3,$2A,$B2
KS2:	DC.B	$BA,$92,$92,$93,$BA
KS3:	DC.B	$51,$CA,$C4,$4A,$51
;
EB0:	DC.B	$E8,$85,$C2,$85,$E8	;EXTRA BONUS
EB1:	DC.B	$92,$12,$13,$12,$BB
EB2:	DC.B	$54,$54,$9C,$54,$88
EB3:	DC.B	$62,$55,$65,$55,$62
EB4:	DC.B	$49,$4A,$5A,$6A,$4A
EB5:	DC.B	$B8,$88,$B8,$A0,$B8
;
J1:	DC.B	$46,$46,$4F,$4F,$46
J2:	DC.B	$E6,$86,$6F,$2F
J3:	DC.B	$C6,$26,$CF,$2F
J5:	DC.B	$C6,$26,$CF,$8F,$E6
J4:	DC.B	$26,$26,$EF,$AF,$86
;
	.ORG	BANK1 + $E00
;
SPACE1:	DC.B	0,0,0,0,0
KU0:	DC.B	$97,$A2,$C2,$A2,$97	;KILL EWE
KU1:	DC.B	$7B,$42,$42,$42,$42
KU2:	DC.B	$C0,$00,$00,$00,$00
KU3:	DC.B	$61,$91,$92,$94,$94
KU4:	DC.B	$47,$C4,$A7,$94,$17
KU5:	DC.B	$B8,$08,$38,$20,$B8
;
WP0:	DC.B	$28,$38,$54,$92,$82	;WARP
WP1:	DC.B	$94,$94,$F7,$94,$67
WP2:	DC.B	$A0,$A0,$38,$A4,$38
;
AF0:	DC.B	$97,$92,$F2,$92,$67	;AIR FREEZE
AF1:	DC.B	$48,$48,$70,$48,$70
AF2:	DC.B	$84,$84,$E7,$84,$F7
AF3:	DC.B	$BD,$A1,$39,$A1,$3D
AF4:	DC.B	$EF,$04,$C2,$01,$EF
AF5:	DC.B	$B8,$20,$38,$20,$BC
;
DC0:	DC.B	$E7,$94,$97,$94,$E7	;DECOY
DC1:	DC.B	$9C,$21,$21,$21,$9C
DC2:	DC.B	$C6,$26,$26,$29,$C9
;
SH0:	DC.B	$F4,$14,$F7,$84,$F4	;SHACKLES
SH1:	DC.B	$A4,$A5,$BD,$A5,$98
SH2:	DC.B	$E9,$0A,$0C,$0A,$E9
SH3:	DC.B	$7B,$42,$43,$42,$43
SH4:	DC.B	$DE,$02,$9E,$10,$DE
;
PL0:	DC.B	$83,$84,$E4,$94,$E3	;POLYWATER
PL1:	DC.B	$3C,$A0,$A0,$A1,$21
PL2:	DC.B	$C2,$C3,$C5,$29,$28
PL3:	DC.B	$89,$89,$4F,$29,$26
PL4:	DC.B	$11,$11,$11,$11,$7D
PL5:	DC.B	$E9,$09,$CE,$09,$EE
;
BK0:	DC.B	$CA,$AA,$CC,$AA,$CC	;BROKEN WING
BK1:	DC.B	$4A,$AA,$AC,$AA,$4A
BK2:	DC.B	$E9,$8B,$CB,$8D,$E9
BK3:	DC.B	$05,$07,$0A,$12,$10
BK4:	DC.B	$1D,$09,$89,$49,$5D
BK5:	DC.B	$27,$69,$6B,$A8,$27
;
HS0:	DC.B	$93,$94,$F4,$94,$93	;HOLD SPHINX
HS1:	DC.B	$3D,$A1,$A1,$A1,$21
HS2:	DC.B	$C7,$20,$27,$24,$C7
HS3:	DC.B	$A2,$A2,$B3,$2A,$B2
HS4:	DC.B	$BA,$92,$92,$93,$BA
HS5:	DC.B	$51,$CA,$C4,$4A,$51
;
;HB0:	DC.B	$97,$92,$F2,$92,$97	;HIT BUTTON
;HB1:	DC.B	$10,$10,$10,$10,$7C
;HB2:	DC.B	$71,$4A,$72,$4A,$72
;HB3:	DC.B	$84,$44,$44,$44,$5F
;HB4:	DC.B	$10,$11,$11,$11,$7C
;HB5:	DC.B	$C9,$2B,$2B,$2D,$C9
;
;HB0:	DC.B	$E3,$94,$94,$94,$E3	;DOUBLE HIT
;HB1:	DC.B	$19,$A5,$A5,$A5,$25
;HB2:	DC.B	$CF,$28,$C8,$28,$C8
;HB3:	DC.B	$78,$40,$70,$40,$78
;HB4:	DC.B	$4B,$49,$79,$49,$4B
;HB5:	DC.B	$90,$10,$10,$10,$B8
;
HB0:	DC.B	$E7,$94,$97,$94,$E7	;DBL - PRESS
HB1:	DC.B	$3C,$A0,$23,$A0,$20
HB2:	DC.B	$10,$10,$DC,$12,$1C
HB3:	DC.B	$97,$94,$E7,$94,$E7
HB4:	DC.B	$B9,$04,$18,$21,$9C
HB5:	DC.B	$C0,$20,$C0,$00,$E0
;
	.ORG	BANK1 +$F00
;
SPACE2:	DC.B	0,0,0,0,0
HV0:	DC.B	$93,$94,$F4,$94,$93	;HOVER
HV1:	DC.B	$08,$94,$94,$A2,$22
HV2:	DC.B	$E9,$89,$EE,$89,$EE
;
SU0:	DC.B	$F4,$14,$F7,$84,$F7	;SPEED UP
SU1:	DC.B	$3D,$21,$39,$A1,$3D
SU2:	DC.B	$EE,$09,$C9,$09,$EE
SU3:	DC.B	$06,$09,$09,$09,$09
SU4:	DC.B	$40,$40,$70,$48,$70
;
SD0:	DC.B	$F7,$14,$F4,$84,$F4	;SLOW DOWN
SD1:	DC.B	$98,$24,$24,$25,$19
SD2:	DC.B	$50,$70,$88,$04,$04
SD3:	DC.B	$71,$4A,$4A,$4A,$71
SD4:	DC.B	$85,$47,$48,$50,$90
SD5:	DC.B	$12,$16,$96,$5A,$52
;
RL0:	DC.B	$93,$94,$E4,$94,$E3	;ROULETTE
RL1:	DC.B	$19,$A5,$A5,$A5,$25
RL2:	DC.B	$EF,$08,$0E,$08,$0F
RL3:	DC.B	$22,$22,$22,$22,$77
RL4:	DC.B	$78,$40,$70,$40,$78
;
OM0:	DC.B	$65,$95,$95,$96,$64	;OMNICIDE
OM1:	DC.B	$52,$56,$56,$DA,$52
OM2:	DC.B	$E7,$48,$48,$48,$E7
OM3:	DC.B	$77,$24,$24,$24,$77
OM4:	DC.B	$3C,$A0,$B8,$A0,$3C
;
QS0:	DC.B	$79,$B2,$92,$92,$62	;QUICK SHOTS
QS1:	DC.B	$9C,$49,$49,$49,$5C
QS2:	DC.B	$E9,$0A,$0C,$0A,$E9
QS3:	DC.B	$7A,$0A,$7B,$42,$7A
QS4:	DC.B	$4C,$52,$D2,$52,$4C
QS5:	DC.B	$4F,$41,$4F,$48,$EF
;
IN0:	DC.B	$E9,$4B,$4B,$4D,$E9	;INVINCIBLE
IN1:	DC.B	$11,$28,$44,$44,$45
IN2:	DC.B	$D2,$96,$96,$9A,$D2
IN3:	DC.B	$77,$82,$82,$82,$77
IN4:	DC.B	$73,$4A,$72,$4A,$72
IN5:	DC.B	$DE,$10,$1C,$10,$1E
;
TM0:	DC.B	$4E,$44,$44,$44,$EE	;TIME SCORE
TM1:	DC.B	$8B,$8A,$AB,$DA,$8B
TM2:	DC.B	$C3,$00,$81,$02,$C1
TM3:	DC.B	$8E,$50,$90,$10,$CE
TM4:	DC.B	$64,$94,$97,$94,$67
TM5:	DC.B	$BC,$A0,$38,$A0,$3C
;
	.ORG	BANK1+$FD8
SWBEG0:
	LDA	$FFF6
	JMP	BEGIN0
;
	LDA	$FFF9
	JMP	(JMPPTR)
;
	LDA	$FFF8
	JMP	(JMPPTR)
;
	LDA	$FFF7
	JMP	(JMPPTR)
;
EXIT1:
	LDA	$FFF6
	JMP	(JMPPTR)
;
	DC.B	0,0,0,0
	.DC.W	SWBEG0
	.DC.W	SWBEG0
	.DC.W	SWBEG0
;
	.ORG	BANK2+$100
BANK2C:
	LDA	FRMCNT
	AND	#$01
	BNE	NOFRM11
	JSR	JOYSTK
	JSR	SCOREC
NOFRM11:
	JSR	COLLISIONS
	LDA	BONUSR
	BNE	SKIP88
	JSR	KILLC
	JSR	ALSHADC
	JSR	DETPOW
	JSR	POWERC
	JSR	SETMIS
	JSR	ENEMYMIS
SKIP88:
	LDA	SPSHOW
	BMI	OUTB2
	JSR	SETSCR
	JSR	RSTOBE
OUTB2:
	LDA	#OUTBK3&$FF
	STA	JMPPTR
	LDA	#OUTBK3&$FF00/256
	STA	JMPPTR+1
	JMP	EXIT2
;----------------------------
RSTOBE:
	LDA	SPSHOW
	BMI	OUTRSTOBE
	LDX	SCROLL
	CPX	#124
	BNE	OUTRSTOBE
;
	LDA	#0
	STA	JEWEL
	LDX	PLAYER
	LDA	SCREEN,X
	BEQ	NOS1
ANOTHJ:
	LDA	RAND
	AND	#$03
	TAY
	BEQ	NOS1
	CMP	#3
	BNE	NOGOOD3
	LDY	#2
NOGOOD3:
	LDX	BONUSR
	BNE	NOS1
	LDX	PLAYER
	LDA	NJEWL,Y
	AND	OFFSE3,X
	CMP	OFFSET4,X
	BNE	NOS1
	LDY	#0
NOS1:
	STY	NEWOBE
	LDA	#160
	STA	HPM1
	LDA	#$F0
	STA	OBMASK
OUTRSTOBE:
	RTS
;------------------------------
ALSHADC:
	LDX	ALIEN
	LDA	ANIMAT2,X
	BEQ	NOUWESHAD
	STA	ALSHAD
	RTS
NOUWESHAD:
	LDA	SHADHT
	STA	WORK
	LDX	PLAYER
	LDA	POWER,X
	CMP	#11
	BNE	NODECOY
	LDA	PWRTMR
	BEQ	NODECOY
	LDA	RAND
	STA	WORK
NODECOY:
	LDA	ALMASK
	BEQ	OUTDSH
;
	LDA	FRMCNT
	AND	#$01
	BNE	OUTDSH
	LDA	ALSHAD
	SEC
	SBC	#15
	TAX
	CPX	WORK
	BEQ	OUTDSH
	BCC	INCALSHAD
	DEX
	BNE	STOALSHAD
INCALSHAD:
	CPX	#MAXALN
	BCS	OUTDSH
	INX
STOALSHAD:
	TXA
	CLC
	ADC	#15
	STA	ALSHAD
OUTDSH:
	RTS
;--------------------------
SETSCR:
	LDA	SPSHOW
	BMI	OUTSCR2	
	LDA	ATT
	BEQ	NOSPD
	JSR	BUMPS2
NOSPD:
	LDA	BONTIM
	CMP	#1
	BEQ	STOPSP
	LDX	BUMP
	LDA	BUMPT,X
	TAX
	LDA	STARTG
	BNE	NOSPX
	LDA	SPSHOW
	BNE	NOSPX
	LDA	SPEED
	CMP	#HOPSPD
	BEQ	DO1F
STOPSP:	
	LDX	#$FF
	STX	SPEED
OUTSCR2:
	RTS
NOSPX:
	LDY	PLAYER
	LDA	POWER,Y
	CMP	#16		;HOVER
	BEQ	STOPSP
NOFSL:
	LDA	ATT
	BNE	NOCRUISE
	LDA	SHADHT
	CMP	#5
	BCS	NOCRUISE
	LDA	CRUISET,X
	TAX
NOCRUISE:
	TXA
	STX	SPEED
DO1F:
	AND	FRMCNT
	BNE	OUTSCR
	LDA	SPSHOW
	BNE	MOVSPH
	INC	SCROLL
	LDX	BUMP
	CPX	#3
	BNE	NOINCZ
	INC	SCROLL
	LDA	SCROLL
	AND	#$FE
	STA	SCROLL
NOINCZ:
	LDA	SCROLL
	CMP	#110
	BNE	NOWAVE
	LDA	#0
	STA	SPEED
	LDX	PLAYER
	INC	SCREEN,X
;
DOWAVE2:
	LDA	WAVE,X
	CMP	#8
	BCC	DOWORK77
	LDA	#7
DOWORK77:
	STA	WORK
	LDA	GAME
	AND	#$0F
	CLC
	ADC	WORK
	TAY
	LDA	WAVETT,Y
;
	LDY	PLAYER
	CMP	SCREEN,Y
	BNE	NOWAVE
	LDA	BONUSR
	BNE	NOWAVE
	STA	SCREEN,Y
	LDA	#1
	STA	SPSHOW
	LDA	#0
	STA	HSPH
	LDA	#4
	STA	SPANIM
NOWAVE:
	LDA	SCROLL
	CMP	#174
	BCC	OUTSCR
	LDA	#0
	STA	SCROLL
OUTSCR:
	RTS
MOVSPH:
	LDA	SPSHOW
	BMI	OUTSC1
	LDA	HP0
	CMP	#80
	BCS	NOBACKUP
	INC	HP0
	DEC	VPOSP0
NOBACKUP:
	INC	HSPH
	LDA	HSPH
	CMP	#50
	BNE	OUTSC1
	LDA	#9
	STA	ALIEN
	LDA	#$FF
	STA	SPSHOW
	LDA	#SFINKS		;SPHINX HOWLING TUNE
	STA	TUNT1
	LDA	#65
	STA	HP1
	LDA	#100
	STA	VPOSP1
OUTSC1:
	RTS
;----------------------------
COLLISIONS:
	JSR	COLL0	;PLAYER 1 AND BALL
;
	LDA	ATT
	BNE	NOCOLZ
	LDA	SHADHT
	BEQ	NOCOLZ
	LDA	PWRTMR
	BEQ	SKICHP
	LDA	PWRTMR	;WARP
	BMI	NOCOLZ
SKICHP:
	JSR	COLL1	;PLAYER 1 AND PLAYER 0
	JSR	COLL2	;MISSILE AND ALIEN
	JSR	COLL3	;MISSILE0 AND MISSILE 1 (OBELISK)
	JSR	COLL4	;COLLISION WITH PLAYER 0 AND MISSILE 1
	JSR	COLL5	;COLLISION WITH PLAYER 0 AND MISSILE 0
NOCOLZ:
	RTS
;-----------------
COLL1:
	LDA	SHADHT
	CMP	#1
	BNE	NOCHAX
	LDA	#3
	STA	SHADHT
NOCHAX:
	LDA	CXPPMM	;COLLISION WITH PLAYER 1 AND PLAYER 0
	BPL	NOCOLZ
;
	LDA	SPSHOW
	BNE	ITSALN
	LDA	SHADHT
	CLC
	ADC	SCROLL
	CLC
	ADC	VPOSP0
	TAX
	CMP	#186
	BCS	NOCOLZ
	CMP	#155
	BCS	ITSALN
	CMP	#146
	BCS	NOCOLZ
ITSLAK:
	LDA	SPSHOW
	BNE	OUTCL1
	LDA	NEWLAK
	BEQ	COLLAKE
;
	LDY	#6
CHKCOLTBL:
	LDA	COLLITBL,Y
	CMP	NEWLAK
	BEQ	COLPTS
	DEY
	BPL	CHKCOLTBL
;
	LDA	NEWLAK
	CMP	#5
	BCS	COLHIR
	RTS
ITSALN:
	SEC
	SBC	ALSHAD
	CLC
	ADC	#15
	CMP	#155
	BCC	NOCOLZ2
	LDA	ALIND
	CMP	#3
	BEQ	OUTCL1
	LDA	ALIEN
	CMP	#10
	BNE	NODARTAL
	LDA	MISHOT+SARAH
	AND	#$10
	BNE	OUTCL1	
NODARTAL:
	CMP	#1
	BNE	NOUWE
	LDA	STARTG
	BEQ	IMHIT
NOCOLZ2:
	RTS
NOUWE:
	LDA	ALSHAD
	SEC
	SBC	#15
	CMP	SHADHT
	BEQ	IMHIT
	CMP	#MAXALN
	BNE	OUTCL1
	LDA	SHADHT
	CMP	#17
	BNE	OUTCL1
IMHIT:
	JMP	ITSOBE
COLLAKE:
	LDA	STARTG
	BNE	OUTCL1
	LDA	#1
	STA	SHADHT
OUTCOLAK:
	RTS
COLMIN:
COLPYR:
COLPTS:
	LDA	SHADHT
	CMP	#12
	BCS	OUTCL1
	LDA	VPOSP0
	CLC
	ADC	SHADHT
	CMP	#$0F
	BCC	OUTCL1
	CMP	#$23
	BCS	OUTCL1
	LDA	SCROLL
	CMP	#$4D
	BCC	OUTCL1
	JMP	ITSOBE
OUTCL1:
	RTS
;---------------------
;	HIERO CALCS
;
COLHIR:
	LDA	NEWLAK	
	CMP	#6
	BNE	NOJEWLHIR
	LDA	SHADHT
	CMP	#5
	BCS	OUTCHI
	BCC	DOERASHIR
NOJEWLHIR:
	LDA	STARTG
	BNE	OUTCHI
DOERASHIR:
	LDA	#0
	JSR	ERASHIR
	LDX	#0
	CPY	#0
	BEQ	NOINH2
	INX
NOINH2:
	LDA	TYPHIR+SARAH,X
	CMP	#10
	BNE	NOJHIR
	LDX	#0
	JMP	NOINC4
NOJHIR:
	LDY	PLAYER
	LDA	HIEROC,Y
	TAY
	CPY	#3
	BNE	NOMAXS
;
	LDY	PLAYER
	LDA	#0
	STA	HIERO0,Y
	STA	HIERO1,Y
	STA	HIERO2,Y
	STA	HIEROC,Y
	LDY	#0
NOMAXS:
;	INC	POWER
	LDA	TYPHIR+SARAH,X
	CLC
	ADC	#1
	STA	WORK
	TYA
	ASL
	CLC
	ADC	PLAYER
	TAY
	LDA	WORK
	STA	HIERO0,Y
	LDX	PLAYER
	LDA	HIEROC,X	;HIERO TUNE
	INC	HIEROC,X
	TAX
	CLC
	ADC	#NHIERO1
	STA	TUNT0
	CPX	#2
	BNE	OUTCHI
	LDX	PLAYER
	LDA	#0
	STA	POWER,X
	STA	PWRTMR
	STA	FRMCNT
OUTCHI:
	RTS
;------------------
ERASHIR:
	LDY	#45
	CPX	#116
	BCS	NOYELW
	LDY	#14
NOYELW:
	LDX	#13
ERASHI:
	STA	PLAY1,Y
	DEY
	DEX
	BPL	ERASHI
	RTS
;----------------------------
COLL4:			;COLLISION WITH PLAYER 0 AND OBELISK/JEWELS
	LDA	CXM1P
	BPL	OUTDL
	LDA	SHADHT
	CLC
	ADC	SCROLL
	CLC
	ADC	VPOSP0
	CMP	#52
	BCC	ITSJEW
;
	LDA	VPOSP0
	CLC
	ADC	SCROLL
	CMP	#$36
	BCC	OUTDL
	CMP	#$3A
	BCS	OUTDL
ITSOBE:
	LDA	#NOHIT
	BNE	OUTDL
	LDA	PWRTMR
	BEQ	DOCUCK
	LDX	PLAYER
	LDA	POWER,X
	CMP	#22
	BEQ	OUTDL
DOCUCK:
	JSR	RSTM
	LDA	SPSHOW
	BMI	DECLIV
	LDX	#15
	LDA	HP0
	CMP	HP1
	BCC	CONITSOBE
	LDX	#$F3
DOINXMAX:
	INX
CONITSOBE:
	STX	WORK
	LDA	HP0
	SEC
	SBC	WORK
	CMP	#MAXHP0
	BCS	DOINXMAX
	CMP	#MINHP0
	BCC	DECLIV
	STA	HP0
	LDA	VPOSP0
	CLC
	ADC	WORK
	STA	VPOSP0
DECLIV:
	LDA	#0
	STA	FRMCNT
	STA	SHADHT
	STA	STARTG
OUTDL:
	RTS
ITSJEW:
	LDA	JEWEL
	BNE	OUTDL
	LDA	NEWOBE
	BEQ	OUTDL
	LDA	HP0
	CMP	HPM1
	BCS	OUTDL
;
	LDA	SHADHT
	CMP	#5
	BCS	OUTDL
	LDA	#0
	STA	WORK1
	LDA	#3
	STA	JEWEL
	LDX	NEWOBE
NOINC4:
	LDA	#JWTUNE		;JEWEL TUNE
	STA	TUNT0
	LDY	PLAYER
	LDA	BONUSR
	BNE	NOINC5
	LDA	NJEWL,X
	CLC
	ADC	OFFSE2,Y
	STA	NJEWL,X
;
	LDA	PLAYER
	BNE	WORKP11
	LDA	VV0+SARAH
	AND	#$0F
	STA	S0
	LDA	VV1+SARAH
	AND	#$0F
	STA	S1
	LDA	VV2+SARAH
	AND	#$0F
	STA	S2
	RTS
WORKP11:
	LDA	VV0+SARAH
	LSR
	LSR
	LSR
	LSR
	STA	S0
	LDA	VV1+SARAH
	LSR
	LSR
	LSR
	LSR
	STA	S1
	LDA	VV2+SARAH
	LSR
	LSR
	LSR
	LSR
	STA	S2
	RTS
NOINC5:
	LDA	NJEWL,X
	AND	OFFSE3,Y
	STA	WORK1
	LDA	PLAYER
	BEQ	BNLOOP
	LDA	WORK1
	LSR
	LSR
	LSR
	LSR
	STA	WORK1	
BNLOOP:
	LDA	VV0+SARAH
	LDX	PLAYER
	AND	OFFSE3,X
	BNE	BNLOOP2
	LDA	VV1+SARAH
	AND	OFFSE3,X
	BNE	BNLOOP2
	LDA	VV2+SARAH
	AND	OFFSE3,X
	BNE	BNLOOP2
	LDA	S3
	CLC
	ADC	WORK1
	ADC	#1
	STA	S3
	RTS
BNLOOP2:
	LDY	#0
	STY	WORK
NEXY:
	LDA	VV0+SARAH,Y
	LDX	PLAYER
	BNE	BNLOOPP1
;
	AND	OFFSE3
	BPL	NEXYYY
;
BNLOOPP1:
	LSR
	LSR
	LSR
	LSR
NEXYYY:
	CLC
	ADC	S0,Y
	ADC	WORK
	LDX	#0
	CMP	#$0A
	BCC	NEXTY7
	CPY	#3
	BEQ	NOSUBS3
	INX
	SEC
	SBC	#$0A
NEXTY7:
	STX	WORK
NOSUBS3:
	STA	S0,Y
	INY
	CPY	#3
	BCC	NEXY
	LDA	#0
	CPY	#4
	BNE	NEXYYY
	DEC	WORK1
	BPL	BNLOOP2
;
OUTCL4:
	RTS
;-----------------------
COLL0:
	LDA	SPSHOW
	BNE	OUTCL0
	LDA	CXP1FB	;COLLISION PLAYER 1 AND BALL
	AND	#$40
	BEQ	OUTCL0
	LDA	#0
	STA	ALMASK
OUTCL0:
	RTS
;--------------------
COLL5:
	LDA	SPSHOW
	BNE	OUTCL5
;	LDX	PLAYER
;	LDA	POWER,X
;	CMP	#11
;	BEQ	OUTCL5
	LDA	CXM0P
	AND	#$40
	BEQ	OUTCL5
	LDA	VPOSP0
	CLC
	ADC	SHADHT
	CMP	VENM
	BCS	OUTCL5
	LDA	MISHOT+SARAH	;PLAYER 0 AND MISSILE 0
	BPL	OUTCL5
	AND	#$F0
	CMP	#$90
	BEQ	OUTCL5
	LDA	FRMCNT
	AND	#$01
	BNE	OUTCL5
NOFRMCHK9:
	LDA	HPENM
	CMP	#90
	BCS	OUTCL5
	JMP	ITSOBE
OUTCL5:
	RTS
;----------------------
COLL2:				;MISSILE AND ALIEN
	LDA	MISHOT+SARAH
	AND	#$0F
	BEQ	OUTCL2
	LDA	FRMCNT
	AND	#$01
	BEQ	OUTCL2
	LDA	CXM0P
	BPL	OUTCL2
;
	LDA	SPSHOW
	BNE	ITSAL2
	LDA	SCROLL
	CLC
	ADC	VM0
	ADC	SHADHT
	CMP	#160
	BCC	ITSSPH
	CMP	#175
	BCC	OUTCL2
ITSAL2:
	LDX	ALIND
	CPX	#3
	BEQ	OUTCL2
	LDX	ALIEN
	CPX	#1
	BNE	NOUWE2
	LDA	SHADHT
	CMP	#6
	BCS	OUTCL2
NOUWE2:	
;	LDA	SHADHT
;	CMP	#14
;	BCS	OUTCL2
AFTUWE:
	LDA	#0
	STA	MISHOT
	JMP	KILLAL
OUTCL2:
	RTS
ITSSPH:			;MISSILE AND LAKE,PYRMID,MINI
	TAY
	LDA	SHADHT
	CMP	#12
	BCS	OUTCL2
	LDA	MISHOT+SARAH
	BPL	NOFRMCHK
	LDA	FRMCNT
	AND	#$01
	BEQ	OUTCL2
NOFRMCHK:
	LDX	NEWLAK
	CPX	#4	;MINI
	BEQ	PICKONE
	CPX	#9
	BEQ	ITS9
	CPX	#8
	BNE	ITSPYR
ITS8:
	CPY	#125
	BCS	STOPMS
	BCC	OTHDED
ITS9:
	CPY	#125
	BCC	STOPMS
OTHDED:
	LDA	#0
	STA	MISHOT
	LDY	#1
	JSR	KILLMI2
	LDX	#8
	JSR	CLRMINI
	LDX	#9
	JMP	CLRMINI
PICKONE:
	LDX	#8
	CPY	#125
	BCS	DOKILLMI
	INX
DOKILLMI:
	LDA	#0
	STA	MISHOT
	LDY	#1
	JSR	KILLMI1
CLRMINI:
	TXA
	LDX	#24
	LDY	#24
	CMP	#9
	BEQ	KILLMINI
	LDX	#45
	CMP	#8
	BNE	NOKMIN
KILLMINI:
	LDA	PLAY1+SARAH,X
	AND	#$0F
	STA	PLAY1,X
	DEX
	DEY
	BPL	KILLMINI
NOKMIN:
	RTS
;
ITSPYR:
;	CPX	#6	;JEWELS
;	BNE	NOJEWELS
;	LDA	VM0
;	CLC
;	ADC	SCROLL
;	TAX
;	LDA	#0
;	CMP	STARTG
;	BNE	OUTCL3
;	JSR	ERASHIR
;	LDX	PLAYER
;	LDA	NJEWL
;	AND	OFFSE6,X
;	STA	NJEWL
;	BPL	STOPMS
NOJEWELS:
	LDA	DISPAR,X
	BEQ	OUTSTPMS
STOPMS:
	LDA	MISHOT+SARAH
	AND	#$F0
	STA	MISHOT
	LDA	#RICCHET	;RICOCHET
	STA	TUNT1
OUTSTPMS:
	RTS
COLL3:			;COLLISION OF MISSILE AND OBELISK
	LDA	MISHOT+SARAH
	AND	#$0F
	BEQ	OUTCL3
	LDA	SHADHT
	CMP	#12
	BCS	OUTCL3
	LDA	FRMCNT
	AND	#$01
	BEQ	OUTCL3
	LDA	CXPPMM
	AND	#$40
	BEQ	OUTCL3
	LDA	HPM0
	CLC
	ADC	#10
	CMP	HPM1
	BCC	OUTCL3
;
;	LDX	STARTG
;	BNE	OUTCL3
;	STA	JEWEL
;	LDX	NEWOBE
;	LDY	PLAYER
;	LDA	NJEWL,X
;	AND	OFFSE6,Y
;	STA	NJEWL,X
;	BPL	STOPMS
HITOBE:
	LDX	VPOSP0
	LDA	HPM1
	CLC
	ADC	#10
	CMP	HPM0
	BCS	NOTFARPYR
	TXA
	CLC
	ADC	#$10
	TAX
NOTFARPYR:
	CPX	#$19
	BCS	OUTCL3
	CPX	#$10
	BCS	STOPMS
OUTCL3:
	RTS
;------------------------
JOYSTK:
	LDA	BONTIM
	CMP	#1
	BEQ	NOMOVE
	LDA	#ENDGAM0
	CMP	TUNNUM
	BEQ	NOMOVE
	CMP	TUNNUM+1
	BEQ	NOMOVE
	LDA	SHADHT
	BEQ	NOMOVE
	LDA	SPSHOW
	BEQ	DOMOVE
	BPL	NOMOVE
DOMOVE:
	LDA	PWRTMR
	BMI	NOMOVE
NOWARP:
	LDA	ATT
	BNE	JOYPL0
	LDA	PLAYER
	BEQ	JOYPL0
	LDA	SWCHA
	ASL
	ASL
	ASL
	ASL
	JMP	JOYPL1
JOYPL0:	
	LDA	SWCHA
	AND	#$F0
JOYPL1:
	LDX	#8
JOYLP:
	CMP	RUNTBL,X
	BEQ	FOUNDJ
	DEX
	BPL	JOYLP
	RTS
NOMOVE:
	LDX	#0
	STX	HORDIR
	STX	VERDIR
	DEX
	STX	SPEED
	RTS
FOUNDJ:
	LDA	ATT
	BNE	VERTJY
	LDA	HORTAB,X
	STA	HORDIR
VERTJY:
	LDA	VERTAB,X
	STA	VERDIR
	BEQ	NOTINGAME
;
	TAX
	LDA	ATT
	BEQ	NOTINGAME
	LDA	#0
	STA	WORK1
;
	LDA	TTIMER
	BMI	OUTSAVEG
	CMP	#95
	BCC	XGAME
	CMP	#101
	BCC	OUTGAME
;
XGAME:
	CPX	#1
	BEQ	INCGAME
DECGAME:
	DEC	GAME
	LDA	GAME
	AND	#$0F
	CMP	#$0F
	BNE	OUTSAVEG
	LDA	GAME
	AND	#$80
	ORA	#$03
	STA	GAME
	BNE	OUTSAVEG
INCGAME:
	INC	GAME
	LDA	GAME
	AND	#$0F
	TAX
	CPX	#4
	BNE	OUTSAVEG
SAVEGAME:
	LDA	GAME
	AND	#$F0
	EOR	#$80
	STA	GAME
OUTSAVEG:
	LDA	#100
	STA	TTIMER
OUTGAME:
	RTS
;
NOTINGAME:
	LDA	VERDIR
	BEQ	OUTV1
	BPL	MOVED
	LDA	STARTG
	BEQ	OUTJOY
MOVED:
	LDA	VPOSP0
	CLC
	ADC	SHADHT
	CMP	#$48
	BEQ	NOORMIS
	LDA	MISHOT+SARAH
	ORA	#$10
	STA	MISHOT
NOORMIS:
	LDA	VERDIR
	BMI	OUTJOY
OUTV1:
	LDX	SPEED
	CPX	#HOPSPD
	BNE	DOV1
	DEC	SPEED
DOV1:
	LDA	VERDIR
	CMP	#1
	BCC	OUTJOY
;
	LDA	STARTG
	BNE	OUTJOY
	LDA	FRMCNT
	CMP	#129
	BCC	OKSTARTG
	CMP	#132
	BCC	OUTJOY
OKSTARTG:
	INC	STARTG
	LDA	#3
	STA	SHADHT
OUTJOY:
	RTS
;-----------------------
SCOREC:
	LDA	BONUSR
	BNE	OUTJOY
;
	LDY	#0
	STY	WORK3
	LDA	S3
	CMP	#10
	BCC	NOSAVE
	SEC
	SBC	#9
	STA	WORK3
	LDA	#9
	STA	S3
NOSAVE:
	LDA	PLAYER
	BEQ	DOPL0
	JMP	DOPL1
DOPL0:
	LDA	S3
	BEQ	NOS3V
	LDA	D02+SARAH
	AND	#$0F
	CMP	#$0A
	BNE	NOS3V
	LDA	D02+SARAH
	AND	#$F0
	STA	D02
	JMP	TRYD01
NOS3V:
	LDA	S2
	BEQ	NEXY1
TRYD01:
	LDA	D01+SARAH
	AND	#$0F
	CMP	#$0A
	BNE	NEXY1
	LDA	D01+SARAH
	AND	#$F0
	STA	D01
NEXY1:
	STY	WORK
NEXY2:
	LDA	S0,Y
NEXY3:
	CLC
	ADC	WORK
	BEQ	NEXTY9
	STA	WORK1
	LDA	D00+SARAH,Y
	AND	#$0F
	CMP	#$0A
	BNE	NOFIXD
	LDA	D00+SARAH,Y
	AND	#$F0
	STA	D00,Y
NOFIXD:
	SED
	CLC
	ADC	WORK1
	CLD
	LDX	#0
	CMP	#$0A
	BCC	NEXTY8
	CPY	#3	
	BNE	NOEXLV
	JSR	EXTRALIFE
NOEXLV:
	INX
NEXTY8:
	STX	WORK
	AND	#$0F
	STA	WORK4
	LDA	D00+SARAH,Y
	AND	#$F0
	EOR	WORK4	
	STA	D00,Y
NEXTY9:
	LDA	#0
	INY
	CPY	#6
	BEQ	OUTXY1
	CPY	#4
	BCS	NEXY3
	BCC	NEXY2
OUTXY1:
	STA	S0
	STA	S1
	STA	S2
	LDA	WORK3
	STA	S3
OUTSCL:
	RTS
DOPL1:
	LDA	S3
	BEQ	NOS13V
;
	LDA	D02+SARAH
	AND	#$F0
	CMP	#$A0
	BNE	NOS13V
	LDA	D02+SARAH
	AND	#$0F
	STA	D02
	BPL	TRYD11
NOS13V:
	LDA	S2
	BEQ	NEXY11
TRYD11:
	LDA	D01+SARAH
	AND	#$F0
	CMP	#$A0
	BNE	NEXY11
	LDA	D01+SARAH
	AND	#$0F
	STA	D01
NEXY11:
	STY	WORK
NEXY12:
	LDA	S0,Y
NEXY13:
	CLC
	ADC	WORK
	BEQ	NEXT19
	ASL
	ASL
	ASL
	ASL
	STA	WORK1
	LDA	D00+SARAH,Y
	AND	#$F0
	CMP	#$A0
	BNE	NOFIX1
	LDA	D00+SARAH,Y
	AND	#$0F
	STA	D00,Y
NOFIX1:
	SED
	CLC
	ADC	WORK1
	CLD
	LDX	#0
	BCC	NEXT18
	CPY	#3	
	BNE	NOEXL1
	JSR	EXTRALIFE
NOEXL1:
	INX
NEXT18:
	STX	WORK
	AND	#$F0
	STA	WORK4
	LDA	D00+SARAH,Y
	AND	#$0F
	EOR	WORK4
	STA	D00,Y
NEXT19:
	LDA	#0
	INY
	CPY	#6
	BEQ	OUTXYZ
	CPY	#4
	BCS	NEXY13
	BCC	NEXY12
OUTXYZ:
	STA	S0
	STA	S1
	STA	S2
	LDA	WORK3
	STA	S3
OUTSCORC:
	RTS
;----------------------
DETPOW:
	LDA	#ENDGAM0
	CMP	TUNNUM
	BEQ	TURNTAKE
	CMP	TUNNUM+1
	BNE	NOTURN
TURNTAKE:
	LDA	#0
	STA	WORK4
	LDA	FRMCNT
	BMI	NO2PL
	CMP	#25
	BCC	NO2PL
	LDA	GAME
	BPL	NO2PL
	LDA	PLAYER
	LDX	ATT
	BNE	NOEORPL
NOEORPL:
	STA	WORK4
	INC	WORK4
NO2PL:
	LDX	PLAYER
	LDA	WORK4
	CLC
	ADC	#25
	STA	POWER,X
	RTS
;
NOTURN:
	LDX	PLAYER
	LDA	POWER,X
	BEQ	DOPOW
	CMP	#25
	BCC	CHKFF
	LDA	GAME
	BPL	CHKFF
	LDA	#$FF
	STA	POWER,X
CHKFF:
	LDA	POWER,X
	BPL	OUTPOW
DOPOW:
	LDA	POWER,X
	BMI	DOPOW3
	BNE	OUTPOW
DOPOW3:
	LDY	PLAYER
	LDA	HIEROC,Y
	CMP	#3
	BNE	OUTPOW
;			;SUPER POWERS
	LDX	HIERO0+SARAH,Y
	LDA	PGHASH1,X
	LDX	HIERO1+SARAH,Y
	EOR	PGHASH2,X
	LDX	HIERO2+SARAH,Y
	EOR	PGHASH3,X
;
	LDX	#0
COMPLP:
	CMP	PGDIV,X
	BCS	GOTOIT
	INX
	CPX	#31
	BNE	COMPLP
GOTOIT:
	LDA	PGID,X
;
;	LDA	#14
;
	LDY	PLAYER
;
	STA	POWER,Y
	TAX
	LDA	WAVE,Y
	AND	#$07
	STA	WORK
;
	CPX	#5
	BNE	NOPOTWAVE
	TAY
	LDA	NOPOTT,Y
	BEQ	DOPOINTS
NOPOTWAVE:
	CPX	#4
	BNE	NOMINIWAVE
	TAY
	LDA	NOMINIT,Y
	BEQ	DOPOINTS
NOMINIWAVE:
	LDA	GAME
	AND	#$0F
	TAY
	CPX	#10
	BEQ	AF
	CPX	#12
	BEQ	AF
	CPX	#13
	BEQ	AF
	CPX	#14
	BEQ	AF
	CPX	#16
	BNE	NOAF
AF:
	LDA	WORK
	CMP	AFMAX,Y
	BCC	DOPOINTS
	RTS
DOPOINTS:
	LDY	PLAYER
	LDA	#2
	STA	POWER,Y
OUTPOW:
	RTS
NOAF:
	CPX	#11
	BEQ	DC
	CPX	#18
	BEQ	DC
	CPX	#7
	BNE	NODC
DC:	
	LDA	WORK
	CMP	AFDC,Y
	BCC	DOPOINTS
	RTS
NODC:
	CPX	#3
	BEQ	AB
	CPX	#9
	BEQ	AB
	CPX	#17
	BNE	OUTPOW
AB:
	LDA	WORK
	CMP	AFAB,Y
	BCC	DOPOINTS
OUTSPA:
	RTS
;
AFMAX:
	DC.B	4,3,2,0
AFDC:
	DC.B	3,2,0,0
AFAB:
	DC.B	1,1,0,0
;-----------------------------------
SETMIS:
	LDA	BONUSR
	BNE	RSTM
	LDA	MISHOT+SARAH
	AND	#$0F
	BNE	MOVMIS
	JSR	RSTM
NORESTMIS:
	LDX	PLAYER
	LDA	TRIG
	AND	OFFSE3,X
	BEQ	OUTSPA
FALSHOOT:
	JSR	RSTM
	LDA	TUNT0+SARAH
	CMP	#POWRUSE
	BEQ	OUTSPA	
	LDA	MISHOT+SARAH
	ORA	#1
	STA	MISHOT
	LDA	#BRDSHOT
	STA	TUNT0
MOVMIS:
	INC	VM0
	INC	VM0
	LDX	PLAYER
	LDA	POWER,X
	CMP	#21
	BNE	NOQUIK
	INC	HPM0
	INC	VM0
NOQUIK:
	LDA	FRMCNT
	AND	#$03
	BNE	NOSAVEMIS
	LDA	MISHOT+SARAH
	CLC
	ADC	#1
	STA	MISHOT
NOSAVEMIS:
	LDA	FRMCNT
	AND	#$02
	BNE	DOHPM
	INC	VM0
	INC	HPM0
DOHPM:
	LDA	SPSHOW
	BNE	NOINCV
	INC	HPM0
	INC	HPM0
NOINCV:
	LDA	HPM0
	CMP	#152
	BCS	RSTM
	LDA	#154
	LDX	SPSHOW
	CPX	#$FF
	BNE	NOSPS5
	LDA	#130
NOSPS5:
	CMP	VM0
	BCS	OUTMS
RSTM:
	LDA	MISHOT+SARAH
	AND	#$F0
	STA	MISHOT
RSTMS:
	LDA	HP0
	CLC
	ADC	#6
	STA	HPM0
	LDA	VPOSP0
	CLC
	ADC	#8
	ADC	SHADHT
	STA	VM0
OUTMS:
	RTS
;----------------------------------
ENEMYMIS:
	LDA	SPSHOW
	BNE	OUTMS
	LDA	SHADHT
	BEQ	RSTENMIS
	LDA	#ENDGAM0
	CMP	TUNNUM
	BEQ	NOSHOTS
	CMP	TUNNUM+1
	BNE	NOP25
NOSHOTS:
	LDA	MISHOT+SARAH
	AND	#$0F
	STA	MISHOT
	RTS
NOP25:
	LDA	MISHOT+SARAH
	BMI	MOVENEMYMIS
	JMP	NEWENEMY
MOVENEMYMIS:
	LDA	GAME
	AND	#$0F
	TAX
	LDA	GAMESPD,X
	AND	FRMCNT
	BNE	SLOWMIS
	DEC	VENM
	DEC	HPENM
	LDA	NEWLAK
	CMP	#4
	BNE	FASTMS
	LDA	HP1
	CMP	#80
	BCC	FASTMS
	CMP	#140
	BCC	SLOWMIS
FASTMS:
	DEC	HPENM
SLOWMIS:
;	LDA	FRMCNT
;	AND	#$02
;	BNE	NOFRM2
	DEC	VENM
	DEC	HPENM
	LDA	NEWLAK
	CMP	#4
	BNE	FASTMS2
	LDA	HP1
	CMP	#80
	BCC	FASTMS2
	CMP	#140
	BCC	NOFRM2
FASTMS2:
	DEC	HPENM
NOFRM2:
	LDA	HPENM
	CMP	#4
	BCC	RSTENMIS
	LDA	VENM
	CMP	#2
	BCS	OUTMM
RSTENMIS:
	LDA	HP0
	LSR
	STA	WORK
	LDA	VPOSP0
	CLC
	ADC	#86
	ADC	SHADHT
	SEC
	SBC	WORK
	AND	#$FE
	STA	VENM
	LDX	PLAYER
	LDA	POWER,X
	CMP	#11
	BNE	RSTMZ
	LDA	PWRTMR
	BEQ	RSTMZ
	LDA	RAND
	AND	#$03
	TAX
	LDA	VENM
	CLC
	ADC	OFFSED,X
	STA	VENM
RSTMZ:	LDA	#160
	STA	HPENM
	LDA	MISHOT+SARAH
	AND	#$0F
	STA	MISHOT
OUTMM:
	RTS
NEWENEMY:
	LDA	SHADHT
	BEQ	OUTMINSH
	LDA	NEWLAK
	CMP	#4
	BNE	NOMINISHOOT
	LDA	HP1
	CMP	#80
	BCC	NOMINISHOOT
	CMP	#140
	BCS	NOMINISHOOT
	SEC
	SBC	#4
	STA	HPENM
	LDA	#172
	SEC
	SBC	SCROLL
	SBC	#37
	STA	VENM
	LDA	RAND
	BPL	DECMISH
	LDA	HPENM
	SEC
	SBC	#6
	STA	HPENM
	LDA	VENM
	SEC
	SBC	#22
	STA	VENM
	BNE	DECMISH
NOMINISHOOT:
	LDA	GAME
	AND	#$0F
	TAX
	LDA	STARTG
	BNE	DORANDSH
	LDA	FRMCNT
	BMI	OUTMINSH
	CMP	#20
	BCC	OUTMINSH
DORANDSH:
	LDY	PLAYER
	LDA	WAVE,Y
	CMP	#7
	BCS	NORANDSH
	LDA	RAND
	AND	GAMESPD2,X
	BNE	OUTMINSH
NORANDSH:
	LDA	BONUSR
	BNE	OUTMINSH
	LDA	TUNNUM		;SONG STILL PLAYING
	CMP	#4
	BEQ	OUTMINSH
	CMP	#$0A
	BEQ	OUTMINSH
	LDA	SHADHT
	BEQ	OUTMINSH
	LDA	PWRTMR
	BMI	OUTMINSH
	LDA	ATT
	BNE	OUTMINSH
	JSR	RSTENMIS
DECMISH:
	LDA	#MINISHOT
	STA	TUNT1
	LDA	MISHOT+SARAH
	ORA	#$80
	STA	MISHOT
OUTMINSH:
	RTS
;---------------
CHK3H:
	LDX	PLAYER
	LDA	POWER,X
	CMP	#25
	BEQ	OUTCHK3H
	LDA	WAVE	
	BNE	FFPOWER
	LDA	GAME
	AND	#$0F
	CMP	#2
	BCC	OUTCHK3H
FFPOWER:
	DEC	POWER,X
OUTCHK3H:
	RTS
;--------------------------
POWERC:
	LDX	PLAYER
	LDA	POWER,X
	CMP	#25
	BCS	OUTCHK3H
	LDA	PWRTMR
	BNE	AFTTRG
NEWPOW:
	LDA	POWER,X
	BMI	OUTCHK3H
	BEQ	CHK3H
	CMP	#23
	BEQ	OUTCHK3H
	TAY
	LDA	POWERT,Y
	BNE	NOPTRG
	JMP	DOPTRG	
DOPOWR:
	JSR	DOPOW12
SPHNX:
	LDA	SPSHOW
	BPL	NOEND
	LDA	ENDSP,Y
	BEQ	ENDPOW
NOEND:
	RTS
NOPTRG:
	TAX
	STX	PWRTMR
	LDA	#POWRUSE	;POWER TUNE
	STA	TUNT0
	CPY	#18	;SLOW DOWN
	BNE	AFTTRG
	JSR	BUMPS2
	TAX
	LDA	SLOWDWNT,X
	STA	BUMP
AFTTRG:
	LDA	SHADHT	;DEATH
	BEQ	ENDPOW
	LDX	PLAYER
	LDA	POWER,X
	TAY
	CPY	#9
	BEQ	SPHNX
	CPY	#2	;POINTS
	BNE	NOPW2
	LDA	FRMCNT
	AND	#$01
	BNE	DOPOWR
	LDA	#1
	STA	S1
	BNE	NOFRMCHK4
NOPW2:
	LDA	FRMCNT
	AND	#$07
	BNE	DOPOWR
NOFRMCHK4:
	DEC	PWRTMR
	BNE	DOPOWR
	CPY	#1	;EXTRA LIFE
	BNE	NOPW1
	JSR	EXTRALIFE
NOPW1:
	CPY	#7	;EXTRA BONUS
	BNE	NOPW7
	LDA	BONTIM
	CLC
	ADC	#$10
	STA	BONTIM
NOPW7:
	CPY	#19	;ROULETTE
	BNE	NOPW19
	LDY	PLAYER
	LDA	RAND
	AND	#$07
	CLC
	ADC	#1
	STA	HIERO0,Y
TRYHRA:
	LDA	FRMCNT
	AND	#$07
	CLC
	ADC	#1
	STA	HIERO1,Y
	LDA	#2
	STA	HIEROC,Y
	LDA	#0
	STA	HIERO2,Y
	BEQ	AFT19
NOPW19:
;
EP:
ENDPOW:
	JSR	CLRHIR2
AFT19:
	LDX	PLAYER
	LDA	#0
	STA	POWER,X
	STA	PWRTMR
	JMP	BUMPS2
;
DOPOW12:
	CPY	#12	;SHACKLES
	BNE	NOPW12
	LDA	SHADHT
	CMP	#1
	BEQ	NOPW12
	BNE	NOHOP
NOPW12:
DOPOW13:
	CPY	#13	;POLYWATER
	BNE	NOPW13
	LDA	SHADHT
	CMP	#1
	BNE	NOPW13
NOHOP:
	LDX	#0
	CPX	STARTG
	BNE	NOPW13
	STX	HORDIR
	STX	VERDIR
	STX	SPEED
	RTS
NOPW13:
DOPOW14:
	CPY	#14	;BROKEN WING
	BNE	NOPW14
	LDA	STARTG
	BEQ	NOPW14
	LDA	#0
	STA	STARTG
	RTS
NOPW14:
DOPOW15:
	CPY	#15	;HOLD SPHINX
	BNE	NOPW15
	LDA	SPSHOW
	BPL	NOPW15
	LDA	#0
	STA	ALMASK
	RTS
NOPW15:
DOPOW16:
;	CPY	#16	;HOVER
;	BNE	NOPW16
;	LDA	STARTG
;	BEQ	NOPW16
;	LDA	#0
;	STA	HORDIR
NOPW16:
	RTS
DOPTRG:
	LDX	PLAYER
	LDA	TRIG
	AND	OFFSE3,X
	CMP	OFFSE2,X
	BNE	NOPW16
	LDA	MISHOT+SARAH
	AND	#$0F
	CMP	#3
	BCC	NOPW16
	CMP	#7
	BCS	NOPW16
	LDA	MISHOT+SARAH
	ORA	#1
	STA	MISHOT
	LDA	#POWRUSE	;POWER USE TUNE
	STA	TUNT0
;
	LDA	DOPTRGL,Y
	STA	WORK
	LDA	DOPTRGH,Y
	STA	WORK+1
	JMP	(WORK)
;
DOPTRGL:
	DC.B	EP&$FF,EP&$FF,EP&$FF,DP3&$FF,DP4&$FF,DP5&$FF,DP6&$FF
	DC.B	EP&$FF,DP8&$FF,DP9&$FF,DP10&$FF,DP11&$FF,EP&$FF,EP&$FF
	DC.B	EP&$FF,DP15&$FF,EP&$FF,DP17&$FF,EP&$FF,EP&$FF
	DC.B	DP20&$FF,DP21&$FF,DP22&$FF,EP&$FF,EP&$FF
DOPTRGH:
	DC.B	EP&$FF00/256,EP&$FF00/256,EP&$FF00/256,DP3&$FF00/256
	DC.B	DP4&$FF00/256,DP5&$FF00/256,DP6&$FF00/256
	DC.B	EP&$FF00/256,DP8&$FF00/256,DP9&$FF00/256,DP10&$FF00/256
	DC.B	DP11&$FF00/256,EP&$FF00/256,EP&$FF00/256
	DC.B	EP&$FF00/256,DP15&$FF00/256,EP&$FF00/256
	DC.B	DP17&$FF00/256,EP&$FF00/256,EP&$FF00/256
	DC.B	DP20&$FF00/256,DP21&$FF00/256,DP22&$FF00/256
	DC.B	EP&$FF00/256,EP&$FF00/256
;
DP3:			;AIR BOMB
	LDA	MISHOT+SARAH
	AND	#$0F
	STA	MISHOT
	LDA	ALIEN
	CMP	#1
	BEQ	NOPW3
	JSR	KILLAL
NOPW3:
	JMP	EP
DP4:			;KILL MINIS
	JSR	TRYKMINI
	JMP	EP
DP5:			;KILL POTS
	LDA	NEWLAK
	CMP	#2
	BNE	NOPW5
	LDA	#7
	STA	NEWLAK
NOPW5:
	JMP	EP
DP6:			;KILL SPHINX
	LDA	SPSHOW
	BEQ	NOPW6
	JSR	KILLSP
NOPW6:
	JMP	EP
DP8:			;KILL UWES
	LDA	ALIEN
	CMP	#1
	BNE	NOPW8
	JSR	KILLAL
NOPW8:
	JMP	EP
DP9:			;WARP
	LDA	#3
	STA	BUMP
	STA	STARTG
	LDA	#15
	STA	SHADHT
	LDA	#$FF
	BNE	SETPT
DP17:			;SPEED UP
	JSR	BUMPS2
	TAX
	LDA	SPEEDUPT,X
	STA	BUMP
DP10:			;AIR FREEZE
DP11:			;DECOY
DP15:			;HOLD SPHINX
DP21:			;QUICK SHOTS
DP22:			;INVINCIBLE
;
POWT:
	LDA	#PTM
SETPT:
	STA	PWRTMR
CLRHIR2:
	LDY	PLAYER
	LDA	HIEROC,Y
	CMP	#3
	BNE	OUTPWC
	LDA	#0
	STA	HIERO0,Y
	STA	HIERO1,Y
	STA	HIERO2,Y
	STA	HIEROC,Y
OUTPWC:
	RTS
;
DP20:			;OMNICIDE
	LDA	MISHOT+SARAH
	AND	#$0F
	STA	MISHOT
	JSR	KILLAL
	JSR	TRYKMINI
	LDA	NEWLAK
	CMP	#2
	BNE	NOPOTO
	LDX	#7
	STX	NEWLAK
NOPOTO:
	LDA	SPSHOW
	BPL	NOPW20
	JSR	KILLSP
NOPW20:
	JMP	EP
;
EXTRALIFE:
	STA	WORK
	STX	WORK2
	LDX	PLAYER
	LDA	LIVES0,X
	CMP	#MAXLIF
	BEQ	OUTEXTRA
	INC	LIVES0,X
EXTRALTUNE:
	LDX	#XLPOWER	;EXTRA LIFE TUNE
	STX	TUNT0
OUTEXTRA:
	LDX	WORK2
	LDA	WORK
	RTS
;---------------------------
TRYKMINI:
	LDA	NEWLAK
	CMP	#4
	BEQ	KILLMI
	CMP	#8
	BEQ	KILLMI
	CMP	#9
	BNE	OUTMINO
KILLMI:
	LDY	#2
	CMP	#4
	BEQ	NOTONE
	CMP	#10
	BEQ	OUTMINO
	DEY
NOTONE:
	JSR	KILLMI2
	LDA	#$0F
	STA	LKMASK
OUTMINO:
	RTS
;-----------------------
BUMPS2:
	LDA	BONUSR
	BNE	BONBUMP2
	LDX	PLAYER
	LDA	WAVE,X
	CMP	#2
	BCC	NOFASTBUMP2
BONBUMP2:
	LDA	#0
	STA	BUMP
	RTS
NOFASTBUMP2:
	LDA	GAME
	AND	#$0F
	LSR	
	EOR	#1
	STA	BUMP
	RTS
;-----------
KILLAL:
	STY	WORK
	LDA	#0
	STA	ALMASK
;
	LDX	ALIEN
	CPX	#10
	BEQ	OUTKA
	LDA	SHADHT
	BEQ	OUTKX
	LDA	#BRDSHOT
	CMP	TUNNUM
	BEQ	OUTKX
	CMP	TUNNUM+1
	BEQ	OUTKX
;	
	LDA	ALIENDS,X	;ALIEN DEATH SOUNDS
	STA	TUNT1
OUTKX:
	LDA	VALJEW,X
	TAY
AGNJEW:
	DEY
	BMI	OUTAGNJEW
	LDX	#0
;
	LDA	PLAYER
	BNE	DOPLV1
;
	LDA	VV0+SARAH
	AND	#$0F
	BNE	DOJEWL1
	LDA	VV1+SARAH
	AND	#$0F
	BNE	DOJEWL1
	LDA	VV2+SARAH
	AND	#$0F
	BEQ	OUTAGNJEW
DOJEWL1:
	LDA	VV0+SARAH,X
	CLC
	ADC	#1
	STA	VV0,X
	AND	#$0F
	CMP	#$0A
	BNE	AGNJEW
	LDA	VV0+SARAH,X
	AND	#$F0
	STA	VV0,X
	INX
	CPX	#3
	BNE	DOJEWL1
OUTKA:
	RTS
OUTAGNJEW:
	LDX	ALIEN
	LDA	VALS2,X
	STA	S2
	LDA	VALS1,X
	STA	S1
OUTSJ:
	LDY	WORK
	RTS
DOPLV1:
	LDA	VV0+SARAH
	AND	#$F0
	BNE	OK999
	LDA	VV1+SARAH
	AND	#$F0
	BNE	OK999
	LDA	VV2+SARAH
	AND	#$F0
	BEQ	OUTAGNJEW
OK999:
	LDA	VV0+SARAH,X
	CLC
	ADC	#$10
	STA	VV0,X
	AND	#$F0
	CMP	#$A0
	BNE	AGNJEW
	LDA	VV0+SARAH,X
	AND	#$0F
	STA	VV0,X
	INX
	CPX	#3
	BNE	OK999
	RTS
;
;----------------------------------
KILLC:
	LDA	ATT
	BNE	OUTKP
	LDA	SPANIM
	CMP	#9
	BNE	NOINC2
	LDA	TTIMER
	CMP	#200
	BCS	NOINC2
	INC	TTIMER
NOINC2:
	LDA	NEWLAK	;KILL POTS
	CMP	#7
	BNE	NOKPOT
	LDY	#11
	LDA	#0
KILLP:
	STA	PLAY1+8,Y
	STA	PLAY1+33,Y
	DEY
	BPL	KILLP
OUTKP:
	RTS
NOKPOT:	
	LDA	SPSHOW
	BPL	NOKSPH
	LDA	SHADHT
	CMP	#10
	BCC	NOKSPH
	CMP	#15
	BCS	NOKSPH
	LDA	VM0
	CMP	#90
	BCC	NOKSPH
	LDA	HPM0
	CMP	#65
	BCC	NOKSPH
	CMP	#70
	BCS	NOKSPH
	LDA	SPANIM
	CMP	#9
	BEQ	NOKSPH
KILLSP:
	LDA	#160
	STA	TTIMER
	LDA	#166
	STA	VM0
	LDA	#9
	STA	HPM0
	STA	SPANIM
	LDA	#SPHXBM0	;SPHINX KILL TUNE
	STA	TUNT0
	LDA	#SPHXBM1
	STA	TUNT1
SPHPTS:
	LDX	PLAYER
	LDA	WAVE,X
	CLC
	ADC	#5
;	ADC	#20
	STA	S3
	CMP	#10
	BCC	NOKSPH
	LDA	#10
	STA	S3
NOKSPH:	
	RTS		
KILLMI2:
	LDX	#10
KILLMI1:
	STX	NEWLAK
	LDA	#MINIBM		;MINI HIT TUNE
	STA	TUNT1
	STY	S3
	LDA	#0
	STA	FRMCNT
OUTKILLMII:
	RTS
;--------------------
;	BANK2 TABLES
;----------------------
;WAVECL:	DC.B	LTGRN,$C3,$07,$00,$37,$8B,$47,$05
VALS2:	DC.B	1,1,5,2,3,2,1,1
VALS1:	DC.B	0,5,0,0,0,0,0,0
SPEEDUPT:
	DC.B	3,0,1,3
SLOWDWNT:
	DC.B	1,2,2,0
OFFSED:
	DC.B	$F0,$E0,$10,$20
ANIMAT2:
	DC.B	0,17,0,0,0,0,0,0,17,0
CRUISET:
	DC.B	1,3,2,7
SLOWSPD:
	DC.B	$FF,$7F,$3F,$1F
;MAXFRM:
;	DC.B	$90,$80,$50
GAMESPD:
	DC.B	$07,$03,$01,$01
GAMESPD2:
	DC.B	$1F,$0F,$07,$01
;VALJEW:	DC.B	80,80,80,80,80
COLLITBL:
	DC.B	1,2,4,7,8,9,10
VALJEW:	DC.B	1,0,5,2,3,2,0,1,0
DISPAR:	DC.B	0,1,1,1,1,0,0,0,1,1,1,0
BUMPT:	DC.B	0,1,3,0
WAVETT:	DC.B	8,11,15,23,28,33,37,40,40,40,40
;WAVETT:	DC.B	4,4,4,4,4,4,4,4,4,4,4,4,4,4,4
OFFSE2:	DC.B	$01,$10
OFFSE3:	DC.B	$0F,$F0
OFFSE6:	DC.B	$F0,$0F
OFFSET4:
	DC.B	$04,$40
ALIENDS:DC.B	GLDEXPL,UWEEXP,HMISS3,GLDEXPL,SCBEXPL,GLDEXPL,0,GLDEXPL,UWEEXP
;
NOPOTT:
	DC.B	0,1,0,1,1,1,1,1
NOMINIT:
	DC.B	0,0,1,0,1,1,1,1
;
PGHASH1:
	  DC.B      0,$E9,$23,$A8,$53,$FD,$CE,$9F,$69

PGHASH2:
	  DC.B      0,$8D,$9A,$C6,$08,$4B,$B3,$1C,$AA

PGHASH3:
	  DC.B      0,$38,$EC,$A1,$7B,$55,$B9,$98,$43
POWERT:
	DC.B	$01,PTT,50,$00,$00,$00,$00,PTT,$00,$00,$00,$00,PTM,PTM
;		    XLF PTS  AB  KM  KP  KS  EB  KU  WP  AF DEC  SH PLY
	DC.B	PTM,$00,PTM,$00,PTM,PTT,$00,$00,$00,1,1,1
;		 BW  HS HOV  SU  SD ROU OMN  QS INV				
;
PGDIV:
	  DC.B      249                    ;SLOWDOWN
          DC.B      235                    ;POINTS
          DC.B      229                    ;KILL EVERYTHING
          DC.B      225                    ;INVINCIBILITY
          DC.B      220                    ;POINTS
          DC.B      212                    ;KILL UWES
          DC.B      204                    ;SPEED UP
          DC.B      197                    ;POINTS
          DC.B      195                    ;EXTRA LIFE
          DC.B      185                    ;AIR FREEZE
          DC.B      173                    ;POINTS
          DC.B      160                    ;SUPERSONIC SHOTS
          DC.B      154                    ;HOVER
          DC.B      133                    ;POINTS
          DC.B      126                    ;EXTRA BONUS
          DC.B      119                    ;RANDOM HIEROS
          DC.B      98                     ;POINTS
          DC.B      96                     ;KILL SPHINX
          DC.B      91                     ;CAN'T HOP
          DC.B      87                     ;POINTS
          DC.B      84                     ;BROKEN WING
          DC.B      76                     ;KILL MINIS
          DC.B      71                     ;POINTS
          DC.B      58                     ;KILL POTS
          DC.B      50                     ;POINTS
          DC.B      46                     ;CAN'T SWIM
          DC.B      40                     ;PARALIZE SPHINX
          DC.B      35                     ;POINTS
          DC.B      31                     ;DECOY
          DC.B      23                     ;AIR BOMB
          DC.B      8                      ;POINTS
          DC.B      0                      ;WARP
;
PGID:
	  DC.B      18                     ;SLOWDOWN
          DC.B      2                      ;POINTS
          DC.B      20                     ;KILL EVERYTHING
          DC.B      22                     ;INVINCIBILITY
          DC.B      2                      ;POINTS
          DC.B      8                      ;KILL UWES
          DC.B      17                     ;SPEED UP
          DC.B      2                      ;POINTS
          DC.B      1                      ;EXTRA LIFE
          DC.B      10                     ;AIR FREEZE
          DC.B      2                      ;POINTS
          DC.B      21                     ;SUPERSONIC SHOTS
          DC.B      16                     ;HOVER
          DC.B      2                      ;POINTS
          DC.B      7                      ;EXTRA BONUS
          DC.B      19                     ;RANDOM HIEROS
          DC.B      2                      ;POINTS
          DC.B      6                      ;KILL SPHINX
          DC.B      12                     ;CAN'T HOP
          DC.B      2                      ;POINTS
          DC.B      14                     ;BROKEN WING
          DC.B      4                      ;KILL MINIS
          DC.B      2                      ;POINTS
          DC.B      5                      ;KILL POTS
          DC.B      2                      ;POINTS
          DC.B      13                     ;CAN'T SWIM
          DC.B      15                     ;PARALIZE SPHINX
          DC.B      2                      ;POINTS
          DC.B      11                     ;DECOY
          DC.B      3                      ;AIR BOMB
          DC.B      2                      ;POINTS
          DC.B      9                      ;WARP
ENDSP:	
	DC.B	0,1,1,1,0,0,1,1,0,0,1,1,0,0,1,1,1,0,0,0,1,1,1
;
HORTAB:	DC.B	$FF,$FF,$FF,$01,$01,$01,$00,$00,$00
VERTAB:	DC.B	$FF,$00,$01,$01,$FF,$00,$01,$FF,$00
RUNTBL:	DC.B	$A0,$B0,$90,$50,$60,$70,$D0,$E0,$F0
;
	.ORG	BANK2+$FD8
SWBG0:
	LDA	$FFF6
	JMP	BEGIN0
;
	LDA	$FFF9
	JMP	(JMPPTR)
;
	LDA	$FFF8
	JMP	(JMPPTR)
;
	LDA	$FFF7
	JMP	(JMPPTR)
;
EXIT2:
	LDA	$FFF6
	JMP	(JMPPTR)
;
	DC.B	0,0,0,0
	.DC.W	SWBG0
	.DC.W	SWBG0
	.DC.W	SWBG0
;
;--------------------
	.ORG	BANK3+$100
BANK3C:
	JSR	SETKER
	JSR	GAMERC
	JSR	EASTER
;
	LDA	RSTFLG
	CMP	#1
	BEQ	OUTB3
	JSR	SETPOTS
	JSR	BONUSC2
	JSR	SOUNDC
	JSR	TUNER
	JSR	TRIGC
	JSR	SURFSOUND
	JSR	SPHALN
;
	LDA	SPSHOW
	BMI	OUTB3
	JSR	DETLAKE
OUTB3:
	LDA	#OUTBK4&$FF
	STA	JMPPTR
	LDA	#OUTBK4&$FF00/256
	STA	JMPPTR+1
	JMP	EXIT4
;------------------------------
TRIGC:
	LDA	PWRTMR
	BMI	OUTTRG
	LDA	#ALADIN0
	CMP	TUNNUM+1
	BNE	NOALADIN
	LDA	STARTG
	BNE	NOALADIN
	LDA	FRMCNT
	CMP	#$98
	BCC	OUTTRG
NOALADIN:
	LDA	SHADHT
	BEQ	OUTTRG
	LDA	#ENDGAM0
	CMP	TUNT0+SARAH
	BEQ	OUTTRG
	CMP	TUNNUM
	BEQ	OUTTRG
	CMP	TUNNUM+1
	BEQ	OUTTRG
	LDX	PLAYER
	BNE	CTRIG1
	LDA	INPT4
	BMI	OUTTRG
	LDA	TRIG
	AND	#$0F
	BEQ	NOALT0
	LDA	TRIG
	ORA	#$03
	STA	TRIG
	RTS
NOALT0:
	LDA	TRIG
	ORA	#$01
	STA	TRIG
	RTS
CTRIG1:
	LDA	INPT5
	BMI	OUTTRG
	LDA	TRIG
	AND	#$F0
	BEQ	NOALT1
	LDA	TRIG
	ORA	#$30
	STA	TRIG
	RTS
NOALT1:
	LDA	TRIG
	ORA	#$10
	STA	TRIG
	RTS
OUTTRG:
	LDA	#0
	STA	TRIG
	RTS
;-----------------------------
SETPOTS:
	LDA	S1
	CLC
	ADC	S2
	BNE	OUTLAKE2
	LDY	NEWLAK
	CPY	#2
	BEQ	DOPOTS	
	CPY	#20
	BEQ	NOTHING
	RTS
DOPOTS:
	LDY	#12
	LDA	FRMCNT
	AND	#$03
	BEQ	OUTLAKE2
	AND	#$01
	BNE	NOCHNG2
ANIMLP2:
	LDA	POTS0,Y
	STA	PLAY1+8,Y
	STA	PLAY1+33,Y
	DEY
	BPL	ANIMLP2
OUTLAKE2:
	RTS
NOCHNG2:
ANIMLP3:
	LDA	POTS1,Y
	STA	PLAY1+8,Y
	STA	PLAY1+33,Y
	DEY
	BPL	ANIMLP3
	RTS
NOTHING:
	LDY	#45
	LDA	#0
NOTHINGLP:
	STA	PLAY1,Y
	DEY
	BPL	NOTHINGLP
	RTS
;---------------------------------
SPHALN:
	LDA	#3
	STA	HPBL
	STA	ENABL
	LDA	SPSHOW
	BEQ	OUTSPA2
	LDA	SPANIM
	CMP	#9
	BEQ	NOANSP
	LDA	FRMCNT
	AND	#$0F
	BNE	NOANSP
	INC	SPANIM
	INC	SPANIM
	INC	SPANIM
	LDA	SPANIM
	CMP	#10
	BNE	NOANSP
	LDA	#4
	STA	SPANIM
NOANSP:
	LDA	SPSHOW
	BPL	OUTSPA2
;
	LDA	HPM0
	STA	HPBL
	LDA	#0
	STA	ENABL
	LDA	ALIEN
	CMP	#10
	BNE	OUTSPA2
	LDA	ALMASK
	BEQ	OUTSPA2
	LDA	VPOSP1
	CMP	#81
	BNE	OUTSPA2
	LDA	#$F0
	STA	ALIENT+20
	STA	ALIENT+21
	LDA	MISHOT+SARAH
	AND	#$EF
	STA	MISHOT
OUTSPA2:
	RTS
;----------------------
BONUSC2:
	LDA	BONUSR
	BEQ	OUTBON2
	LDA	#0
	STA	ALMASK
	STA	HPM0
	STA	HPENM
;
	LDA	FRMCNT
	AND	#$3F
	BNE	OUTBON2
	SED
	LDA	BONTIM
	SEC
	SBC	#1
	CLD
	STA	BONTIM
	CMP	#1
	BNE	OUTBON2
	LDA	#FLASHT0	;FLASHING TUNE
	STA	TUNT0
	LDA	#FLASHT1
	STA	TUNT1
OUTBON2:
	RTS
;----------------------------
SETKER:
	LDX	#0
	STX	PLAY0
	STX	PLAY0+5
	STX	PLAY0+6
	STX	PLAY0+7
	STX	PLAY0+8
	STX	PLAY0+9
	STX	PLAY0+10
	STX	PLAY0+11
	STX	PLAY0+12
	STX	PLAY0+13
	STX	PLAY0+14
	STX	PLAY0+15
	STX	PLAY0+16
	STX	PLAY0+17
	STX	PLAY0+18
	STX	PLAY0+19
	STX	PLAY0+20
	STX	PLAY0+21
	STX	PLAY0+22
	STX	PLAY0+23
	STX	PLAY0+24
	STX	PLAY0+25
	STX	PLAY0+26
	STX	PLAY0+27
	STX	PLAY0+28
	STX	PLAY0+29
	STX	PLAY0+30
	STX	PLAY0+31
	STX	PLAY0+32
	LDA	#$25
	STA	CTRLPF
	LDA	#$F5
	STA	NUSIZ0
	LDY	PLAYER
	LDA	WAVE,Y
	AND	#$07
	TAY
	LDX	BULCOL,Y
	TXA
	TAY
	LDA	MISHOT+SARAH
	BPL	SMALLMIS
	AND	#$0F
	BEQ	MISCOLOR
	LDA	FRMCNT
	AND	#$01
	BNE	MISCOLOR
SMALLMIS:
	LDX	#GREEN
MISCOLOR:
	STX	COLUP0
	LDA	ALMASK
	BEQ	NOCOLALN
	LDX	ALIEN
	CPX	#10
	BNE	NODARTCOL
	TYA
	ORA	#$01
	STA	ALMASK
	RTS
NODARTCOL:
	LDA	COLORA,X
	STA	ALMASK
NOCOLALN:
	RTS
;------------------------------
EASTER:
	LDA	PWRTMR
	BEQ	NOEASTER
	CMP	#40
	BCS	NOEASTER
	LDA	SHADHT
	CMP	#1
	BNE	NOEASTER
	LDY	#6
INITLP:
	LDA	INITIALS,Y
	STA	PLAY0+20,Y
	DEY
	BPL	INITLP
	RTS
NOEASTER:
	RTS
;-------------------------
GAMERC:	LDA     ATT		;NO TRIGGER CHECK IN ATT	
	BEQ     NOTRGCK 
	LDA	TRIG
	BNE	DOINIT
NOTRGCK:
	LDA     SWCHB		;GAME RESET		
	ROR      
	BCS     SELECT 
DOINIT:
	LDA	RSTFLG
	CMP	#2
	BEQ	ENDGMC
	LDX	#0
	STX	ATT
	INC	RSTFLG
	JSR	SCRAPALL
	LDA	#ALADIN0	;ALADDIN TUNE
	JSR	TUNIN
	LDA	#ALADIN1
	JSR	TUNIN
	RTS
SELECT:
	LDX	#0
	STX	RSTFLG
	ROR      
	BCC     STIME 
	RTS
STIME:
	LDA	ATT
	BEQ	SETTIMER
	LDA	TTIMER
	BMI	SETTIMER	
	CMP	#95
	BCC	INCGME
	CMP	#101
	BCC	ENDGMC
INCGME:
	INC	GAME
	LDA	GAME
	AND	#$0F
	TAX
	CPX	#4
	BNE	SETTIMER
;
	LDA	GAME
	AND	#$F0
	EOR	#$80
	STA	GAME
SETTIMER:
	LDA	#100
	STA	TTIMER
	JSR	SCRAPALL
	LDA	#$FF
	STA	STARTG
	STA	HORDIR
	STA	ATT
ENDGMC:	RTS      
;----------------------------
SURFSOUND:
	LDA	STARTG
	BNE	NOHOPSND
	LDA	HORDIR
	BNE	DOHOPSND
	LDA	VERDIR
	BEQ	NOHOPSND
DOHOPSND:
	LDA	SHADHT
	CMP	#1
	BEQ	DOSWIMSND
	LDA	#RUNNING2
	JSR	SNDCOMP
	BEQ	OUTSURF
	STA	TUNT0
	LDA	#SWIM2
	JMP	ENDCOMP
DOSWIMSND:
	LDA	#SWIM2
	JSR	SNDCOMP
	BEQ	OUTSURF
	STA	TUNT0
	LDA	#RUNNING2
	JSR	ENDCOMP
NOHOPSND:
	LDA	#SWIM2
	JSR	ENDCOMP
	LDA	#RUNNING2
ENDCOMP:
	CMP	TUNNUM
	BEQ	ENDTUN0
	CMP	TUNNUM+1
	BNE	OUTSURF
ENDTUN1:
	LDX	#$FF
	STX	TUNNUM+1
	INX
	STX	AUDV1
OUTSURF:
	RTS
ENDTUN0:
	LDX	#$FF
	STX	TUNNUM
	INX
	STX	AUDV0
	RTS
SNDCOMP:
	CMP	TUNNUM
	BEQ	OUTCOMP
	CMP	TUNNUM+1
OUTCOMP:
	RTS
;-------------
DETLAKE:
	LDA	NEWLAK
	CMP	#$80
	BNE	ENDGMC
DETLK:
	LDA	BONUSR
	BEQ	NOBONUSRD
	LDA	#$86
	STA	NEWLAK
	BNE	DOMASKS
NOBONUSRD:
	LDX	PLAYER
	LDA	WAVE,X
	AND	#$07
	STA	WORK
	LDA	GAME
	AND	#$0F
	CLC
	ADC	WORK
	TAY
	LDA	WAVESL,Y
	STA	WORK
	LDA	WAVESH
	STA	WORK+1
	LDY	SCREEN,X
	LDA	NEWLAK
	ORA	(WORK),Y
	STA	NEWLAK
	CMP	#$85
	BNE	CHKDETLAKE
	LDA	RAND		;CHECK FOR JEWELS
	AND	#$03
	BNE	CHKDETLAKE
	LDX	PLAYER
	LDA	NJEWL
	AND	OFFSE9,X
	CMP	OFFSE93,X
	BCS	DOMASKS
	INC	NEWLAK
	JMP	DOMASKS
CHKDETLAKE:
	LDY	PLAYER
	LDA	WAVE,Y
	AND	#$07
	TAX
	LDA	NEWLAK
	CMP	WAVE0,X
	BEQ	DOPUTIN
	CMP	WAVE1,X
	BNE	DOMASKS
DOPUTIN:
	LDA	PUTIN,X
	STA	NEWLAK
DOMASKS:
	LDA	NEWLAK
	AND	#$0F
	TAY
	LDA	HP1
	CMP	#RSTHP1
	BEQ	NOLAKEMASK
	LDA	MASKT,Y
	STA	LKMASK
NOLAKEMASK:
	LDA	LAKENZT,Y
	STA	LAKENZ
	CPY	#0
	BEQ	FILLLAKE
	LDA	COLLAKET,Y
	STA	COLLAK
;
	LDA	#0
	STA	PLAY1+14
	STA	PLAY1+15
	STA	PLAY1+16
	STA	PLAY1+17
	STA	PLAY1+18
	STA	PLAY1+19
	STA	PLAY1+20
	STA	PLAY1+21
	STA	PLAY1+22
	STA	PLAY1+23
	STA	PLAY1+24
	STA	PLAY1+25
	STA	PLAY1+26
	STA	PLAY1+27
	STA	PLAY1+28
	STA	PLAY1+29
	STA	PLAY1+30
	STA	PLAY1+31
	STA	PLAY1+32
	STA	PLAY1+33
;
	CPY	#5
	BCS	OUTDETLAKE
	CPY	#2
	BNE	NOPOTBOT
	TYA
	TAX
	LDY	#7
	BNE	DOMINIS
NOPOTBOT:
	CPY	#4
	BNE	NOMINI
MINIFILL:
	LDX	PLAYER
	LDA	WAVE,X
	AND	#$07
	TAX
	LDA	SPCOLOR,X
	STA	COLLAK	
NOMINI:
	TYA
	TAX
	LDY	#21
DOMINIS:
	LDA	G1L,X
	STA	WORK
	LDA	G1H
	STA	WORK+1
MINILOOP:
	LDA	(WORK),Y
	STA	PLAY1,Y
	STA	PLAY1+25,Y
	DEY
	BPL	MINILOOP
OUTDETLAKE:
	RTS
;
FILLLAKE:
	LDY	PLAYER
	LDA	WAVE,Y
	AND	#$07
	TAX
	LDA	LAKECLT,X
	STA	COLLAK
;
	LDX	#45
LAKELOOP:
	LDA	LAKE0,X
	STA	PLAY1,X
	DEX
	BPL	LAKELOOP
	RTS
;----------------------
SOUNDC:
	LDA	SHADHT
	BEQ	NOSFINKS
	LDA	SPANIM
	CMP	#9
	BEQ	NOSFINKS
	LDA	SHADHT
	BNE	NOSCRH
NOSFINKS:
	LDA	#SFINKS
	JSR	SCRAPONE
NOSCRH:
	LDA	TUNT0+SARAH
	BPL	NOBACKG
	LDA	#$FF
	CMP	TUNNUM
	BNE	NOBACKG
	CMP	TUNNUM1
	BNE	NOBACKG
	LDA	#BACKGR0
	STA	TUNT0
	LDA	#BACKGR1
	STA	TUNT1
NOBACKG:
	LDA	ATT
	BEQ	NOSTPS
	LDA	#ENDGAM0
	CMP	TUNT0+SARAH
	BEQ	NOSTPS
	CMP	TUNNUM
	BEQ	NOSTPS
	CMP	TUNNUM+1
	BEQ	NOSTPS
	JMP	SCRAPALL
NOSTPS:
	LDA	TUNT0+SARAH
	BMI	NOTUN0
	JSR	TUNIN
	LDA	#$FF
	STA	TUNT0
NOTUN0:
	LDA	TUNT1+SARAH
	BMI	NOTUN1
DOTUNIN:
	CMP	#SPTUNE
	BNE	NOSPTUNE
	LDX	#POWRUSE
	CPX	TUNT0+SARAH
	BEQ	NOTUN1
	CPX	TUNNUM
	BEQ	NOTUN1
	CPX	TUNNUM+1
	BEQ	NOTUN1
	LDX	#MINIBM
	CPX	TUNNUM
	BEQ	NOTUN1
	CPX	TUNNUM+1
	BEQ	NOTUN1
NOSPTUNE:
	CMP	#MINIBM
	BNE	DOMINIBM
	LDX	FRMCNT
	CPX	#2
	BNE	NOTUN1
DOMINIBM:
	JSR	TUNIN
	LDA	#$FF
	STA	TUNT1
NOTUN1:
OUTSND:
	RTS	
;
;	BANK3 TABLES
G1L:
	DC.B	0,SMPY&$FF
	DC.B	POTBOT&$FF,0,LILSPH&$FF
;
G1H:
	DC.B	SMPY&$FF00/256
COLLAKET:
	DC.B	$88,$FF,$34,$18,$86,$0B,PINK,$88
;		BLUE,WHITE,MGREY,MGREY,MGREY,LTGREY,PINK
;		LAKE SMPY  POTS0POTS1 SPH HIEROS JEWL
LAKECLT:
	DC.B	$85,$40,$C3,$43,$82,$42,$33,$06	
;
SPCOLOR:
	DC.B	$21,$00,$41,$C2,$02,$82,$03,$C2
LAKE0:
	DC.B	$00,$30,$30,$30,$38,$38,$78,$78
	DC.B	$FC,$FC,$FC,$FC,$FE,$FE,$FE,$FE
	DC.B	$FE,$FE,$FE,$FE,$FF,$FF,$FF,$FF
	DC.B	$FF,$FF,$7F,$7F,$7F,$7F,$7F,$7F
	DC.B	$3F,$3F,$3F,$3F,$3F,$3F,$3E,$1E
	DC.B	$1E,$1E,$1E,$1C,$1C,0,0
;
BULCOL:	DC.B	0,0,0,$17,0,0,0,0
;
COLORA:	DC.B	RED,$44,DKRED,YELLOW,LTBRN,RED,LTGRN,RED,$44,LTGRN,WHITE
;		GLIDER,UWE,FLEA,FISH,SCAROB,VULTURE
OFFSE9:
	DC.B	$0F,$F0
OFFSE93:
	DC.B	$03,$30
;
INITIALS:
	DC.B	$C8,$A8,$A8,$CC,$AA,$AA,$CC
POTS0:
	DC.B	$6E,$6E,$ED,$ED,$6D,$75,$76,$76,$66,$66,$24,$24,0
POTS1:
	DC.B	$6E,$6C,$76,$76,$F7,$76,$6E,$6E,$2C,$2C,$28,$00,0
;
	.ORG	BANK3+$600
;
WV0:
	DC.B	1,0,2,1,2,4,4,0
WV1:
	DC.B	1,5,1,2,5,0,5,2,2,1,2,5,5
WV2:
	DC.B	0,1,5,0,5,4,4,5,1,4,0,5,1,4,5
WV3:
	DC.B	1,5,2,4,5,2,0,1,5,5,4,5,2,0,5
WV4:	
	DC.B	5,1,0,5,2,5,5,1,5,0,1,0,5,4,1,5
WV5:
	DC.B	1,5,5,0,5,1,5,2,4,1,1,5,0,5,1
WV6:
	DC.B	4,2,1,2,5,2,1,5,2,5,0,4,5,4,1
WV7:
	DC.B	2,5,1,4,1,2,5,1,5,0,1,1,0,1
WV8:
	DC.B	4,5,2,1,5,5
	DC.B	5,2,0,5,2,5,2,1,0
WV9:
	DC.B	1,5,4,1,1,5,5,1,5,0,5
	DC.B	5,0,2,2,4
WV10:
	DC.B	1,5,5,1,2,5,2,2,4,1,0,5,5,1,1
	DC.B	4,5,0,1,1,5,4,2,4,5,2,1,4,5,5,2,4,1,1,5,1,0,2,4,1,0,0,1
;
LILSPH:
	DC.B	0,$06,$06,$04,$64,$76,$3B,$DF,$BF,$6F,$7F,$6F,$56,$20,$70,$70
	DC.B	$60,$00,0,0,0,0
SMPY:
	DC.B	0
	DC.B	$18,$34,$7A,$F5,$FA,$F5,$FA,$F5,$7A,$74,$7A,$74,$38,$34,$38,$30
	DC.B	$18,$10,0,0
POTBOT:
	DC.B	0,$FF,$FF,$7E,$7E,$7E,$FF,0
WAVESL:
	DC.B	WV0&$FF,WV1&$FF,WV2&$FF,WV3&$FF,WV4&$FF,WV5&$FF,WV6&$FF,WV7&$FF
	DC.B	WV8&$FF,WV9&$FF,WV10&$FF
WAVESH:
	DC.B	WV0&$FF00/256
;
MASKT:
	DC.B	$80,$E0,$FF,$FF,$F0,$FF,$FF
LAKENZT:
	DC.B	$0F,$05,$0,0,$05,$0,0
PUTIN:
	DC.B	$85,$82,$84,$82,$80,$80,$80,$80
WAVE0:
	DC.B	$82,$84,$82,$84,$80,$80,$80,$80
WAVE1:
	DC.B	$84,$82,$84,$82,$80,$80,$80,$80
;
	.ORG	BANK3+$750
	.INCLUDE STUNES
	.ORG	BANK3+$FD8
SWBG03:
	LDA	$FFF6
	JMP	BEGIN0
;
	LDA	$FFF9
	JMP	(JMPPTR)
;
	LDA	$FFF8
	JMP	(JMPPTR)
;
	LDA	$FFF7
	JMP	(JMPPTR)
;
EXIT4:
	LDA	$FFF6
	JMP	(JMPPTR)
;
	DC.B	0,0,0,0
	.DC.W	SWBG03
	.DC.W	SWBG03
	.DC.W	SWBG03
	.END
